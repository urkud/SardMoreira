import Mathlib
import SardMoreira.ContDiff
import SardMoreira.ContinuousMultilinearMap

open scoped unitInterval Topology NNReal
open Asymptotics Filter Set

namespace Asymptotics

/-- If `a ‚â§ b`, then `x^b = O(x^a)` as `x ‚Üí 0`, `x ‚â• 0`, unless `b = 0` and `a ‚â† 0`. -/
theorem IsBigO.rpow_rpow_nhdsGE_zero_of_le {a b : ‚Ñù} (h : a ‚â§ b) (himp : b = 0 ‚Üí a = 0) :
    (¬∑ ^ b : ‚Ñù ‚Üí ‚Ñù) =O[ùìù[‚â•] 0] (¬∑ ^ a) :=
  .of_bound' <| mem_of_superset (Icc_mem_nhdsGE one_pos) fun x hx ‚Ü¶ by
    simpa [Real.abs_rpow_of_nonneg hx.1, abs_of_nonneg hx.1]
     using Real.rpow_le_rpow_of_exponent_ge_of_imp hx.1 hx.2 h fun _ ‚Ü¶ himp

theorem IsBigO.id_rpow_of_le_one {a : ‚Ñù} (ha : a ‚â§ 1) :
    (id : ‚Ñù ‚Üí ‚Ñù) =O[ùìù[‚â•] 0] (¬∑ ^ a) := by
  simpa using rpow_rpow_nhdsGE_zero_of_le ha (by simp)

end Asymptotics

variable {E F G : Type*}
  [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
  [NormedAddCommGroup F] [NormedSpace ‚Ñù F]
  [NormedAddCommGroup G] [NormedSpace ‚Ñù G]

@[mk_iff]
structure ContDiffMoreiraHolderAt (k : ‚Ñï) (Œ± : I) (f : E ‚Üí F) (a : E) : Prop where
  contDiffAt : ContDiffAt ‚Ñù k f a
  isBigO : (iteratedFDeriv ‚Ñù k f ¬∑ - iteratedFDeriv ‚Ñù k f a) =O[ùìù a] (‚Äñ¬∑ - a‚Äñ ^ (Œ± : ‚Ñù))

theorem ContDiffAt.contDiffMoreiraHolderAt {n : WithTop ‚Ñï‚àû} {k : ‚Ñï} {f : E ‚Üí F} {a : E}
    (h : ContDiffAt ‚Ñù n f a) (hk : k < n) (Œ± : I) : ContDiffMoreiraHolderAt k Œ± f a where
  contDiffAt := h.of_le hk.le
  isBigO := calc
    (iteratedFDeriv ‚Ñù k f ¬∑ - iteratedFDeriv ‚Ñù k f a) =O[ùìù a] (¬∑ - a) :=
      (h.differentiableAt_iteratedFDeriv hk).isBigO_sub
    _ =O[ùìù a] (‚Äñ¬∑ - a‚Äñ ^ (Œ± : ‚Ñù)) :=
      .of_norm_left <| .comp_tendsto (.id_rpow_of_le_one Œ±.2.2) <| tendsto_norm_sub_self_nhdsGE a

namespace ContDiffMoreiraHolderAt

theorem continuousAt {k : ‚Ñï} {Œ± : I} {f : E ‚Üí F} {a : E} (h : ContDiffMoreiraHolderAt k Œ± f a) :
    ContinuousAt f a :=
  h.contDiffAt.continuousAt

theorem differentiableAt {k : ‚Ñï} {Œ± : I} {f : E ‚Üí F} {a : E} (h : ContDiffMoreiraHolderAt k Œ± f a)
    (hk : k ‚â† 0) : DifferentiableAt ‚Ñù f a :=
  h.contDiffAt.differentiableAt <| mod_cast hk

@[simp]
theorem zero_exponent_iff {k : ‚Ñï} {f : E ‚Üí F} {a : E} :
    ContDiffMoreiraHolderAt k 0 f a ‚Üî ContDiffAt ‚Ñù k f a := by
  refine ‚ü®contDiffAt, fun h ‚Ü¶ ‚ü®h, ?_‚ü©‚ü©
  simpa using ((h.continuousAt_iteratedFDeriv le_rfl).sub_const _).norm.isBoundedUnder_le

theorem zero_left_iff {Œ± : I} {f : E ‚Üí F} {a : E} :
    ContDiffMoreiraHolderAt 0 Œ± f a ‚Üî
      ContDiffAt ‚Ñù 0 f a ‚àß (f ¬∑ - f a) =O[ùìù a] (‚Äñ¬∑ - a‚Äñ ^ (Œ± : ‚Ñù)) := by
  simp only [contDiffMoreiraHolderAt_iff, Nat.cast_zero, and_congr_right_iff]
  intro hfc
  simp only [iteratedFDeriv_zero_eq_comp, Function.comp_def, ‚Üê map_sub]
  rw [‚Üê isBigO_norm_left]
  simp_rw [LinearIsometryEquiv.norm_map, isBigO_norm_left]

theorem of_exponent_le {k : ‚Ñï} {f : E ‚Üí F} {a : E} {Œ± Œ≤ : I}
    (hf : ContDiffMoreiraHolderAt k Œ± f a) (hle : Œ≤ ‚â§ Œ±) : ContDiffMoreiraHolderAt k Œ≤ f a where
  contDiffAt := hf.contDiffAt
  isBigO := hf.isBigO.trans <| by
    refine .comp_tendsto (.rpow_rpow_nhdsGE_zero_of_le hle fun hŒ± ‚Ü¶ ?_) ?_
    ¬∑ exact le_antisymm (le_trans (mod_cast hle) hŒ±.le) Œ≤.2.1
    ¬∑ exact tendsto_norm_sub_self_nhdsGE a

theorem of_lt {k l : ‚Ñï} {f : E ‚Üí F} {a : E} {Œ± Œ≤ : I} (hf : ContDiffMoreiraHolderAt k Œ± f a)
    (hlt : l < k) : ContDiffMoreiraHolderAt l Œ≤ f a :=
  hf.contDiffAt.contDiffMoreiraHolderAt (mod_cast hlt) _

theorem of_toLex_le {k l : ‚Ñï} {f : E ‚Üí F} {a : E} {Œ± Œ≤ : I} (hf : ContDiffMoreiraHolderAt k Œ± f a)
    (hle : toLex (l, Œ≤) ‚â§ toLex (k, Œ±)) : ContDiffMoreiraHolderAt l Œ≤ f a :=
  (Prod.Lex.le_iff.mp hle).elim hf.of_lt <| by rintro ‚ü®rfl, hle‚ü©; exact hf.of_exponent_le hle

theorem of_le {k l : ‚Ñï} {f : E ‚Üí F} {a : E} {Œ± : I} (hf : ContDiffMoreiraHolderAt k Œ± f a)
    (hl : l ‚â§ k) : ContDiffMoreiraHolderAt l Œ± f a :=
  hf.of_toLex_le <| Prod.Lex.toLex_mono ‚ü®hl, le_rfl‚ü©

theorem of_contDiffOn_holderWith {f : E ‚Üí F} {s : Set E} {k : ‚Ñï} {Œ± : I} {a : E} {C : ‚Ñù‚â•0}
    (hf : ContDiffOn ‚Ñù k f s) (hs : s ‚àà ùìù a)
    (hd : HolderOnWith C ‚ü®Œ±, Œ±.2.1‚ü© (iteratedFDeriv ‚Ñù k f) s) :
    ContDiffMoreiraHolderAt k Œ± f a where
  contDiffAt := hf.contDiffAt hs
  isBigO := .of_bound C <| mem_of_superset hs fun x hx ‚Ü¶ by
    simpa [Real.abs_rpow_of_nonneg, ‚Üê dist_eq_norm, dist_nonneg]
      using hd.dist_le hx (mem_of_mem_nhds hs)

theorem fst {k : ‚Ñï} {Œ± : I} {a : E √ó F} : ContDiffMoreiraHolderAt k Œ± Prod.fst a :=
  contDiffAt_fst.contDiffMoreiraHolderAt (WithTop.coe_lt_top _) Œ±

theorem snd {k : ‚Ñï} {Œ± : I} {a : E √ó F} : ContDiffMoreiraHolderAt k Œ± Prod.snd a :=
  contDiffAt_snd.contDiffMoreiraHolderAt (WithTop.coe_lt_top _) Œ±

theorem prodMk {k : ‚Ñï} {Œ± : I} {f : E ‚Üí F} {g : E ‚Üí G} {a : E}
    (hf : ContDiffMoreiraHolderAt k Œ± f a) (hg : ContDiffMoreiraHolderAt k Œ± g a) :
    ContDiffMoreiraHolderAt k Œ± (fun x ‚Ü¶ (f x, g x)) a where
  contDiffAt := hf.contDiffAt.prodMk hg.contDiffAt
  isBigO := calc
    _ =·∂†[ùìù a] (fun x ‚Ü¶ (iteratedFDeriv ‚Ñù k f x - iteratedFDeriv ‚Ñù k f a).prod
                (iteratedFDeriv ‚Ñù k g x - iteratedFDeriv ‚Ñù k g a)) := by
      filter_upwards [hf.contDiffAt.eventually (by simp),
        hg.contDiffAt.eventually (by simp)] with x hfx hgx
      apply DFunLike.ext
      rw [iteratedFDeriv_prodMk _ _ le_rfl, iteratedFDeriv_prodMk _ _ le_rfl] <;>
        simp [hfx, hgx, hf.contDiffAt, hg.contDiffAt]
    _ =O[ùìù a] fun x ‚Ü¶ ‚Äñx - a‚Äñ ^ (Œ± : ‚Ñù) := by
      refine .of_norm_left ?_
      simp only [ContinuousMultilinearMap.opNorm_prod, ‚Üê Prod.norm_mk]
      exact (hf.isBigO.prod_left hg.isBigO).norm_left

theorem comp' {g : F ‚Üí G} {f : E ‚Üí F} {a : E} {k : ‚Ñï} {Œ± : I}
    (hg : ContDiffMoreiraHolderAt k Œ± g (f a)) (hf : ContDiffMoreiraHolderAt k Œ± f a)
    (hd : DifferentiableAt ‚Ñù g (f a) ‚à® DifferentiableAt ‚Ñù f a) :
    ContDiffMoreiraHolderAt k Œ± (g ‚àò f) a where
  contDiffAt := hg.contDiffAt.comp a hf.contDiffAt
  isBigO := calc
    (iteratedFDeriv ‚Ñù k (g ‚àò f) ¬∑ - iteratedFDeriv ‚Ñù k (g ‚àò f) a)
      =·∂†[ùìù a] fun x ‚Ü¶ (ftaylorSeries ‚Ñù g (f x)).taylorComp (ftaylorSeries ‚Ñù f x) k -
        (ftaylorSeries ‚Ñù g (f a)).taylorComp (ftaylorSeries ‚Ñù f a) k := by
      filter_upwards [hf.contDiffAt.eventually (by simp),
        hf.continuousAt.eventually (hg.contDiffAt.eventually (by simp))] with x hfx hgx
      rw [iteratedFDeriv_comp hgx hfx le_rfl,
        iteratedFDeriv_comp hg.contDiffAt hf.contDiffAt le_rfl]
    _ =O[ùìù a] fun x ‚Ü¶ ‚Äñx - a‚Äñ ^ (Œ± : ‚Ñù) := by
      apply FormalMultilinearSeries.taylorComp_sub_taylorComp_isBigO
      ¬∑ intro i hi
        exact ((hg.contDiffAt.continuousAt_iteratedFDeriv (mod_cast hi)).comp hf.continuousAt)
          |>.norm.isBoundedUnder_le
      ¬∑ intro i hi
        by_cases hfd : DifferentiableAt ‚Ñù f a
        ¬∑ refine ((hg.of_le hi).isBigO.comp_tendsto hf.continuousAt).trans ?_
          refine .rpow Œ±.2.1 (.of_forall fun _ ‚Ü¶ norm_nonneg _) <| .norm_norm ?_
          exact hfd.isBigO_sub
        ¬∑ obtain rfl : k = 0 := by
            contrapose! hfd
            exact hf.differentiableAt hfd
          obtain rfl : i = 0 := by rwa [nonpos_iff_eq_zero] at hi
          refine .of_norm_left ?_
          simp only [ftaylorSeries, iteratedFDeriv_zero_eq_comp, Function.comp_apply, ‚Üê map_sub,
            LinearIsometryEquiv.norm_map, isBigO_norm_left]
          refine ((hd.resolve_right hfd).isBigO_sub.comp_tendsto hf.continuousAt).trans ?_
          refine .trans (.of_norm_right ?_) hf.isBigO
          simp [iteratedFDeriv_zero_eq_comp, ‚Üê map_sub, Function.comp_def, isBigO_refl]
      ¬∑ intro i hi
        exact (hf.contDiffAt.continuousAt_iteratedFDeriv (mod_cast hi)).norm.isBoundedUnder_le
      ¬∑ exact fun _ _ ‚Ü¶ isBoundedUnder_const
      ¬∑ exact fun i hi ‚Ü¶ (hf.of_le hi).isBigO

theorem comp {g : F ‚Üí G} {f : E ‚Üí F} {a : E} {k : ‚Ñï} {Œ± : I}
    (hg : ContDiffMoreiraHolderAt k Œ± g (f a)) (hf : ContDiffMoreiraHolderAt k Œ± f a)
    (hk : k ‚â† 0) : ContDiffMoreiraHolderAt k Œ± (g ‚àò f) a :=
  hg.comp' hf (.inl <| hg.differentiableAt hk)

theorem _root_.ContinuousLinearMap.contDiffMoreiraHolderAt
    (f : E ‚ÜíL[‚Ñù] F) {a : E} {k : ‚Ñï} {Œ± : I} :
    ContDiffMoreiraHolderAt k Œ± f a :=
  f.contDiff.contDiffAt.contDiffMoreiraHolderAt (WithTop.coe_lt_top _) _

theorem _root_.ContinuousLinearEquiv.contDiffMoreiraHolderAt
    (f : E ‚âÉL[‚Ñù] F) {a : E} {k : ‚Ñï} {Œ± : I} :
    ContDiffMoreiraHolderAt k Œ± f a :=
  f.toContinuousLinearMap.contDiffMoreiraHolderAt

theorem continuousLinearMap_comp {f : E ‚Üí F} {a : E} {k : ‚Ñï} {Œ± : I}
    (hf : ContDiffMoreiraHolderAt k Œ± f a) (g : F ‚ÜíL[‚Ñù] G) :
    ContDiffMoreiraHolderAt k Œ± (g ‚àò f) a :=
  g.contDiffMoreiraHolderAt.comp' hf <| .inl g.differentiableAt

@[simp]
theorem _root_.ContinuousLinearEquiv.contDiffMoreiraHolderAt_left_comp
    {f : E ‚Üí F} {a : E} {k : ‚Ñï} {Œ± : I} (g : F ‚âÉL[‚Ñù] G) :
    ContDiffMoreiraHolderAt k Œ± (g ‚àò f) a ‚Üî ContDiffMoreiraHolderAt k Œ± f a :=
  ‚ü®fun h ‚Ü¶ by simpa [Function.comp_def] using h.continuousLinearMap_comp (g.symm : G ‚ÜíL[‚Ñù] F),
    fun h ‚Ü¶ h.continuousLinearMap_comp (g : F ‚ÜíL[‚Ñù] G)‚ü©

@[simp]
theorem _root_.LinearIsometryEquiv.contDiffMoreiraHolderAt_left_comp
    {f : E ‚Üí F} {a : E} {k : ‚Ñï} {Œ± : I} (g : F ‚âÉ‚Çó·µ¢[‚Ñù] G) :
    ContDiffMoreiraHolderAt k Œ± (g ‚àò f) a ‚Üî ContDiffMoreiraHolderAt k Œ± f a :=
  g.toContinuousLinearEquiv.contDiffMoreiraHolderAt_left_comp

protected theorem id {k : ‚Ñï} {Œ± : I} {a : E} : ContDiffMoreiraHolderAt k Œ± id a :=
  ContinuousLinearMap.id ‚Ñù E |>.contDiffMoreiraHolderAt

protected theorem const {k : ‚Ñï} {Œ± : I} {a : E} {b : F} :
    ContDiffMoreiraHolderAt k Œ± (Function.const E b) a :=
  contDiffAt_const.contDiffMoreiraHolderAt (WithTop.coe_lt_top _) Œ±

protected theorem fderiv {f : E ‚Üí F} {a : E} {k l : ‚Ñï} {Œ± : I}
    (hf : ContDiffMoreiraHolderAt k Œ± f a) (hl : l + 1 ‚â§ k) :
    ContDiffMoreiraHolderAt l Œ± (fderiv ‚Ñù f) a where
  contDiffAt := hf.contDiffAt.fderiv_right (mod_cast hl)
  isBigO := .of_norm_left <| by
    simpa [iteratedFDeriv_succ_eq_comp_right, Function.comp_def, ‚Üê dist_eq_norm_sub]
      using hf.of_le hl |>.isBigO |>.norm_left

protected theorem iteratedFDeriv {f : E ‚Üí F} {a : E} {k l m : ‚Ñï} {Œ± : I}
    (hf : ContDiffMoreiraHolderAt k Œ± f a) (hl : l + m ‚â§ k) :
    ContDiffMoreiraHolderAt l Œ± (iteratedFDeriv ‚Ñù m f) a := by
  induction m generalizing l with
  | zero =>
    simpa +unfoldPartialApp [iteratedFDeriv_zero_eq_comp] using hf.of_le hl
  | succ m ihm =>
    rw [‚Üê add_assoc, add_right_comm] at hl
    -- TODO: why `simp` fails to apply the lemma? Does it fail to unify some instances?
    -- Does it happen on the latest Mathlib?
    simp +unfoldPartialApp [iteratedFDeriv_succ_eq_comp_left]
    convert (ihm hl).fderiv le_rfl using 0
    convert LinearIsometryEquiv.contDiffMoreiraHolderAt_left_comp _ <;> rfl

theorem congr_eventuallyEq {f g : E ‚Üí F} {a : E} {k : ‚Ñï} {Œ± : I}
    (hf : ContDiffMoreiraHolderAt k Œ± f a) (hfg : f =·∂†[ùìù a] g) :
    ContDiffMoreiraHolderAt k Œ± g a where
  contDiffAt := hf.contDiffAt.congr_of_eventuallyEq hfg.symm
  isBigO := by
    refine EventuallyEq.trans_isBigO (.sub ?_ ?_) hf.isBigO
    ¬∑ exact hfg.symm.iteratedFDeriv ‚Ñù _
    ¬∑ rw [hfg.symm.iteratedFDeriv ‚Ñù _ |>.self_of_nhds]

end ContDiffMoreiraHolderAt

theorem OpenPartialHomeomorph.contDiffMoreiraHolderAt_symm [CompleteSpace E] {k : ‚Ñï} {Œ± : I}
    (f : OpenPartialHomeomorph E F) {a : F} (ha : a ‚àà f.target)
    (hf' : (fderiv ‚Ñù f (f.symm a)).IsInvertible)
    (hf : ContDiffMoreiraHolderAt k Œ± f (f.symm a)) :
    ContDiffMoreiraHolderAt k Œ± f.symm a where
  contDiffAt := contDiffAt_symm' f ha hf' hf.contDiffAt
  isBigO := by
    have hrpow : (‚Äñ¬∑ - a‚Äñ) =O[ùìù a] (‚Äñ¬∑ - a‚Äñ ^ (Œ± : ‚Ñù)) :=
      (IsBigO.id_rpow_of_le_one Œ±.2.2).comp_tendsto <| tendsto_norm_sub_self_nhdsGE _
    rcases eq_or_ne k 0 with rfl | hk‚ÇÄ
    ¬∑ calc
        _ =O[ùìù a] fun x ‚Ü¶ f.symm x - f.symm a := by
          refine .of_norm_left ?_
          simp [iteratedFDeriv_zero_eq_comp, ‚Üê map_sub, isBigO_refl]
        _ =O[ùìù a] fun x ‚Ü¶ ‚Äñf (f.symm x) - f (f.symm a)‚Äñ := by
          simpa using hf'.hasFDerivAt.isBigO_sub_rev hf'.choose.antilipschitz |>.comp_tendsto <|
            f.continuousAt_symm ha
        _ =·∂†[ùìù a] fun x ‚Ü¶ ‚Äñx - a‚Äñ := by
          filter_upwards [f.eventually_right_inverse ha] with x hx
          simp [hx, ha]
        _ =O[ùìù a] fun x ‚Ü¶ ‚Äñx - a‚Äñ ^ (Œ± : ‚Ñù) := hrpow
    ¬∑ have hinv : ‚àÄ·∂† x in ùìù (f.symm a), (fderiv ‚Ñù f x).IsInvertible :=
        (hf.contDiffAt.continuousAt_fderiv <| mod_cast hk‚ÇÄ).eventually <|
           ContinuousLinearEquiv.isOpen.mem_nhds hf'
      have hinv' : ‚àÄ·∂† x in ùìù a, (fderiv ‚Ñù f (f.symm x)).IsInvertible :=
        f.continuousAt_symm ha |>.eventually hinv
      have hfderiv_isBigO :
          (fun x ‚Ü¶ fderiv ‚Ñù f.symm x - fderiv ‚Ñù f.symm a) =O[ùìù a]
            fun x ‚Ü¶ fderiv ‚Ñù f (f.symm x) - fderiv ‚Ñù f (f.symm a) := by
        refine EventuallyEq.trans_isBigO ?_
          (ContinuousLinearMap.isBigO_inverse_sub_inverse hinv' ?_ ?_ ?_)
        ¬∑ filter_upwards [f.continuousAt_symm ha hinv, f.open_target.mem_nhds ha] with x hfx hx
          rw [f.fderiv_symm hx hfx, f.fderiv_symm ha hf']
        ¬∑ refine f.contDiffAt_symm' ha hf' hf.contDiffAt |>.continuousAt_fderiv (mod_cast hk‚ÇÄ)
            |>.norm |>.isBoundedUnder_le |>.mono_le ?_
          filter_upwards [hinv', f.open_target.mem_nhds ha] with x hfx hx
          simp [f.fderiv_symm hx hfx]
        ¬∑ simp [hinv.self_of_nhds]
        ¬∑ apply isBoundedUnder_const
      have hsymm_isBigO : (f.symm ¬∑ - f.symm a) =O[ùìù a] (¬∑ - a) := by
        simpa using f.hasFDerivAt_symm ha hf'.hasFDerivAt |>.isBigO_sub
      have hsymm_rpow_isBigO : (‚Äñf.symm ¬∑ - f.symm a‚Äñ ^ (Œ± : ‚Ñù)) =O[ùìù a] (‚Äñ¬∑ - a‚Äñ ^ (Œ± : ‚Ñù)) :=
        hsymm_isBigO.norm_norm.rpow Œ±.2.1 (by simp [EventuallyLE])
      obtain rfl | hk‚ÇÅ : k = 1 ‚à® 1 < k := by grind
      ¬∑ calc
          _ =O[ùìù a] fun x ‚Ü¶ fderiv ‚Ñù f.symm x - fderiv ‚Ñù f.symm a :=
            .of_norm_left <| by simp [iteratedFDeriv_one_eq, ‚Üê map_sub, isBigO_refl]
          _ =O[ùìù a] fun x ‚Ü¶ fderiv ‚Ñù f (f.symm x) - fderiv ‚Ñù f (f.symm a) := hfderiv_isBigO
          _ =O[ùìù a] fun x ‚Ü¶ ‚Äñf.symm x - f.symm a‚Äñ ^ (Œ± : ‚Ñù) := by
            simpa [iteratedFDeriv_one_eq, ‚Üê map_sub]
              using hf.isBigO.comp_tendsto (f.continuousAt_symm ha) |>.norm_left
          _ =O[ùìù a] fun x ‚Ü¶ ‚Äñx - a‚Äñ ^ (Œ± : ‚Ñù) := hsymm_rpow_isBigO
      ¬∑ calc
          (fun x ‚Ü¶ iteratedFDeriv ‚Ñù k f.symm x - iteratedFDeriv ‚Ñù k f.symm a)
            =·∂†[ùìù a] fun x ‚Ü¶
              (FormalMultilinearSeries.id ‚Ñù E (f.symm x) k -
                ‚àë c ‚â† OrderedFinpartition.atomic k,
                  c.compAlongOrderedFinpartition (iteratedFDeriv ‚Ñù c.length f.symm x)
                    (fun m ‚Ü¶ iteratedFDeriv ‚Ñù (c.partSize m) f (f.symm x))).compContinuousLinearMap
                      (fun _ ‚Ü¶ fderiv ‚Ñù f.symm x) -
              (FormalMultilinearSeries.id ‚Ñù E (f.symm a) k -
                ‚àë c ‚â† OrderedFinpartition.atomic k,
                  c.compAlongOrderedFinpartition (iteratedFDeriv ‚Ñù c.length f.symm a)
                    (fun m ‚Ü¶ iteratedFDeriv ‚Ñù (c.partSize m) f (f.symm a))).compContinuousLinearMap
                      (fun _ ‚Ü¶ fderiv ‚Ñù f.symm a) := by
            rw [‚Üê f.symm.symm_map_nhds_eq ha, f.symm_symm, eventuallyEq_map]
            filter_upwards [hf.contDiffAt.eventually (by simp),
              f.open_source.mem_nhds (f.symm_mapsTo ha), hinv]
              with x hx hfx hinv
            simp only [Function.comp_apply]
            rw [f.iteratedFDeriv_symm_eq_rec ha hf.contDiffAt le_rfl (fun _ ‚Ü¶ hf'),
              f.iteratedFDeriv_symm_eq_rec (f.mapsTo hfx) (by simpa [hfx]) le_rfl (by simp [*])]
          _ = fun x ‚Ü¶
            -‚àë c ‚â† OrderedFinpartition.atomic k,
              ((c.compAlongOrderedFinpartition (iteratedFDeriv ‚Ñù c.length f.symm x)
                (fun m ‚Ü¶ iteratedFDeriv ‚Ñù (c.partSize m) f (f.symm x))).compContinuousLinearMap
                  (fun _ ‚Ü¶ fderiv ‚Ñù f.symm x) -
                (c.compAlongOrderedFinpartition (iteratedFDeriv ‚Ñù c.length f.symm a)
                  (fun m ‚Ü¶ iteratedFDeriv ‚Ñù (c.partSize m) f (f.symm a))).compContinuousLinearMap
                    (fun _ ‚Ü¶ fderiv ‚Ñù f.symm a)) := by
            simp only [hk‚ÇÅ, FormalMultilinearSeries.id_apply_of_one_lt, zero_sub, neg_sub_neg,
              Finset.sum_sub_distrib, ContinuousMultilinearMap.compContinuousLinearMap_neg_left,
              ContinuousMultilinearMap.compContinuousLinearMap_sum_left, neg_sub]
          _ =O[ùìù a] fun x ‚Ü¶ ‚Äñx - a‚Äñ ^ (Œ± : ‚Ñù) := .neg_left <| .sum fun c hc ‚Ü¶ ?_
        simp only [OrderedFinpartition.compContinuousLinearMap_compAlongOrderedFinpartition_left]
        simp only [Finset.mem_erase, Finset.mem_univ, and_true, ‚Üê c.length_lt_iff] at hc
        apply c.compAlongOrderedFinpartition_sub_compAlongOrderedFinpartition_isBigO
        ¬∑ exact f.contDiffAt_symm' ha hf' hf.contDiffAt
            |>.continuousAt_iteratedFDeriv (mod_cast hc.le) |>.norm |>.isBoundedUnder_le
        ¬∑ refine .trans (.norm_right ?_) hrpow
          exact f.contDiffAt_symm' ha hf' hf.contDiffAt
            |>.differentiableAt_iteratedFDeriv (mod_cast hc) |>.isBigO_sub
        ¬∑ intro m
          refine (ContinuousAt.tendsto <| .norm ?_).isBoundedUnder_le
          simp only [‚Üê ContinuousMultilinearMap.compContinuousLinearMapL_apply]
          refine .clm_apply ?_ ?_
          ¬∑ refine map_continuous
              (ContinuousMultilinearMap.compContinuousLinearMapContinuousMultilinear ‚Ñù _ _ _)
              |>.continuousAt.comp ?_
            refine continuousAt_pi.2 fun _ ‚Ü¶ ?_
            exact f.contDiffAt_symm' ha hf' hf.contDiffAt |>.continuousAt_fderiv (mod_cast hk‚ÇÄ)
          ¬∑ refine hf.contDiffAt.continuousAt_iteratedFDeriv (mod_cast c.partSize_le _) |>.comp ?_
            exact f.continuousAt_symm ha
        ¬∑ exact fun _ ‚Ü¶ isBoundedUnder_const
        ¬∑ intro m
          apply ContinuousMultilinearMap.compContinuousLinearMap_sub_compContinuousLinearMap_isBigO
          ¬∑ apply isBoundedUnder_const
          ¬∑ exact (hf.of_le (c.partSize_le m) |>.isBigO |>.comp_tendsto <| f.continuousAt_symm ha)
              |>.trans hsymm_rpow_isBigO
          ¬∑ intro i
            exact f.contDiffAt_symm' ha hf' hf.contDiffAt |>.continuousAt_fderiv (mod_cast hk‚ÇÄ)
              |>.norm |>.isBoundedUnder_le
          ¬∑ exact fun _ ‚Ü¶ isBoundedUnder_const
          ¬∑ refine fun i ‚Ü¶ hfderiv_isBigO.trans (.trans (.trans ?_ hsymm_isBigO.norm_right) hrpow)
            exact hf.contDiffAt.fderiv_right (mod_cast hk‚ÇÅ) |>.differentiableAt one_ne_zero
              |>.isBigO_sub |>.comp_tendsto <| f.continuousAt_symm ha
