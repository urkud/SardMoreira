import Mathlib.Analysis.Calculus.ContDiff.Basic
import Mathlib.Analysis.SpecialFunctions.Pow.Real
import Mathlib.Topology.MetricSpace.Holder

open scoped unitInterval Topology NNReal
open Asymptotics Filter Set

namespace Asymptotics

/-- If `a â‰¤ b`, then `x^b = O(x^a)` as `x â†’ 0`, `x â‰¥ 0`, unless `b = 0` and `a â‰  0`. -/
theorem IsBigO.rpow_rpow_nhdsWithin_Ici_zero_of_le {a b : â„} (h : a â‰¤ b) (himp : b = 0 â†’ a = 0) :
    (Â· ^ b : â„ â†’ â„) =O[ğ“[â‰¥] 0] (Â· ^ a) :=
  .of_bound' <| mem_of_superset (Icc_mem_nhdsGE one_pos) fun x hx â†¦ by
    simpa [Real.abs_rpow_of_nonneg hx.1, abs_of_nonneg hx.1]
     using Real.rpow_le_rpow_of_exponent_ge_of_imp hx.1 hx.2 h fun _ â†¦ himp

theorem IsBigO.id_rpow_of_le_one {a : â„} (ha : a â‰¤ 1) :
    (id : â„ â†’ â„) =O[ğ“[â‰¥] 0] (Â· ^ a) := by
  simpa using rpow_rpow_nhdsWithin_Ici_zero_of_le ha (by simp)

end Asymptotics

@[to_additive]
theorem tendsto_norm_div_self_nhdsLE {E : Type*} [SeminormedGroup E] (a : E) :
    Tendsto (â€–Â· / aâ€–) (ğ“ a) (ğ“[â‰¥] 0) :=
  tendsto_nhdsWithin_iff.mpr âŸ¨tendsto_norm_div_self a, by simpâŸ©

section NormedField

variable {ğ•œ E F G : Type*} [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  [NormedAddCommGroup G] [NormedSpace ğ•œ G]

theorem ContDiffOn.continuousAt_iteratedFDerivWithin {f : E â†’ F} {s : Set E} {n : WithTop â„•âˆ}
    {k : â„•} {a : E} (hf : ContDiffOn ğ•œ n f s) (hs : UniqueDiffOn ğ•œ s) (ha : s âˆˆ ğ“ a) (hk : k â‰¤ n) :
    ContinuousAt (iteratedFDerivWithin ğ•œ k f s) a :=
  (hf.continuousOn_iteratedFDerivWithin hk hs).continuousAt ha

theorem ContDiffWithinAt.continuousWithinAt_iteratedFDerivWithin
    {f : E â†’ F} {s : Set E} {n : WithTop â„•âˆ} {k : â„•} {a : E} (hf : ContDiffWithinAt ğ•œ n f s a)
    (hs : UniqueDiffOn ğ•œ s) (ha : a âˆˆ s) (hk : k â‰¤ n) :
    ContinuousWithinAt (iteratedFDerivWithin ğ•œ k f s) s a :=
  (hf.iteratedFderivWithin_right hs (by rwa [zero_add]) ha).continuousWithinAt

theorem ContDiffAt.continuousAt_iteratedFDeriv
    {f : E â†’ F} {n : WithTop â„•âˆ} {k : â„•} {a : E} (hf : ContDiffAt ğ•œ n f a) (hk : k â‰¤ n) :
    ContinuousAt (iteratedFDeriv ğ•œ k f) a := by
  simp only [â† continuousWithinAt_univ, â† iteratedFDerivWithin_univ]
  exact hf.contDiffWithinAt.continuousWithinAt_iteratedFDerivWithin uniqueDiffOn_univ trivial hk

theorem ContDiffAt.differentiableAt_iteratedFDeriv
    {f : E â†’ F} {a : E} {n : WithTop â„•âˆ} {m : â„•} (hf : ContDiffAt ğ•œ n f a) (hm : m < n) :
    DifferentiableAt ğ•œ (iteratedFDeriv ğ•œ m f) a := by
  simp only [â† differentiableWithinAt_univ, â† iteratedFDerivWithin_univ]
  exact hf.differentiableWithinAt_iteratedFDerivWithin hm (by simp [uniqueDiffOn_univ])

variable (ğ•œ) in
structure ContDiffHolderAt (k : â„•) (Î± : I) (f : E â†’ F) (a : E) : Prop where
  contDiffAt : ContDiffAt ğ•œ k f a
  isBigO : (iteratedFDeriv ğ•œ k f Â· - iteratedFDeriv ğ•œ k f a) =O[ğ“ a] (â€–Â· - aâ€– ^ (Î± : â„))

theorem ContDiffAt.contDiffHolderAt {n : WithTop â„•âˆ} {k : â„•} {f : E â†’ F} {a : E}
    (h : ContDiffAt ğ•œ n f a) (hk : k < n) (Î± : I) : ContDiffHolderAt ğ•œ k Î± f a where
  contDiffAt := h.of_le hk.le
  isBigO := calc
    (iteratedFDeriv ğ•œ k f Â· - iteratedFDeriv ğ•œ k f a) =O[ğ“ a] (Â· - a) :=
      (h.differentiableAt_iteratedFDeriv hk).isBigO_sub
    _ =O[ğ“ a] (â€–Â· - aâ€– ^ (Î± : â„)) :=
      .of_norm_left <| .comp_tendsto (.id_rpow_of_le_one Î±.2.2) <| tendsto_norm_sub_self_nhdsLE a

namespace ContDiffHolderAt

@[simp]
theorem zero_exponent_iff {k : â„•} {f : E â†’ F} {a : E} :
    ContDiffHolderAt ğ•œ k 0 f a â†” ContDiffAt ğ•œ k f a := by
  refine âŸ¨contDiffAt, fun h â†¦ âŸ¨h, ?_âŸ©âŸ©
  simpa using ((h.continuousAt_iteratedFDeriv le_rfl).sub_const _).norm.isBoundedUnder_le

theorem of_exponent_le {k : â„•} {f : E â†’ F} {a : E} {Î± Î² : I} (hf : ContDiffHolderAt ğ•œ k Î± f a)
    (hle : Î² â‰¤ Î±) : ContDiffHolderAt ğ•œ k Î² f a where
  contDiffAt := hf.contDiffAt
  isBigO := hf.isBigO.trans <| by
    refine .comp_tendsto (.rpow_rpow_nhdsWithin_Ici_zero_of_le hle fun hÎ± â†¦ ?_) ?_
    Â· exact le_antisymm (le_trans (mod_cast hle) hÎ±.le) Î².2.1
    Â· exact tendsto_norm_sub_self_nhdsLE a

theorem of_lt {k l : â„•} {f : E â†’ F} {a : E} {Î± Î² : I} (hf : ContDiffHolderAt ğ•œ k Î± f a)
    (hlt : l < k) : ContDiffHolderAt ğ•œ l Î² f a :=
  hf.contDiffAt.contDiffHolderAt (mod_cast hlt) _

theorem of_toLex_le {k l : â„•} {f : E â†’ F} {a : E} {Î± Î² : I} (hf : ContDiffHolderAt ğ•œ k Î± f a)
    (hle : toLex (l, Î²) â‰¤ toLex (k, Î±)) : ContDiffHolderAt ğ•œ l Î² f a :=
  ((Prod.Lex.le_iff _ _).mp hle).elim hf.of_lt <| by rintro âŸ¨rfl, hleâŸ©; exact hf.of_exponent_le hle

theorem of_contDiffOn_holderWith {f : E â†’ F} {s : Set E} {k : â„•} {Î± : I} {a : E} {C : â„â‰¥0}
    (hf : ContDiffOn ğ•œ k f s) (hs : s âˆˆ ğ“ a)
    (hd : HolderOnWith C âŸ¨Î±, Î±.2.1âŸ© (iteratedFDeriv ğ•œ k f) s) :
    ContDiffHolderAt ğ•œ k Î± f a where
  contDiffAt := hf.contDiffAt hs
  isBigO := .of_bound C <| mem_of_superset hs fun x hx â†¦ by
    simpa [Real.abs_rpow_of_nonneg, â† dist_eq_norm, dist_nonneg]
      using hd.dist_le hx (mem_of_mem_nhds hs)

end ContDiffHolderAt
