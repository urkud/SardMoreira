import Mathlib

noncomputable section

variable {ğ•œ E F : Type*} [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] [CompleteSpace E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] [CompleteSpace F]

namespace HasStrictFDerivAt

@[irreducible]
def implicitFunctionDataOfComplementedKerRange (f : E â†’ F) (f' : E â†’L[ğ•œ] F) {a : E}
    (hf : HasStrictFDerivAt f f' a) (hker : (LinearMap.ker f').ClosedComplemented)
    (hrange : (LinearMap.range f').ClosedComplemented) :
    have := hrange.isClosed.completeSpace_coe
    ImplicitFunctionData ğ•œ E (LinearMap.range f') (LinearMap.ker f') := by
  haveI := hrange.isClosed.completeSpace_coe
  have hrange_apply (x) : hrange.choose (f' x) = âŸ¨f' x, by simpâŸ© :=
    hrange.choose_spec âŸ¨f' x, by simpâŸ©
  have hker_eq : LinearMap.ker (hrange.choose âˆ˜L f') = LinearMap.ker f' := by
    ext x
    simp_all
  have hrange_eq : LinearMap.range (hrange.choose âˆ˜L f') = âŠ¤ := by
    rw [LinearMap.range_eq_top]
    rintro âŸ¨_, x, rflâŸ©
    use x
    simp_all
  let Ï† := implicitFunctionDataOfComplemented (hrange.choose âˆ˜ f) (hrange.choose âˆ˜L f')
    (hrange.choose.hasStrictFDerivAt.comp a hf) hrange_eq (by rwa [hker_eq])
  refine
    { __ := Ï†,
      rightFun := hker.choose
      rightDeriv := hker.choose
      right_range := LinearMap.range_eq_of_proj (Classical.choose_spec hker)
      right_has_deriv := hker.choose.hasStrictFDerivAt
      isCompl_ker := ?_ }
  simpa only [Ï†, implicitFunctionDataOfComplemented, hker_eq]
    using LinearMap.isCompl_of_proj hker.choose_spec

def implicitToOpenPartialHomeomorphOfComplementedKerRange (f : E â†’ F) (f' : E â†’L[ğ•œ] F) {a : E}
    (hf : HasStrictFDerivAt f f' a) (hker : (LinearMap.ker f').ClosedComplemented)
    (hrange : (LinearMap.range f').ClosedComplemented) :
    OpenPartialHomeomorph E (LinearMap.range f' Ã— LinearMap.ker f') :=
  have := hrange.isClosed.completeSpace_coe
  (hf.implicitFunctionDataOfComplementedKerRange f f' hker hrange).toOpenPartialHomeomorph
    
theorem implicitToOpenPartialHomeomorphOfComplementedKerRange_apply {f : E â†’ F} {f' : E â†’L[ğ•œ] F}
    {a : E} (hf : HasStrictFDerivAt f f' a) (hker : (LinearMap.ker f').ClosedComplemented)
    (hrange : (LinearMap.range f').ClosedComplemented) (x : E) :
    implicitToOpenPartialHomeomorphOfComplementedKerRange f f' hf hker hrange x =
      (hrange.choose (f x), hker.choose x) := by
  -- `simp [implicitToOpenPartialHomeomorphOfComplementedKerRange,
  --  implicitFunctionDataOfComplementedKerRange]` works but it's much slower
  simp only [implicitToOpenPartialHomeomorphOfComplementedKerRange,
    implicitFunctionDataOfComplementedKerRange, implicitFunctionDataOfComplemented,
    Function.comp_apply, ImplicitFunctionData.toOpenPartialHomeomorph_apply]

theorem implicitToOpenPartialHomeomorphOfComplementedKerRange_apply_fst {f : E â†’ F} {f' : E â†’L[ğ•œ] F}
    {a : E} (hf : HasStrictFDerivAt f f' a) (hker : (LinearMap.ker f').ClosedComplemented)
    (hrange : (LinearMap.range f').ClosedComplemented) (x : E) :
    (implicitToOpenPartialHomeomorphOfComplementedKerRange f f' hf hker hrange x).fst =
      hrange.choose (f x) := by
  simp [implicitToOpenPartialHomeomorphOfComplementedKerRange_apply]

def implicitOfComplementedKerRangeConjugated (f : E â†’ F) (f' : E â†’L[ğ•œ] F) {a : E}
    (hf : HasStrictFDerivAt f f' a) (hker : (LinearMap.ker f').ClosedComplemented)
    (hrange : (LinearMap.range f').ClosedComplemented)
    (x : LinearMap.range f' Ã— LinearMap.ker f') :
    LinearMap.range f' Ã— LinearMap.ker hrange.choose :=
  (x.fst, hrange.choose.projKerOfRightInverse (LinearMap.range f').subtypeL hrange.choose_spec <|
    f ((implicitToOpenPartialHomeomorphOfComplementedKerRange f f' hf hker hrange).symm x) - f a)

@[simp]
theorem implicitOfComplementedKerRangeConjugated_fst {f : E â†’ F} {f' : E â†’L[ğ•œ] F} {a : E}
    (hf : HasStrictFDerivAt f f' a) (hker : (LinearMap.ker f').ClosedComplemented)
    (hrange : (LinearMap.range f').ClosedComplemented) (x : LinearMap.range f' Ã— LinearMap.ker f') :
    (implicitOfComplementedKerRangeConjugated f f' hf hker hrange x).fst = x.fst := rfl
