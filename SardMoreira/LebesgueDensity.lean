import SardMoreira.UpperLowerSemicontinuous
import SardMoreira.NormedSpace
import SardMoreira.MeasureComap
import SardMoreira.MeasureBallSemicontinuous
import Mathlib.Data.Real.StarOrdered
import Mathlib.LinearAlgebra.FreeModule.PID
import Mathlib.MeasureTheory.Constructions.HaarToSphere
import Mathlib.MeasureTheory.Covering.Besicovitch
import Mathlib.RingTheory.SimpleRing.Principal
import Mathlib.Topology.EMetricSpace.Paracompact
import Mathlib.Topology.Separation.CompletelyRegular
import Mathlib.Dynamics.Ergodic.Action.Regular
import Mathlib.MeasureTheory.Constructions.Polish.Basic
import Mathlib.MeasureTheory.Measure.Decomposition.RadonNikodym
import Mathlib.MeasureTheory.Measure.Haar.Unique

open scoped ENNReal NNReal Set.Notation Pointwise
open MeasureTheory Filter Set Function Metric Topology

theorem MeasureTheory.Measure.cofinite_le_ae {Î± : Type*} {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) :
    Î¼.cofinite â‰¤ ae Î¼ := by
  intro s hs
  simp_all [mem_cofinite, mem_ae_iff]

theorem absolutelyContinuous_volumeIoiPow_left (n : â„•) :
    .volumeIoiPow n â‰ª .comap Subtype.val volume := by
  apply MeasureTheory.withDensity_absolutelyContinuous

protected theorem MeasureTheory.Measure.AbsolutelyContinuous.comap {Î± Î² : Type*}
    {_ : MeasurableSpace Î±} {_ : MeasurableSpace Î²} {Î¼ Î½ : Measure Î²} (h : Î¼ â‰ª Î½) (f : Î± â†’ Î²)
    (hfÎ½ : âˆ€ s, MeasurableSet s â†’ NullMeasurableSet (f '' s) Î½) :
    Î¼.comap f â‰ª Î½.comap f := by
  by_cases hf : Injective f âˆ§ âˆ€ s, MeasurableSet s â†’ NullMeasurableSet (f '' s) Î½
  Â· refine .mk fun s hsm hs â†¦ ?_
    rw [Measure.comap_applyâ‚€ _ _ hf.1] at *
    Â· exact h hs
    Â· exact hfÎ½
    Â· exact hsm.nullMeasurableSet
    Â· exact fun t ht â†¦ (hf.2 t ht).mono_ac h
    Â· exact hsm.nullMeasurableSet
  Â· rw [Measure.comap, dif_neg]
    Â· exact .zero _
    Â· contrapose! hf
      exact âŸ¨hf.1, hfÎ½âŸ©

theorem MeasurableEmbedding.quasiMeasurePreserving_iff_comap {Î± Î² : Type*}
    {_ : MeasurableSpace Î±} {_ : MeasurableSpace Î²} {e : Î± â†’ Î²} (he : MeasurableEmbedding e)
    {Î¼ : Measure Î±} {Î½ : Measure Î²} :
    Measure.QuasiMeasurePreserving e Î¼ Î½ â†” Î¼ â‰ª .comap e Î½ := by
  constructor <;> intro h
  Â· rw [â† he.comap_map Î¼]
    exact h.2.comap _ fun s hs â†¦ (he.measurableSet_image.mpr hs).nullMeasurableSet
  Â· use he.measurable
    refine (h.map he.measurable).trans ?_
    rw [he.map_comap]
    exact Î½.restrict_le_self.absolutelyContinuous

theorem MeasureTheory.Measure.QuasiMeasurePreserving.subtypeVal_volumeIoiPow (n : â„•) :
    Measure.QuasiMeasurePreserving Subtype.val (.volumeIoiPow n) volume := by
  rw [MeasurableEmbedding.quasiMeasurePreserving_iff_comap]
  exacts [absolutelyContinuous_volumeIoiPow_left n, .subtype_coe measurableSet_Ioi]

theorem absolutelyContinuous_volumeIoiPow_right (n : â„•) :
    .comap Subtype.val volume â‰ª .volumeIoiPow n := by
  refine MeasureTheory.withDensity_absolutelyContinuous' ?_ <| .of_forall ?_
  Â· fun_prop
  Â· rintro âŸ¨x, hx : 0 < xâŸ©
    positivity

/-- If a finite measure `Î¼` is absolutely continuous with respect to a Ïƒ-finite measure `Î½`,
then `Î¼ s â†’ 0` as `Î½ s â†’ 0`. More precisely, for any `Îµ â‰  0` there exists `Î´ > 0`
such that all sets of `Î½` measure less than `Î´` have a `Î¼` measure less than `Îµ`.
 -/
theorem MeasureTheory.Measure.AbsolutelyContinuous.exists_pos_forall_lt_imp_lt_of_isFiniteMeasure
    {Î± : Type*} {_ : MeasurableSpace Î±} {Î¼ Î½ : Measure Î±} [IsFiniteMeasure Î¼]
    [Î¼.HaveLebesgueDecomposition Î½]
    (h : Î¼ â‰ª Î½) {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) :
    âˆƒ Î´ : â„â‰¥0, Î´ > 0 âˆ§ âˆ€ s, Î½ s < Î´ â†’ Î¼ s < Îµ := by
  obtain âŸ¨Ï†, hÏ†m, rflâŸ© : âˆƒ Ï† : Î± â†’ â„â‰¥0âˆ, Measurable Ï† âˆ§ Î¼ = Î½.withDensity Ï† := by
    refine âŸ¨Î¼.rnDeriv Î½, by fun_prop, ?_âŸ©
    symm
    refine Measure.absolutelyContinuous_iff_withDensity_rnDeriv_eq.mp h
  have hÏ† : âˆ«â» x, Ï† x âˆ‚Î½ â‰  âŠ¤ := by
    rw [â† setLIntegral_univ, â† withDensity_apply _ .univ]
    apply measure_ne_top
  rcases exists_pos_setLIntegral_lt_of_measure_lt hÏ† hÎµ with âŸ¨Î´, hÎ´â‚€, hÎ´âŸ©
  rcases ENNReal.lt_iff_exists_nnreal_btwn.mp hÎ´â‚€ with âŸ¨Î´', hÎ´'â‚€, hÎ´'âŸ©
  refine âŸ¨Î´', mod_cast hÎ´'â‚€, fun s hs â†¦ ?_âŸ©
  calc
    Î½.withDensity Ï† s â‰¤ Î½.withDensity Ï† (toMeasurable Î½ s) := by gcongr; apply subset_toMeasurable
    _ = âˆ«â» a in toMeasurable Î½ s, Ï† a âˆ‚Î½ := withDensity_apply _ (measurableSet_toMeasurable _ _)
    _ < Îµ := by
      apply hÎ´
      rw [measure_toMeasurable]
      exact hs.trans hÎ´'

/--
The previous lemma is not true unless we assume that `Î½` is a finite measure.
Indeed, for `Î½ = volume` on `â„`, `Î¼ = Î½.withDensity (Real.nnabs Â·)`,
we can choose `s = Set.Icc a (a + Î´ / 2)` for a large `a`,
and get an arbitriraly large value of `Î¼ s`.
-/
theorem exists_absolutelyContinuous_forall_pos_exists_lt_gt :
    âˆƒ Î¼ Î½ : Measure â„, Î¼ â‰ª Î½ âˆ§ âˆ€ C Î´ : â„â‰¥0, Î´ â‰  0 â†’
      âˆƒ s, MeasurableSet s âˆ§ Î½ s < Î´ âˆ§ Î¼ s > C := by
  -- Define the measures Î¼ and Î½ as described.
  use MeasureTheory.volume.withDensity (â€–Â·â€–â‚‘), MeasureTheory.volume
  constructor
  Â· exact withDensity_absolutelyContinuous volume fun x â†¦ â†‘(Real.nnabs x);
  Â· intro C Î´ hÎ´
    -- Choose $a > 0$ large enough such that $a * Î´ / 2 > C$.
    obtain âŸ¨a, haâ‚€, haâŸ© : âˆƒ a : â„â‰¥0, a > 0 âˆ§ a * Î´ / 2 > C := by
      rcases exists_pos_lt_mul (show 0 < (Î´ / 2 : â„) by positivity) C with âŸ¨a, haâ‚€, haâŸ©
      lift a to â„â‰¥0 using haâ‚€.le
      exact âŸ¨a, haâ‚€, by norm_cast at ha; rwa [mul_div_assoc]âŸ©
    refine âŸ¨ Set.Icc a ( a + Î´ / 2 ), measurableSet_Icc, ?_, ?_ âŸ©
    Â· rw [Real.volume_Icc, add_sub_cancel_left]
      norm_cast
      rw [ENNReal.ofReal_coe_nnreal]
      norm_cast
      exact NNReal.half_lt_self hÎ´
    Â· calc
        (C : â„â‰¥0âˆ) < a * Î´ / 2 := by
          rw [gt_iff_lt, â† ENNReal.coe_lt_coe, ENNReal.coe_div (by simp)] at ha
          exact mod_cast ha
        _ = a * volume (Icc (a : â„) (a + Î´ / 2)) := by
          simp [ENNReal.ofReal_div_of_pos, mul_div_assoc]
        _ â‰¤ _ := by
          rw [withDensity_apply _ measurableSet_Icc, â† setLIntegral_const]
          refine setLIntegral_mono measurable_enorm ?_
          rintro x âŸ¨hx, -âŸ©
          simpa [â† NNReal.coe_le_coe] using hx.trans (le_abs_self x)

/-- If a finite measure `Î¼` is absolutely continuous with respect to a Ïƒ-finite measure `Î½`,
then `Î¼ s â†’ 0` as `Î½ s â†’ 0`. More precisely, for any `Îµ â‰  0` there exists `Î´ > 0`
such that all sets of `Î½` measure less than `Î´` have a `Î¼` measure less than `Îµ`.
 -/
theorem MeasureTheory.Measure.AbsolutelyContinuous.exists_pos_forall_subset_lt_imp_lt
    {Î± : Type*} {_ : MeasurableSpace Î±} {Î¼ Î½ : Measure Î±} [SigmaFinite Î½]
    (h : Î¼ â‰ª Î½) {t : Set Î±} (ht : Î¼ t â‰  âŠ¤) {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) :
    âˆƒ Î´ : â„â‰¥0, Î´ > 0 âˆ§ âˆ€ s âŠ† t, Î½ s < Î´ â†’ Î¼ s < Îµ := by
  have := Fact.mk ht.lt_top
  have : Î¼.restrict t â‰ª Î½ := .trans Î¼.restrict_le_self.absolutelyContinuous h
  rcases this.exists_pos_forall_lt_imp_lt_of_isFiniteMeasure hÎµ with âŸ¨Î´, hÎ´â‚€, hÎ´âŸ©
  refine âŸ¨Î´, hÎ´â‚€, fun s hst hs â†¦ ?_âŸ©
  simpa only [Measure.restrict_eq_self _ hst] using hÎ´ s hs

theorem MeasureTheory.Measure.AbsolutelyContinuous.exists_pos_forall_lt_imp_lt
    {Î± : Type*} {_ : MeasurableSpace Î±} {Î¼ Î½ : Measure Î±} [SFinite Î¼] [SigmaFinite Î½]
    (h : Î¼ â‰ª Î½) {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0)
    (hrnDeriv : âˆƒ C : â„â‰¥0, Î¼.rnDeriv Î½ â‰¤á¶ [Î¼.cofinite] fun _ â†¦ C)  :
    âˆƒ Î´ : â„â‰¥0, Î´ > 0 âˆ§ âˆ€ s, Î½ s < Î´ â†’ Î¼ s < Îµ := by
  rcases hrnDeriv with âŸ¨C, hCâŸ©
  simp only [Filter.EventuallyLE, Measure.eventually_cofinite, not_le] at hC
  rcases h.exists_pos_forall_subset_lt_imp_lt hC.ne (ENNReal.half_pos hÎµ).ne' with âŸ¨Î´', hÎ´'â‚€, hÎ´'âŸ©
  obtain âŸ¨Î´, hÎ´â‚€, hÎ´Î´', hÎ´ÎµâŸ© : âˆƒ Î´ : â„â‰¥0, 0 < Î´ âˆ§ Î´ â‰¤ Î´' âˆ§ C * Î´ â‰¤ Îµ / 2 := by
    have : min 1 (Îµ / 2 / C) â‰  âŠ¤ := by simp
    refine âŸ¨min Î´' (min 1 (Îµ / 2 / C)).toNNReal, ?_, min_le_left _ _, ?_âŸ©
    Â· apply lt_min hÎ´'â‚€
      apply ENNReal.toNNReal_pos
      Â· simp [hÎµ]
      Â· exact this
    Â· push_cast [ENNReal.coe_toNNReal this]
      grw [min_le_right, min_le_right, ENNReal.mul_div_le]
  refine âŸ¨Î´, hÎ´â‚€, fun s hs â†¦ ?_âŸ©
  calc
    Î¼ s = Î¼ (s âˆ© {x | C < Î¼.rnDeriv Î½ x}) + Î¼ (s \ {x | C < Î¼.rnDeriv Î½ x}) := by
      rw [measure_inter_add_diff]
      apply measurableSet_lt <;> fun_prop
    _ < Îµ / 2 + C * Î´ := by
      have : Î¼ (s \ {x | â†‘C < Î¼.rnDeriv Î½ x}) â‰¤ â†‘C * â†‘Î´ := by
        grw [â† setLIntegral_rnDeriv h, â† hs, â† setLIntegral_const]
        refine (setLIntegral_mono measurable_const ?_).trans (lintegral_mono_set diff_subset)
        simp
      refine ENNReal.add_lt_add_of_lt_of_le ?_ ?_ this
      Â· refine ne_top_of_le_ne_top (by finiteness) this
      Â· apply hÎ´' _ inter_subset_right
        grw [inter_subset_left, â† hÎ´Î´']
        assumption
    _ â‰¤ Îµ := by
      grw [hÎ´Îµ, ENNReal.add_halves]

@[simps apply_fst_coe apply_snd_coe symm_apply_coe]
noncomputable def homeomorphSphereProd (E : Type*) [NormedAddCommGroup E] [NormedSpace â„ E]
    (r : â„) (hr : 0 < r) :
    ({0}á¶œ : Set E) â‰ƒâ‚œ (sphere (0 : E) r Ã— Ioi (0 : â„)) where
  toFun x :=
    have : 0 < â€–(x : E)â€– := by simpa [-Subtype.coe_prop] using x.2
    (âŸ¨r â€¢ â€–x.1â€–â»Â¹ â€¢ x.1, by simp [norm_smul, abs_of_pos hr, this.ne']âŸ©,
      âŸ¨â€–x.1â€– / r, by rw [mem_Ioi]; positivityâŸ©)
  invFun x := âŸ¨x.2.1 â€¢ x.1.1, smul_ne_zero x.2.2.out.ne' (ne_of_mem_sphere x.1.2 hr.ne')âŸ©
  left_inv
  | âŸ¨x, hxâŸ© => by
    have : 0 < â€–xâ€– := by simpa using hx
    ext; simp only [smul_smul]; field_simp; simp
  right_inv
  | (âŸ¨x, hxâŸ©, âŸ¨d, hdâŸ©) => by
    rw [mem_Ioi] at hd
    rw [mem_sphere_zero_iff_norm] at hx
    simp (disch := positivity) [norm_smul, smul_smul, abs_of_pos hd, hx]
  continuous_toFun := by
    simp only
    fun_prop (disch := simp)
  continuous_invFun := by fun_prop

theorem IsOpen.smul_sphere {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    {r : â„} (hr : r â‰  0) {U : Set â„} {V : Set (Metric.sphere (0 : E) r)}
    (hU : IsOpen U) (hUâ‚€ : 0 âˆ‰ U) (hV : IsOpen V) : IsOpen (U â€¢ (V : Set E)) := by
  rw [isOpen_iff_mem_nhds]
  rintro _ âŸ¨x, hxU, _, âŸ¨y, hyV, rflâŸ©, rflâŸ©
  wlog hxâ‚€ : 0 < x generalizing x U
  Â· replace hxâ‚€ : 0 < -x := by
      rw [not_lt, le_iff_eq_or_lt, â† neg_pos] at hxâ‚€
      exact hxâ‚€.resolve_left <| ne_of_mem_of_not_mem hxU hUâ‚€
    specialize this hU.neg (by simpa) (-x) (by simpa) hxâ‚€
    simp only [neg_smul, nhds_neg, Set.neg_smul, Filter.mem_neg] at this
    simpa using this
  have hrâ‚€ : 0 < r := lt_of_le_of_ne (by simpa using norm_nonneg y.1) hr.symm
  lift x to Ioi (0 : â„) using hxâ‚€
  have : V Ã—Ë¢ (Ioi (0 : â„) â†“âˆ© U) âˆˆ ğ“ (y, x) :=
    prod_mem_nhds (hV.mem_nhds hyV) (hU.preimage_val.mem_nhds hxU)
  replace := image_mem_map (m := Subtype.val âˆ˜ (homeomorphSphereProd E r hrâ‚€).symm) this
  rw [â† Filter.map_map, (homeomorphSphereProd _ r hrâ‚€).symm.map_nhds_eq,
    map_nhds_subtype_val, IsOpen.nhdsWithin_eq, homeomorphSphereProd_symm_apply_coe] at this
  Â· filter_upwards [this]
    rintro _ âŸ¨âŸ¨a, bâŸ©, âŸ¨ha, hbâŸ©, rflâŸ©
    rw [Function.comp_apply, homeomorphSphereProd_symm_apply_coe]
    apply Set.smul_mem_smul
    exacts [hb, mem_image_of_mem _ ha]
  Â· exact isOpen_compl_singleton
  Â· simp [x.2.out.ne', ne_zero_of_mem_sphere, hrâ‚€.ne']

instance MeasureTheory.Measure.toSphere.instIsOpenPosMeasure
    (E : Type*) [NormedAddCommGroup E] [NormedSpace â„ E] [MeasurableSpace E] [BorelSpace E]
    [FiniteDimensional â„ E] (Î¼ : Measure E) [Î¼.IsOpenPosMeasure] :
    Î¼.toSphere.IsOpenPosMeasure where
  open_pos := by
    nontriviality E using not_nonempty_iff_eq_empty
    rintro U hUo hU
    rw [Î¼.toSphere_apply' hUo.measurableSet]
    apply mul_ne_zero (by simp [Module.finrank_pos.ne'])
    refine IsOpen.measure_ne_zero _ ?_ (by simpa)
    exact isOpen_Ioo.smul_sphere one_ne_zero (by simp) hUo

-- TODO: write a readable docstring
-- TODO: move some code to lemmas
theorem exists_pos_forall_measure_le_toSphere_ge_le
    {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [FiniteDimensional â„ E]
    {_ : MeasurableSpace E} [BorelSpace E] (Î¼ : Measure E) [Î¼.IsAddHaarMeasure]
    {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) :
    âˆƒ Î´ : â„â‰¥0, 0 < Î´ âˆ§ âˆ€ s, Î¼ s â‰¤ Î´ â†’
      Î¼.toSphere {x | volume {t : â„ | 0 â‰¤ t âˆ§ t â€¢ x.1 âˆˆ s} â‰¥ Îµ} < Îµ := by
  nontriviality E using exists_gt, pos_iff_ne_zero.mpr hÎµ
  set d := Module.finrank â„ E
  obtain âŸ¨Î´, hÎ´â‚€, hÎ´âŸ© : âˆƒ Î´ > (0 : â„â‰¥0), âˆ€ s, Measure.volumeIoiPow (d - 1) s < Î´ â†’
      volume.comap Subtype.val s < â†‘Îµ := by
    refine absolutelyContinuous_volumeIoiPow_right _ |>.exists_pos_forall_lt_imp_lt hÎµ âŸ¨1, ?_âŸ©
    rw [Measure.volumeIoiPow]
    set Î¼ : Measure (Ioi (0 : â„)) := .comap Subtype.val volume
    calc
      Î¼.rnDeriv (Î¼.withDensity fun r â†¦ ENNReal.ofReal (â†‘r ^ (d - 1)))
        =á¶ [Î¼.cofinite] (fun x â†¦ (ENNReal.ofReal (â†‘x ^ (d - 1)))â»Â¹ * Î¼.rnDeriv Î¼ x) := by
        apply EventuallyEq.filter_mono _ Î¼.cofinite_le_ae
        apply Î¼.rnDeriv_withDensity_right Î¼
        Â· fun_prop
        Â· refine .of_forall fun âŸ¨x, (hx : 0 < x)âŸ© â†¦ ?_
          positivity
        Â· simp
      _ =á¶ [Î¼.cofinite] (fun x â†¦ (ENNReal.ofReal (â†‘x ^ (d - 1)))â»Â¹) := by
        apply EventuallyEq.filter_mono _ Î¼.cofinite_le_ae
        filter_upwards [Î¼.rnDeriv_self] with x hx
        simp [hx]
      _ â‰¤á¶ [Î¼.cofinite] fun _ â†¦ (1 : â„â‰¥0) := by
        have : Ioi (0 : â„) â†“âˆ© Ici 1 âˆˆ Î¼.cofinite := by
          simp only [Measure.mem_cofinite, Î¼]
          refine (Measure.comap_apply_le _ _ <| by measurability).trans_lt ?_
          simp
        filter_upwards [this]
        simp +contextual [one_le_powâ‚€]
  replace hÎ´ : âˆ€ s âŠ† Ici (0 : â„),
      Measure.volumeIoiPow (d - 1) (Ioi (0 : â„) â†“âˆ© s) < â†‘Î´ â†’ volume s < Îµ := by
    intro s hs hsÎ´
    refine lt_of_le_of_lt ?_ (hÎ´ (Ioi (0 : â„) â†“âˆ© s) hsÎ´)
    rw [(MeasurableEmbedding.subtype_coe measurableSet_Ioi).comap_preimage, Subtype.range_val]
    apply measure_mono_ae
    filter_upwards [Ioi_ae_eq_Ici.symm.le] with a ha hsa using âŸ¨hsa, ha (hs hsa)âŸ©
  set T : Set E â†’ Set (Metric.sphere (0 : E) 1) := fun s â†¦
    {x | Measure.volumeIoiPow (Module.finrank â„ E - 1)
      {t : Ioi (0 : â„) | ((homeomorphUnitSphereProd E).symm (x, t)).1 âˆˆ s} â‰¥ Î´}
  have hT : âˆ€ s,
      Î¼.toSphere {x | volume {t : â„ | 0 â‰¤ t âˆ§ t â€¢ x.1 âˆˆ s} â‰¥ Îµ} â‰¤ Î¼.toSphere (T s)  := by
    refine fun s â†¦ measure_mono fun x hx â†¦ ?_
    simp only [T]
    rw [mem_setOf_eq] at hx âŠ¢
    contrapose! hx
    apply hÎ´
    Â· exact fun _ â†¦ And.left
    Â· convert hx
      ext t
      simp [t.2.out.le]
  obtain âŸ¨Îµ', hÎµ'â‚€, hÎµ'âŸ© : âˆƒ Îµ' : â„â‰¥0, 0 < Îµ' âˆ§ Îµ' < Îµ :=
    mod_cast ENNReal.lt_iff_exists_nnreal_btwn.mp (pos_iff_ne_zero.mpr hÎµ)
  norm_cast at hÎµ
  refine âŸ¨Îµ' * Î´, by positivity, fun s hÎ¼s â†¦ ?_âŸ©
  wlog hsm : MeasurableSet s generalizing s
  Â· refine lt_of_le_of_lt ?_ (this (toMeasurable Î¼ s) ?_ ?_)
    Â· refine measure_mono fun x hx â†¦ ?_
      rw [mem_setOf_eq] at hx âŠ¢
      exact le_trans hx <| measure_mono fun t âŸ¨htâ‚€, htâŸ© â†¦ âŸ¨htâ‚€, subset_toMeasurable _ _ htâŸ©
    Â· rwa [measure_toMeasurable]
    Â· measurability
  refine (hT s).trans_lt ?_
  contrapose! hÎ¼s
  push_cast
  calc
    (Îµ' * Î´ : â„â‰¥0âˆ) < Î¼.toSphere (T s) * Î´ := by
      gcongr
      Â· simp
      Â· exact hÎµ'.trans_le hÎ¼s
    _ â‰¤ Î¼.comap (â†‘) ({(0 : E)}á¶œ â†“âˆ© s) := by
      have := Î¼.measurePreserving_homeomorphUnitSphereProd
      rw [â† Homeomorph.toMeasurableEquiv_coe] at this
      rw [â† this.symm.measure_preimage_emb (MeasurableEquiv.measurableEmbedding _),
        Measure.prod_apply, mul_comm, â† setLIntegral_const]
      Â· refine (setLIntegral_mono ?_ ?_).trans (setLIntegral_le_lintegral _ _)
        Â· apply measurable_measure_prodMk_left
          refine MeasurableEquiv.measurable _ ?_
          exact hsm.preimage measurable_subtype_coe
        Â· intro x hx
          simpa [T] using hx
      Â· refine MeasurableEquiv.measurable _ ?_
        exact hsm.preimage measurable_subtype_coe
    _ â‰¤ Î¼ s := by
      rw [(MeasurableEmbedding.subtype_coe <| by measurability).comap_preimage]
      exact measure_mono inter_subset_left

theorem exists_pos_forall_measure_le_exists_mem_sphere_dist_lt_volume_smul_mem_lt
    {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [FiniteDimensional â„ E]
    {_ : MeasurableSpace E} [BorelSpace E] (Î¼ : Measure E) [Î¼.IsAddHaarMeasure]
    {Îµ : â„â‰¥0} (hÎµ : Îµ â‰  0) :
    âˆƒ Î´ : â„â‰¥0, 0 < Î´ âˆ§ âˆ€ s, Î¼ s â‰¤ Î´ â†’ âˆ€ x âˆˆ Metric.sphere (0 : E) 1,
      âˆƒ y âˆˆ Metric.sphere (0 : E) 1,
        dist y x < Îµ âˆ§ volume {t : â„ | 0 â‰¤ t âˆ§ t â€¢ y âˆˆ s} < Îµ := by
  rcases exists_pos_forall_lt_measure_ball Î¼.toSphere (r := Îµ) (by positivity) with âŸ¨Î´, hÎ´â‚€, hÎ´âŸ©
  rcases exists_pos_forall_measure_le_toSphere_ge_le Î¼ (Îµ := min Î´ Îµ) (by positivity)
    with âŸ¨Î·, hÎ·â‚€, hÎ·âŸ©
  refine âŸ¨Î·, hÎ·â‚€, fun s hs x hx â†¦ ?_âŸ©
  specialize hÎ· s hs
  contrapose! hÎ·
  calc
    min (Î´ : â„â‰¥0âˆ) Îµ â‰¤ Î´ := min_le_left _ _
    _ â‰¤ Î¼.toSphere (ball âŸ¨x, hxâŸ© Îµ) := hÎ´ _ |>.le
    _ â‰¤ Î¼.toSphere {y : sphere (0 : E) 1 | volume {t : â„ | 0 â‰¤ t âˆ§ t â€¢ y.1 âˆˆ s} â‰¥ min â†‘Î´ â†‘Îµ} := by
      gcongr Î¼.toSphere ?_
      intro y hy
      rw [mem_setOf_eq]
      exact (min_le_right _ _).trans <| hÎ· _ y.2 hy

@[simp]
theorem addHaar_nnreal_smul {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    [FiniteDimensional â„ E] {_ : MeasurableSpace E} [BorelSpace E]
    (Î¼ : Measure E) [Î¼.IsAddHaarMeasure] (c : â„â‰¥0) (s : Set E) :
    Î¼ (c â€¢ s) = c ^ Module.finrank â„ E * Î¼ s := by
  simp [NNReal.smul_def]

theorem exists_pos_forall_measure_le_exists_mem_sphere_dist_lt_volume_lineMap_mem_lt
    {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [FiniteDimensional â„ E]
    {Îµ : â„â‰¥0} (hÎµ : Îµ â‰  0) :
    âˆƒ Î´ : â„â‰¥0, 0 < Î´ âˆ§
      âˆ€ {_ : MeasurableSpace E} [BorelSpace E] (Î¼ : Measure E) [Î¼.IsAddHaarMeasure],
        âˆ€ r â‰  0, âˆ€ a s, Î¼ s â‰¤ Î´ * Î¼ (Metric.ball a r) â†’ âˆ€ x âˆˆ Metric.sphere a r,
          âˆƒ y âˆˆ Metric.sphere a r,
            dist y x < Îµ * r âˆ§ volume {t : â„ | 0 â‰¤ t âˆ§ AffineMap.lineMap a y t âˆˆ s} < Îµ := by
  borelize E
  rcases exists_pos_forall_measure_le_exists_mem_sphere_dist_lt_volume_smul_mem_lt (E := E)
    .addHaar hÎµ with âŸ¨Î´, hÎ´â‚€, hÎ´âŸ©
  set Î¼B : â„â‰¥0âˆ := (Measure.addHaar (Metric.ball (0 : E) 1)) with hÎ¼B
  have hÎ¼Bâ‚€ : Î¼B â‰  0 := (Metric.measure_ball_pos _ _ one_pos).ne'
  have hÎ¼Btop : Î¼B â‰  âˆ := measure_ball_lt_top.ne
  have hÎ¼Bâ‚€' : 0 < Î¼B.toNNReal := ENNReal.toNNReal_pos hÎ¼Bâ‚€ hÎ¼Btop
  have hÎ¼B' : Î¼B.toNNReal = Î¼B := ENNReal.coe_toNNReal hÎ¼Btop
  refine âŸ¨Î´ / Î¼B.toNNReal, by positivity, ?_âŸ©
  rintro _ âŸ¨rflâŸ© Î¼ hÎ¼ r hrâ‚€ a s hs x hx
  rcases hrâ‚€.lt_or_gt with hlt | hgt
  Â· simp [Metric.sphere_eq_empty_of_neg hlt] at hx
  lift r to â„â‰¥0 using hgt.le
  norm_cast at hgt
  obtain âŸ¨c, hcâ‚€, rflâŸ© : âˆƒ c : â„â‰¥0, 0 < c âˆ§ Î¼ = c â€¢ .addHaar :=
    âŸ¨Î¼.addHaarScalarFactor .addHaar, Measure.addHaarScalarFactor_pos_of_isAddHaarMeasure ..,
      Î¼.isAddLeftInvariant_eq_smul _âŸ©
  set d := Module.finrank â„ E
  specialize hÎ´ (râ»Â¹ â€¢ (-a +áµ¥ s)) ?_ (râ»Â¹ â€¢ (-a + x)) ?_
  Â· rw [â† ENNReal.mul_le_mul_left (a := c) (by positivity) ENNReal.coe_ne_top]
    calc
      c * Measure.addHaar (râ»Â¹ â€¢ (-a +áµ¥ s)) = râ»Â¹ ^ d * â‡‘(c â€¢ Measure.addHaar) s := by
        simp (disch := positivity) [d, mul_left_comm]
      _ â‰¤ (r : â„â‰¥0âˆ)â»Â¹ ^ d * ((Î´ / Î¼B) * â‡‘(c â€¢ Measure.addHaar) (ball a â†‘r)) := by
        grw [hs]
        simp (disch := positivity) [ENNReal.coe_div, hÎ¼B']
      _ = (r : â„â‰¥0âˆ)â»Â¹ ^ d * (Î´ / Î¼B * (c * (r ^ d * Î¼B))) := by
        simp only [Measure.smul_apply, Measure.nnreal_smul_coe_apply]
        simp [Measure.addHaar.addHaar_ball_of_pos _ (r := r) hgt, d, â† hÎ¼B]
      _ = c * (r / r) ^ d * (Î¼B / Î¼B) * Î´ := by
        simp only [div_eq_mul_inv, mul_pow]
        ac_rfl
      _ â‰¤ c * Î´ := by
        grw [ENNReal.div_self_le_one, ENNReal.div_self_le_one]
        simp
  Â· simp_all [â† sub_eq_neg_add, NNReal.smul_def, norm_smul]
  Â· rcases hÎ´ with âŸ¨y, hyâ‚, hyx, hyÎµâŸ©
    simp only [mem_sphere_iff_norm, sub_zero] at hyâ‚
    refine âŸ¨a + r â€¢ y, by simp [hyâ‚, NNReal.smul_def, norm_smul], ?_, ?_âŸ©
    Â· calc
        dist (a + r â€¢ y) x = dist y (râ»Â¹ â€¢ (-a + x)) * r := by
          rw [mul_comm, â† NNReal.dist_smul, â† dist_add_left (-a)]
          simp (disch := positivity)
        _ < Îµ * r := by
          gcongr
    Â· simpa (disch := positivity) [AffineMap.lineMap_apply_module', mem_inv_smul_set_iffâ‚€,
        mem_neg_vadd_set_iff, add_comm, smul_comm r] using hyÎµ

/-- If $s_b$ is a family of sets such that $\{(a, b) \mid a \in s_b\}$ is a measurable set,
then for any s-finite measure $\mu$, the function $b \mapsto \mu(s_b)$ is measurable.

This is a version of `measurable_measure_prod_mk_right`. -/
theorem Measurable.measure_apply {Î± Î² : Type*} [MeasurableSpace Î±] [MeasurableSpace Î²]
    (Î¼ : Measure Î±) [SFinite Î¼] (s : Î² â†’ Set Î±) (hs : MeasurableSet {p : Î± Ã— Î² | p.1 âˆˆ s p.2}) :
    Measurable fun b â†¦ Î¼ (s b) :=
  measurable_measure_prodMk_right hs

theorem Metric.biInter_lt_rat_closedBall {X : Type*} [PseudoMetricSpace X] (x : X) (r : â„) :
    closedBall x r = â‹‚ (q : â„š) (_ : r < q), closedBall x q := by
  ext
  simpa only [mem_iInterâ‚‚, mem_closedBall] using le_iff_forall_lt_rat_imp_le

theorem eventually_measure_closedBall_lt_top
    {X : Type*} [PseudoMetricSpace X] [MeasurableSpace X]
    (Î¼ : Measure X) [IsLocallyFiniteMeasure Î¼] (x : X) :
    âˆ€á¶  r in ğ“ 0, Î¼ (closedBall x r) < âŠ¤ := by
  rcases (Î¼.finiteAt_nhds x).exists_mem_basis nhds_basis_closedBall with âŸ¨Îµ, Îµâ‚€, hÎµâŸ©
  exact (eventually_lt_nhds Îµâ‚€).mono fun r hr â†¦ lt_of_le_of_lt (by gcongr) hÎµ

theorem eventually_forall_le_continuousWithinAt_Ici_measure_closedBall
    {X : Type*} [PseudoMetricSpace X] [MeasurableSpace X] [OpensMeasurableSpace X]
    (Î¼ : Measure X) [IsLocallyFiniteMeasure Î¼] (x : X) :
    âˆ€á¶  r : â„ in ğ“ 0, âˆ€ Î½ â‰¤ Î¼, ContinuousWithinAt (Î½ <| closedBall x Â·) (Set.Ici r) r := by
  rcases (Î¼.finiteAt_nhds x).exists_mem_basis nhds_basis_closedBall with âŸ¨Îµ, Îµâ‚€, hÎµâŸ©
  filter_upwards [eventually_lt_nhds Îµâ‚€] with r hr Î½ hÎ½
  rw [â† continuousWithinAt_Ioi_iff_Ici, ContinuousWithinAt]
  convert tendsto_measure_biInter_gt (by measurability) (by intros; gcongr)
    âŸ¨Îµ, hr, ((hÎ½ _).trans_lt hÎµ).neâŸ©
  rw [biInter_gt_closedBall]

theorem eventually_continuousWithinAt_Ici_measure_inter_closedBall_div
    {X : Type*} [PseudoMetricSpace X] [MeasurableSpace X] [OpensMeasurableSpace X]
    {Î¼ : Measure X} [IsLocallyFiniteMeasure Î¼] (x : X) {s : Set X} (hs : NullMeasurableSet s Î¼) :
    âˆ€á¶  r : â„ in ğ“[>] 0,
      ContinuousWithinAt (fun r â†¦ Î¼ (s âˆ© closedBall x r) / Î¼ (closedBall x r)) (Set.Ici r) r := by
  by_cases hâ‚€ : âˆƒ Îµ > 0, Î¼ (closedBall x Îµ) = 0
  case pos =>
    rcases hâ‚€ with âŸ¨Îµ, Îµâ‚€, hÎµâŸ©
    filter_upwards [Ioo_mem_nhdsGT Îµâ‚€] with r hr
    refine (continuousAt_const.congr (f := 0) ?_).continuousWithinAt
    filter_upwards [eventually_lt_nhds hr.2] with r' hr'
    rw [measure_mono_null _ hÎµ, ENNReal.zero_div, Pi.zero_apply]
    exact inter_subset_right.trans (by gcongr)
  case neg =>
    rw [eventually_nhdsWithin_iff]
    filter_upwards [eventually_measure_closedBall_lt_top Î¼ x,
      eventually_forall_le_continuousWithinAt_Ici_measure_closedBall Î¼ x] with r hrâ‚ hrâ‚‚ hrâ‚€
    refine ENNReal.Tendsto.div ?_ (by simp_all) (hrâ‚‚ _ le_rfl) (.inl hrâ‚.ne)
    simp only [inter_comm s, â† Measure.restrict_applyâ‚€' hs]
    exact hrâ‚‚ _ Measure.restrict_le_self

theorem eventually_nhdsWithin_nhds {X : Type*} [TopologicalSpace X] {U : Set X} (hU : IsOpen U)
    {p : X â†’ Prop} {x : X} :
    (âˆ€á¶  y in ğ“[U] x, âˆ€á¶  z in ğ“ y, p z) â†” âˆ€á¶  y in ğ“[U] x, p y := by
  conv_rhs => rw [â† eventually_eventually_nhdsWithin]
  refine eventually_congr <| eventually_mem_nhdsWithin.mono fun y hy â†¦ ?_
  rw [hU.nhdsWithin_eq hy]

/-- Let `e : X â†’ Y` be a dense topological embedding, let `Z` be a regular space.
For each `y : Y`, let `U y` be an open set such that `y âˆˆ closure (U y)`.

Let `s` be an open set, let `x : X` be a point.
Suppose that `f : Y â†’ Z` is continuous within `U y` at all `y âˆˆ s` close to `e x`.
Then `f (e x')` tends to `z` as `x'` tends to `x`, `e x' âˆˆ s`,
if and only if `f y` tends to `z` as `y âˆˆ s` tends to `e x`.

If `X = â„š`, `Y = â„`, and `e = Rat.cast`,
then this lemma can be used to restate convergence of a function defined on real numbers
in terms of convergence of a function on rational numbers,
which is more convenient for measure theory, because there are only countably many rational numbers.
-/
theorem IsDenseEmbedding.tendsto_nhdsWithin_preimage_iff_of_eventually_continuousWithinAt
    {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    [TopologicalSpace Z] [RegularSpace Z]
    {e : X â†’ Y} {s : Set Y} {x : X} {z : Z} {f : Y â†’ Z} {U : Y â†’ Set Y} [âˆ€ y, (ğ“[U y] y).NeBot]
    (he : IsDenseEmbedding e) (hs : IsOpen s) (hU : âˆ€ y, IsOpen (U y))
    (hcont : âˆ€á¶  y in ğ“[s] (e x), ContinuousWithinAt f (U y) y) :
    Tendsto (f âˆ˜ e) (ğ“[e â»Â¹' s] x) (ğ“ z) â†” Tendsto f (ğ“[s] (e x)) (ğ“ z) := by
  refine âŸ¨fun h â†¦ ?mp, fun h â†¦ h.comp ?mprâŸ©
  case mpr =>
    exact he.continuous.continuousWithinAt.tendsto_nhdsWithin (mapsTo_preimage _ _)
  case mp =>
    rw [(closed_nhds_basis z).tendsto_right_iff]
    rintro V âŸ¨hV, hVcâŸ©
    rw [â† tendsto_map'_iff, he.isEmbedding.map_nhdsWithin_eq, image_preimage_eq_inter_range] at h
    replace h := h.eventually (eventually_mem_set.mpr hV)
    rw [nhdsWithin_inter', eventually_inf_principal, â† eventually_nhdsWithin_nhds hs] at h
    filter_upwards [hcont, h] with y hyâ‚ hyâ‚‚
    refine hVc.mem_of_frequently_of_tendsto ?_ hyâ‚
    refine .mp ?_ (eventually_nhdsWithin_of_eventually_nhds hyâ‚‚)
    rw [(nhdsWithin_basis_open _ _).frequently_iff]
    rintro W âŸ¨hyW, hWoâŸ©
    simp only [mem_inter_iff, @and_comm _ (_ âˆˆ range e), exists_range_iff]
    apply he.dense.exists_mem_open (hWo.inter (hU y))
    rw [inter_comm]
    exact nonempty_of_mem <| inter_mem_nhdsWithin _ (hWo.mem_nhds hyW)

theorem tendsto_measure_inter_closedBall_div_iff_rat
    {X : Type*} [PseudoMetricSpace X] [MeasurableSpace X] [OpensMeasurableSpace X]
    {Î¼ : Measure X} [IsLocallyFiniteMeasure Î¼] {x : X} {a : â„â‰¥0âˆ} {s : Set X}
    (hs : NullMeasurableSet s Î¼) :
    Tendsto (fun r â†¦ Î¼ (s âˆ© closedBall x r) / Î¼ (closedBall x r)) (ğ“[>] 0) (ğ“ a) â†”
      Tendsto (fun r : â„š â†¦ Î¼ (s âˆ© closedBall x r) / Î¼ (closedBall x r)) (ğ“[>] 0) (ğ“ a) := by
  rw [â† Rat.cast_zero (Î± := â„), â† Rat.preimage_cast_Ioi (K := â„),
    â† Rat.isDenseEmbedding_coe_real.tendsto_nhdsWithin_preimage_iff_of_eventually_continuousWithinAt
      (U := Ioi), comp_def]
  Â· exact isOpen_Ioi
  Â· exact fun _ â†¦ isOpen_Ioi
  Â· simp only [Rat.cast_zero, continuousWithinAt_Ioi_iff_Ici]
    exact eventually_continuousWithinAt_Ici_measure_inter_closedBall_div _ hs

-- The next 2 lemmas depend on Polish spaces,
-- because they are formulated for a measurable `f`.
-- However, we always apply them to simple functions.
-- We may decide to reformulate in order to reduce dependencies.
-- UPD: I'm not sure if they're true without `SecondCountableTopology` assumption,
-- even for simple functions.
theorem MeasurableSet.setOf_tendsto_measure_sectl_inter_closedBall_div
    {X : Type*} [PseudoMetricSpace X] [SecondCountableTopology X]
    [MeasurableSpace X] [OpensMeasurableSpace X]
    {Î± : Type*} [MeasurableSpace Î±]
    (Î¼ : Measure X) [IsLocallyFiniteMeasure Î¼] [SFinite Î¼]
    {s : Set (X Ã— Î±)} (hs : MeasurableSet s) {f : X Ã— Î± â†’ â„â‰¥0âˆ} (hf : Measurable f) :
    MeasurableSet {p : X Ã— Î± |
      Tendsto (fun r : â„ â†¦ Î¼ ((Â·, p.2) â»Â¹' s âˆ© closedBall p.1 r) / Î¼ (closedBall p.1 r)) (ğ“[>] 0)
        (ğ“ (f p))} := by
  have hm : âˆ€ {a}, MeasurableSet ((Â·, a) â»Â¹' s) := hs.preimage (by measurability)
  simp only [tendsto_measure_inter_closedBall_div_iff_rat hm.nullMeasurableSet]
  have {q} : MeasurableSet {p : X Ã— X Ã— Î± | p.1 âˆˆ closedBall p.2.1 q} := by
    simp only [mem_closedBall]
    apply measurableSet_le
    Â· exact measurable_fst.dist measurable_snd.fst
    Â· exact measurable_const
  refine measurableSet_tendsto_fun (fun q â†¦ .div ?_ (.measure_apply Î¼ _ this)) hf
  refine .measure_apply _ _ ?_
  exact .inter (hs.preimage <| .prodMk measurable_fst measurable_snd.snd) this

theorem MeasurableSet.setOf_tendsto_measure_inter_closedBall_div
    {X : Type*} [PseudoMetricSpace X] [SecondCountableTopology X]
    [MeasurableSpace X] [OpensMeasurableSpace X]
    (Î¼ : Measure X) [IsLocallyFiniteMeasure Î¼] [SFinite Î¼]
    {s : Set X} (hs : MeasurableSet s) {f : X â†’ â„â‰¥0âˆ} (hf : Measurable f) :
    MeasurableSet {x : X |
      Tendsto (fun r â†¦ Î¼ (s âˆ© closedBall x r) / Î¼ (closedBall x r)) (ğ“[>] 0) (ğ“ (f x))} := by
  -- Another option is to apply the previous lemma to the product with `univ : Set Unit`,
  -- but repeating the proof is shorter in this case.
  simp only [tendsto_measure_inter_closedBall_div_iff_rat hs.nullMeasurableSet]
  have H {q} : MeasurableSet {p : X Ã— X | p.1 âˆˆ closedBall p.2 q} :=
    measurableSet_le (measurable_fst.dist measurable_snd) measurable_const
  refine measurableSet_tendsto_fun (fun q â†¦ .div (.measure_apply Î¼ _ ?_) (.measure_apply Î¼ _ H)) hf
  exact .inter (hs.preimage measurable_fst) H

/-- Parametrized version of `ae_tendsto_measure_inter_div_of_measurableSet`. -/
theorem Besicovitch.ae_tendsto_measure_sectl_inter_closedBall_div
    {X : Type*} [MetricSpace X] [SecondCountableTopology X] [HasBesicovitchCovering X]
    [MeasurableSpace X] [BorelSpace X]
    {Î± : Type*} [MeasurableSpace Î±]
    (Î¼ : Measure X) [IsLocallyFiniteMeasure Î¼] [SFinite Î¼] (Î½ : Measure Î±) [SFinite Î½]
    {s : Set (X Ã— Î±)} (hs : MeasurableSet s) :
    âˆ€áµ p âˆ‚Î¼.prod Î½, Tendsto (fun r â†¦ Î¼ ((Â·, p.2) â»Â¹' s  âˆ© closedBall p.1 r) / Î¼ (closedBall p.1 r))
      (ğ“[>] 0) (ğ“ (s.indicator 1 p)) := by
  have H := hs.setOf_tendsto_measure_sectl_inter_closedBall_div Î¼ <|
    (measurable_const (a := 1)).indicator hs
  rw [Measure.ae_prod_iff_ae_ae, Measure.ae_ae_comm] <;> try exact H
  refine .of_forall fun y â†¦ ae_tendsto_measure_inter_div_of_measurableSet Î¼ <| hs.preimage ?_
  measurability

/-- Parametrized version of `ae_tendsto_measure_inter_div_of_measurableSet`. -/
theorem Besicovitch.ae_tendsto_measure_sectr_inter_closedBall_div
    {X : Type*} [MetricSpace X] [SecondCountableTopology X] [HasBesicovitchCovering X]
    [MeasurableSpace X] [BorelSpace X]
    {Î± : Type*} [MeasurableSpace Î±]
    (Î¼ : Measure Î±) (Î½ : Measure X) [IsLocallyFiniteMeasure Î½] [SFinite Î½]
    {s : Set (Î± Ã— X)} (hs : MeasurableSet s) :
    âˆ€áµ p âˆ‚Î¼.prod Î½, Tendsto (fun r â†¦ Î½ ((p.1, Â·) â»Â¹' s  âˆ© closedBall p.2 r) / Î½ (closedBall p.2 r))
      (ğ“[>] 0) (ğ“ (s.indicator 1 p)) := by
  rw [Measure.ae_prod_iff_ae_ae]
  Â· refine .of_forall fun y â†¦ ae_tendsto_measure_inter_div_of_measurableSet Î½ <| hs.preimage ?_
    measurability
  Â· rw [â† measurableSet_swap_iff] at hs âŠ¢
    refine hs.setOf_tendsto_measure_sectl_inter_closedBall_div Î½ ?_
    exact (measurable_const (a := 1)).indicator hs
