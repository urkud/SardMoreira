import SardMoreira.Chart

open scoped unitInterval Topology NNReal
open Asymptotics Filter Set Metric Function MeasureTheory Measure

local notation "dim" => Module.finrank â„

namespace Moreira2001


namespace Chart

universe u v w

variable {E : Type u} {F : Type v} {G : Type w}
  [NormedAddCommGroup E] [NormedSpace â„ E]
  [NormedAddCommGroup F] [NormedSpace â„ F]
  [NormedAddCommGroup G] [NormedSpace â„ G]
  {k : â„•} {Î± : I} {s : Set (E Ã— F)} {a : E Ã— F}

instance (Ïˆ : Chart k Î± s) : MeasurableSpace Ïˆ.Dom := borel _

instance (Ïˆ : Chart k Î± s) : BorelSpace Ïˆ.Dom := âŸ¨rflâŸ©

theorem eventually_contDiffAt_comp {f : E Ã— F â†’ G} {Ïˆ : Chart 1 Î± s} {x : E Ã— Ïˆ.Dom}
    (hx : x âˆˆ Ïˆ.set) (hfk : âˆ€á¶  (y : E Ã— F) in ğ“[s] Ïˆ x, ContDiffMoreiraHolderAt k Î± f y)
    (hk : k â‰  0) :
    âˆ€á¶  y in ğ“ x.2, ContDiffAt â„ 1 (fun y â†¦ f (Ïˆ (x.1, y))) y := by
  have htendsto : Tendsto (fun y â†¦ Ïˆ (x.1, y)) (ğ“ x.2) (ğ“ (Ïˆ x)) :=
    Ïˆ.continuousAt hx |>.comp (continuousAt_const.prodMk continuousAt_id) |>.tendsto
  filter_upwards [htendsto.eventually <|
    hfk.self_of_nhdsWithin (Ïˆ.mapsTo hx) |>.contDiffAt |>.eventually (by simp),
    (continuousAt_const.prodMk continuousAt_id).eventually <|
      Ïˆ.contDiffMoreiraHolderAt hx |>.contDiffAt.eventually (by simp)] with y hfy hÏˆy
  exact .comp _ (hfy.of_le (by simpa [Nat.one_le_iff_ne_zero])) <|
    .comp _ (hÏˆy.of_le (by simp)) (by fun_prop)

theorem eventually_differentiableAt_comp {f : E Ã— F â†’ G} {Ïˆ : Chart 1 Î± s} {x : E Ã— Ïˆ.Dom}
    (hx : x âˆˆ Ïˆ.set) (hfk : âˆ€á¶  (y : E Ã— F) in ğ“[s] Ïˆ x, ContDiffMoreiraHolderAt k Î± f y)
    (hk : k â‰  0) :
    âˆ€á¶  y in ğ“ x.2, DifferentiableAt â„ (fun y â†¦ f (Ïˆ (x.1, y))) y := by
  exact eventually_contDiffAt_comp hx hfk hk |>.mono fun y hy â†¦
    hy.differentiableAt (by simp)

theorem fderivâ‚‚_comp_eventuallyEq {f : E Ã— F â†’ G} (Ïˆ : Chart 1 Î± s) {x : E Ã— Ïˆ.Dom} (hx : x âˆˆ Ïˆ.set)
    (hfk : ContDiffAt â„ k f (Ïˆ x)) (hk : k â‰  0) :
    (fderiv â„ fun y â†¦ f (Ïˆ (x.1, y))) =á¶ [ğ“ x.2] fun y â†¦
      (fderiv â„ f (Ïˆ (x.1, y)) âˆ˜L .inr â„ E F) âˆ˜L (fderiv â„ (fun y â†¦ (Ïˆ (x.1, y)).2) y) := by
  have hdf : âˆ€á¶  y in ğ“ x.2, DifferentiableAt â„ f (Ïˆ (x.1, y)) := by
    have : ContinuousAt (fun y â†¦ Ïˆ (x.1, y)) x.2 := by
      have := Ïˆ.continuousAt hx
      fun_prop
    exact this.eventually <| hfk.eventually (by simp) |>.mono fun y hy â†¦
        hy.differentiableAt (by simpa [Nat.one_le_iff_ne_zero])
  have hdÏˆ : âˆ€á¶  y in ğ“ x.2, DifferentiableAt â„ Ïˆ (x.1, y) := by
    have : ContinuousAt (x.1, Â·) x.2 := by fun_prop
    exact this.eventually <| Ïˆ.eventually_differentiableAt hx (by simp)
  filter_upwards [hdf, hdÏˆ] with y hfy hÏˆy
  rw [â† fderiv_comp_prodMk', â† fderiv_comp (f := fun y â†¦ (Ïˆ (x.1, y)).2)]
  Â· simp [Function.comp_def]
  Â· simp only [Chart.fst_apply]
    rw [â† Chart.prodMk_fst_snd_apply] at hfy
    fun_prop
  Â· fun_prop
  Â· simpa using hfy

theorem step_aux {f : E Ã— F â†’ G} (Ïˆ : Chart 1 Î± s) {x : E Ã— Ïˆ.Dom} (hx : x âˆˆ Ïˆ.set)
    (hfk : ContDiffAt â„ k f (Ïˆ x)) (hk : k â‰  0) :
    (fderiv â„ fun y â†¦ f (Ïˆ (x.1, y))) =O[ğ“ x.2]
      fun y â†¦ (fderiv â„ f (Ïˆ (x.1, y)) âˆ˜L .inr â„ E F) := by
  calc
    _ =á¶ [ğ“ x.2] (fun y â†¦ (fderiv â„ f (Ïˆ (x.1, y)) âˆ˜L .inr â„ E F) âˆ˜L
                  fderiv â„ (fun y â†¦ (Ïˆ (x.1, y)).2) y) := by
      apply Ïˆ.fderivâ‚‚_comp_eventuallyEq hx hfk hk
    _ =O[ğ“ x.2] fun y â†¦ â€–fderiv â„ f (Ïˆ (x.1, y)) âˆ˜L .inr â„ E Fâ€– *
                  â€–fderiv â„ (fun z â†¦ (Ïˆ (x.1, z)).2) yâ€– := by
      refine .of_norm_le fun _ â†¦ ContinuousLinearMap.opNorm_comp_le _ _
    _ =O[ğ“ x.2] fun y â†¦ (fderiv â„ f (Ïˆ (x.1, y)) âˆ˜L .inr â„ E F) := by
      have : ContinuousAt (fderiv â„ (fun z â†¦ (Ïˆ (x.1, z)).2)) x.2 := by
        refine ContDiffAt.continuousAt_fderiv ?_ (n := 1) (by simp)
        have := Ïˆ.contDiffMoreiraHolderAt hx |>.contDiffAt |>.of_le (m := 1) (by simp)
        fun_prop
      refine (isBigO_refl _ _).mul (this.isBigO_one â„).norm_left |>.trans ?_
      simp [isBigO_refl]


end Chart

namespace Atlas

section Aux

universe u v w

variable {E : Type u} {F : Type v} {G : Type (max v w)}
  [NormedAddCommGroup E] [NormedSpace â„ E] [FiniteDimensional â„ E]
  [NormedAddCommGroup F] [NormedSpace â„ F] [FiniteDimensional â„ F]
  [NormedAddCommGroup G] [NormedSpace â„ G]
  {k : â„•} {Î± : I} {s : Set (E Ã— F)} {a : E Ã— F}
  {f : E Ã— F â†’ G} {Ïˆ : Chart 1 Î± s} {x : E Ã— Ïˆ.Dom}

-- TODO: this proof was written before I extracted some lemmas.
-- Reuse them here.
theorem isBigO_main_aux
    (hÏˆ : Ïˆ âˆˆ (main k Î± s).charts) (hx : x âˆˆ Ïˆ.set)
    (hfk : âˆ€á¶  y in ğ“[s] (Ïˆ x), ContDiffMoreiraHolderAt k Î± f y)
    (hfâ‚€ : f =á¶ [ğ“[s] (Ïˆ x)] 0) :
    (fun y â†¦ f (Ïˆ (x.1, y))) =O[ğ“ x.2] (fun y â†¦ â€–y - x.2â€– ^ (k + Î± : â„)) := by
  induction k generalizing F G with
  | zero =>
    calc
      _ =O[ğ“ x.2] (fun y â†¦ â€–Ïˆ (x.1, y) - Ïˆ xâ€– ^ (Î± : â„)) := by
        replace hfk := hfk.self_of_nhdsWithin (Ïˆ.mapsTo hx)
        rw [ContDiffMoreiraHolderAt.zero_left_iff] at hfk
        have := hfk.2.comp_tendsto
          ((Ïˆ.continuousAt hx).comp (continuousAt_const.prodMk continuousAt_id))
        simpa [Function.comp_def, hfâ‚€.self_of_nhdsWithin (Ïˆ.mapsTo hx)] using this
      _ =O[ğ“ x.2] (fun y â†¦ â€–y - x.2â€– ^ (Î± : â„)) := by
        have := Ïˆ.differentiableAt one_ne_zero hx |>.isBigO_sub |>.norm_norm
          |>.rpow Î±.2.1 (by simp [EventuallyLE])
          |>.comp_tendsto (k := (x.1, Â·)) (continuousAt_const.prodMk continuousAt_id)
        simpa [Function.comp_def, Prod.sub_def] using this
      _ = _ := by simp
  | succ k ihk =>
    simp only [main, mem_iUnion, mem_image] at hÏˆ
    rcases hÏˆ with âŸ¨Ïˆ, hÏˆ, Ï†, hÏ†, rflâŸ©
    suffices (fun y â†¦ f (Ïˆ (Ï† (x.1, y)))) =O[ğ“ x.2] fun y â†¦ â€–y - x.2â€– ^ (k + Î± + 1 : â„) by
      simpa [add_right_comm _ (1 : â„)]
    have hmems : Ïˆ (Ï† x) âˆˆ s := Ïˆ.mapsTo (Ï†.mapsTo hx).1
    have hÏˆ_tendsto : Tendsto Ïˆ (ğ“[Ïˆ.set] (Ï† x)) (ğ“[s] (Ïˆ (Ï† x))) :=
      .inf (Ïˆ.continuousAt (Ï†.mapsTo hx).1) Ïˆ.mapsTo.tendsto
    have hkey :
        (fun y â†¦ fderiv â„ (f âˆ˜ Ïˆ) (Ï† (x.1, y)) âˆ˜L .inr â„ E Ïˆ.Dom) =O[ğ“ x.2]
          (fun y â†¦ â€–y - x.2â€– ^ (k + Î± : â„)) := by
      refine ihk (f := fun z â†¦ fderiv â„ (f âˆ˜ Ïˆ) z âˆ˜L .inr â„ _ _) hÏ† hx ?_ ?_
      Â· filter_upwards [eventually_mem_nhdsWithin,
          (hÏˆ_tendsto.eventually hfk).filter_mono (nhdsWithin_mono _ (sep_subset _ _))]
          with y hy hfy
        refine (hfy.comp ?_ k.succ_ne_zero).fderiv le_rfl |>.continuousLinearMap_comp
          (.precomp _ (.inr â„ E Ïˆ.Dom))
        exact Ïˆ.contDiffMoreiraHolderAt hy.1
      Â· rw [EventuallyEq, eventually_nhdsWithin_iff]
        replace hfk := hÏˆ_tendsto.eventually hfk
        rw [eventually_nhdsWithin_iff, â† eventually_eventually_nhds] at hfk
        replace hfâ‚€ := hÏˆ_tendsto.eventually hfâ‚€
        rw [eventually_nhdsWithin_iff, â† eventually_eventually_nhds] at hfâ‚€
        filter_upwards [hfk, hfâ‚€] with y hy_contDiff hyâ‚€ hy_mem
        apply hy_mem.2.fderiv_comp_inr_eq_zero
        Â· rw [eventually_nhdsWithin_iff]
          filter_upwards [hy_contDiff] with z hz hz_mem
          exact .comp (hz hz_mem) (Ïˆ.contDiffMoreiraHolderAt hz_mem) k.succ_ne_zero
        Â· rwa [EventuallyEq, eventually_nhdsWithin_iff]
    have hÏ†diff : âˆ€á¶  y in ğ“ x.2, DifferentiableAt â„ Ï† (x.1, y) := by
      refine Continuous.prodMk_right _ |>.tendsto _ |>.eventually ?_
      exact Ï†.contDiffMoreiraHolderAt hx |>.contDiffAt |>.eventually (by simp)
        |>.mono fun y hy â†¦ hy.differentiableAt (by simp)
    have hÏˆdiff : âˆ€á¶  y in ğ“ x.2, DifferentiableAt â„ Ïˆ (Ï† (x.1, y)) := by
      have := (Ï†.continuousAt hx).comp (continuousAt_const.prodMk continuousAt_id)
      refine this.eventually ?_
      exact Ïˆ.contDiffMoreiraHolderAt (Ï†.mapsTo hx).1 |>.contDiffAt |>.eventually (by simp)
        |>.mono fun y hy â†¦ hy.differentiableAt (by simp)
    have hfdiff : âˆ€á¶  y in ğ“ x.2, DifferentiableAt â„ f (Ïˆ (Ï† (x.1, y))) := by
      have : ContinuousAt (fun y â†¦ Ïˆ (Ï† (x.1, y))) x.2 := by
        have := hÏˆdiff.self_of_nhds.continuousAt
        have := Ï†.continuousAt hx
        fun_prop
      refine this.eventually ?_
      exact hfk.self_of_nhdsWithin hmems |>.contDiffAt |>.eventually (by simp)
        |>.mono fun y hy â†¦ hy.differentiableAt (by simp)
    apply isBigO_norm_rpow_add_one_of_fderiv_of_apply_eq_zero
    Â· exact add_nonneg k.cast_nonneg Î±.2.1
    Â· filter_upwards [hÏ†diff, hÏˆdiff, hfdiff] with y hÏ†y hÏˆy hfy
      exact .comp _ hfy <| .comp _ hÏˆy <| .comp _ hÏ†y (by fun_prop)
    Â· refine (Ï†.step_aux (f := (f <| Ïˆ Â·)) (k := k + 1) hx ?_ (by simp)).trans hkey
      refine .comp _ ?_ ?_
      Â· exact hfk.self_of_nhdsWithin (Chart.mapsTo _ hx) |>.contDiffAt
      Â· exact Ïˆ.contDiffAt (Ï†.mapsTo hx).1
    Â· exact hfâ‚€.self_of_nhdsWithin hmems

end Aux

universe u v w

variable {E : Type u} {F : Type v} {G : Type w}
  [NormedAddCommGroup E] [NormedSpace â„ E] [FiniteDimensional â„ E]
  [NormedAddCommGroup F] [NormedSpace â„ F] [FiniteDimensional â„ F]
  [NormedAddCommGroup G] [NormedSpace â„ G]
  {k : â„•} {Î± : I} {s : Set (E Ã— F)} {a : E Ã— F}
  {f : E Ã— F â†’ G} {Ïˆ : Chart 1 Î± s} {x : E Ã— Ïˆ.Dom}

theorem isBigO_main_inr
    (hÏˆ : Ïˆ âˆˆ (main k Î± s).charts) (hx : x âˆˆ Ïˆ.set)
    (hfk : âˆ€á¶  y in ğ“[s] (Ïˆ x), ContDiffMoreiraHolderAt k Î± f y)
    (hfâ‚€ : f =á¶ [ğ“[s] (Ïˆ x)] 0) :
    (fun y â†¦ f (Ïˆ (x.1, y))) =O[ğ“ x.2] (fun y â†¦ â€–y - x.2â€– ^ (k + Î± : â„)) := by
  set e : G â‰ƒL[â„] ULift.{v} G := .symm âŸ¨ULift.moduleEquiv, by fun_prop, by fun_propâŸ©
  set g : E Ã— F â†’ ULift.{v} G := e âˆ˜ f
  have hgk : âˆ€á¶  y in ğ“[s] (Ïˆ x), ContDiffMoreiraHolderAt k Î± g y :=
    hfk.mono fun _ â†¦ e.contDiffMoreiraHolderAt_left_comp.mpr
  have hgâ‚€ : g =á¶ [ğ“[s] (Ïˆ x)] 0 := hfâ‚€.fun_comp e
  refine .trans ?_ (isBigO_main_aux hÏˆ hx hgk hgâ‚€)
  apply e.isBigO_comp_rev

theorem isBigO_main_sub_of_fderiv_zero_right
    (hÏˆ : Ïˆ âˆˆ (main k Î± s).charts) (hx : x âˆˆ Ïˆ.set)
    (hfk : âˆ€á¶  y in ğ“[s] (Ïˆ x), ContDiffMoreiraHolderAt (k + 1) Î± f y)
    (hfâ‚€ : (fderiv â„ f Â· âˆ˜L .inr â„ E F) =á¶ [ğ“[s] (Ïˆ x)] 0) :
    (fun y â†¦ f (Ïˆ (x.1, y)) - f (Ïˆ x)) =O[ğ“ x.2] (fun y â†¦ â€–y - x.2â€– ^ (k + 1 + Î± : â„)) := by
  rw [add_right_comm]
  apply sub_isBigO_norm_rpow_add_one_of_fderiv (f := fun y â†¦ f (Ïˆ (x.1, y)))
  Â· exact add_nonneg k.cast_nonneg Î±.2.1
  Â· exact Ïˆ.eventually_differentiableAt_comp hx hfk (by simp)
  Â· have hcontDiff := (hfk.self_of_nhdsWithin (Ïˆ.mapsTo hx)).contDiffAt
    refine .trans ?_ (isBigO_main_inr hÏˆ hx (hfk.mono fun y hy â†¦ ?_) hfâ‚€)
    apply Ïˆ.step_aux hx hcontDiff (by simp)
    exact (hy.fderiv le_rfl).continuousLinearMap_comp (.precomp _ (.inr â„ E F))

theorem isLittleO_main_sub_of_fderiv_zero_right
    (hÏˆ : Ïˆ âˆˆ (main k Î± s).charts) (hx : x âˆˆ Ïˆ.set)
    (hfk : âˆ€á¶  y in ğ“[s] (Ïˆ x), ContDiffMoreiraHolderAt (k + 1) Î± f y)
    (hfâ‚€ : (fderiv â„ f Â· âˆ˜L .inr â„ E F) =á¶ [ğ“[s] (Ïˆ x)] 0)
    (hdensity : Tendsto (fun r â†¦
      Î¼H[dim Ïˆ.Dom] ({y | (x.1, y) âˆˆ closure Ïˆ.set} âˆ© closedBall x.2 r) /
        Î¼H[â†‘(dim Ïˆ.Dom)] (closedBall x.2 r))
      (ğ“[>] 0) (ğ“ 1)) :
    (fun y â†¦ f (Ïˆ (x.1, y)) - f (Ïˆ x)) =o[ğ“ x.2] (fun y â†¦ â€–y - x.2â€– ^ (k + 1 + Î± : â„)) := by
  rw [add_right_comm]
  have htendsto : Tendsto (fun y â†¦ Ïˆ (x.1, y)) (ğ“ x.2) (ğ“ (Ïˆ x)) :=
    Ïˆ.continuousAt hx |>.comp (continuousAt_const.prodMk continuousAt_id) |>.tendsto
  have hcontDiff := (hfk.self_of_nhdsWithin (Ïˆ.mapsTo hx)).contDiffAt
  apply sub_isLittleO_norm_rpow_add_one_of_fderiv_of_density_point
    (f := fun y â†¦ f (Ïˆ (x.1, y))) (Î¼ := Î¼H[dim Ïˆ.Dom]) (s := {y | (x.1, y) âˆˆ closure Ïˆ.set})
  Â· exact add_nonneg k.cast_nonneg Î±.2.1
  Â· exact Ïˆ.eventually_differentiableAt_comp hx hfk (by simp)
  Â· refine .trans ?_ (isBigO_main_inr hÏˆ hx (hfk.mono fun y hy â†¦ ?_) hfâ‚€)
    apply Ïˆ.step_aux hx hcontDiff (by simp)
    exact (hy.fderiv le_rfl).continuousLinearMap_comp (.precomp _ (.inr â„ E F))
  Â· replace hfâ‚€ : âˆ€á¶  y in ğ“ x.2, (x.1, y) âˆˆ closure Ïˆ.set â†’
        fderiv â„ f (Ïˆ (x.1, y)) âˆ˜L .inr â„ E F = 0 := by
      have Hâ‚ : âˆ€á¶  y in ğ“ x.2, ContinuousAt (fderiv â„ f Â· âˆ˜L .inr â„ E F) (Ïˆ (x.1, y)) := by
        refine htendsto.eventually ?_
        refine hcontDiff.eventually (by simp) |>.mono fun y hy â†¦ ?_
        simp only [â† ContinuousLinearMap.precomp_apply]
        exact .comp (by fun_prop) (hy.continuousAt_fderiv (by simp))
      have Hâ‚‚ := htendsto.eventually
        (eventually_eventually_nhds.mpr <| eventually_nhdsWithin_iff.mp hfâ‚€)
      have Hâ‚ƒ : âˆ€á¶  y in ğ“ x.2, ContinuousAt Ïˆ (x.1, y) := by
        refine (show ContinuousAt (x.1, Â·) x.2 by fun_prop).eventually ?_
        exact Ïˆ.contDiffAt hx |>.eventually (by simp) |>.mono fun y hy â†¦ hy.continuousAt
      filter_upwards [Hâ‚, Hâ‚‚, Hâ‚ƒ]
      intro y hycont hy hÏˆcont hyclos
      rw [mem_closure_iff_nhdsWithin_neBot] at hyclos
      refine tendsto_nhds_unique_of_eventuallyEq (l := ğ“[Ïˆ.set] (x.1, y))
        (f := (fun y â†¦ fderiv â„ f (Ïˆ y) âˆ˜L .inr â„ E F)) ?_ tendsto_const_nhds ?_
      Â· exact hycont.comp_continuousWithinAt hÏˆcont.continuousWithinAt
      Â· rw [EventuallyEq, eventually_nhdsWithin_iff]
        filter_upwards [hÏˆcont.eventually hy] with z hz hÏˆz using hz (Ïˆ.mapsTo hÏˆz)
    rw [EventuallyEq, eventually_nhdsWithin_iff]
    filter_upwards [Ïˆ.fderivâ‚‚_comp_eventuallyEq hx hcontDiff (by simp), hfâ‚€] with y hy_eq hy hy_mem
    rw [hy_eq, hy hy_mem]
    simp
  Â· exact hdensity

end Moreira2001.Atlas
