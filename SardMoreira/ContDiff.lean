import Mathlib.Analysis.Calculus.ContDiff.Basic
import SardMoreira.ContinuousMultilinearMap

open scoped unitInterval Topology NNReal
open Function Asymptotics Filter Set

variable {ğ•œ E F G : Type*} [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  [NormedAddCommGroup G] [NormedSpace ğ•œ G]
  {f : E â†’ F} {s : Set E} {n : WithTop â„•âˆ} {k : â„•} {a : E}

protected alias UniqueDiffOn.univ := uniqueDiffOn_univ

theorem Asymptotics.IsBigO.of_norm_eventuallyLE {Î± : Type*} {l : Filter Î±} {f : Î± â†’ E}
    {g : Î± â†’ â„} (h : (â€–f Â·â€–) â‰¤á¶ [l] g) : f =O[l] g :=
  .of_bound' <| h.mono fun _ h â†¦ h.trans <| le_abs_self _

theorem Asymptotics.IsBigO.of_norm_le {Î± : Type*} {l : Filter Î±} {f : Î± â†’ E}
    {g : Î± â†’ â„} (h : âˆ€ x, â€–f xâ€– â‰¤ g x) : f =O[l] g :=
  .of_norm_eventuallyLE <| .of_forall h

theorem Asymptotics.IsBigO.finsetProd {Î± Î¹ R K : Type*} [SeminormedCommRing R] [NormedField K]
    {l : Filter Î±} {s : Finset Î¹} {f : Î¹ â†’ Î± â†’ R} {g : Î¹ â†’ Î± â†’ K}
    (hf : âˆ€ i âˆˆ s, f i =O[l] g i) : (âˆ i âˆˆ s, f i Â·) =O[l] (âˆ i âˆˆ s, g i Â·) := by
  induction s using Finset.cons_induction with
  | empty => simp [isBoundedUnder_const]
  | cons i s hi ihs =>
    simp only [Finset.prod_cons, Finset.forall_mem_cons] at hf âŠ¢
    exact hf.1.mul (ihs hf.2)

@[simp]
theorem Prod.norm_mk {E F : Type*} [Norm E] [Norm F] (a : E) (b : F) : â€–(a, b)â€– = max â€–aâ€– â€–bâ€– := rfl

protected theorem UniqueDiffOn.frequently_smallSets {s : Set E} (hs : UniqueDiffOn ğ•œ s) (a : E) :
    âˆƒá¶  t in (ğ“[s] a).smallSets, t âˆˆ ğ“[s] a âˆ§ UniqueDiffOn ğ•œ t := by
  rw [(nhdsWithin_basis_open _ _).smallSets.frequently_iff]
  exact fun U âŸ¨haU, hUoâŸ© â†¦ âŸ¨s âˆ© U, (inter_comm _ _).le,
    inter_mem_nhdsWithin _ (hUo.mem_nhds haU), hs.inter hUoâŸ©

theorem ContDiffOn.continuousAt_iteratedFDerivWithin (hf : ContDiffOn ğ•œ n f s)
    (hs : UniqueDiffOn ğ•œ s) (ha : s âˆˆ ğ“ a) (hk : k â‰¤ n) :
    ContinuousAt (iteratedFDerivWithin ğ•œ k f s) a :=
  (hf.continuousOn_iteratedFDerivWithin hk hs).continuousAt ha

theorem ContDiffWithinAt.continuousWithinAt_iteratedFDerivWithin (hf : ContDiffWithinAt ğ•œ n f s a)
    (hs : UniqueDiffOn ğ•œ s) (ha : a âˆˆ s) (hk : k â‰¤ n) :
    ContinuousWithinAt (iteratedFDerivWithin ğ•œ k f s) s a :=
  (hf.iteratedFderivWithin_right hs (by rwa [zero_add]) ha).continuousWithinAt

theorem ContDiffAt.continuousAt_iteratedFDeriv (hf : ContDiffAt ğ•œ n f a) (hk : k â‰¤ n) :
    ContinuousAt (iteratedFDeriv ğ•œ k f) a := by
  simp only [â† continuousWithinAt_univ, â† iteratedFDerivWithin_univ]
  exact hf.contDiffWithinAt.continuousWithinAt_iteratedFDerivWithin uniqueDiffOn_univ trivial hk

theorem ContDiffAt.differentiableAt_iteratedFDeriv (hf : ContDiffAt ğ•œ n f a) (hk : k < n) :
    DifferentiableAt ğ•œ (iteratedFDeriv ğ•œ k f) a := by
  simp only [â† differentiableWithinAt_univ, â† iteratedFDerivWithin_univ]
  exact hf.differentiableWithinAt_iteratedFDerivWithin hk (by simp [uniqueDiffOn_univ])

/-- Generalizes `ContinuousLinearMap.iteratedFderivWithin_comp_left`
by weakening a `ContDiffOn` assumption to `ContDiffWithinAt`.  -/
theorem ContinuousLinearMap.iteratedFDerivWithin_comp_left' (g : F â†’L[ğ•œ] G)
    (hf : ContDiffWithinAt ğ•œ n f s a) (hs : UniqueDiffOn ğ•œ s) (ha : a âˆˆ s) {i : â„•} (hi : i â‰¤ n) :
    iteratedFDerivWithin ğ•œ i (g âˆ˜ f) s a =
      g.compContinuousMultilinearMap (iteratedFDerivWithin ğ•œ i f s a) := by
  rcases hf.contDiffOn' hi (by simp) with âŸ¨U, hU, haU, hfUâŸ©
  rw [â† iteratedFDerivWithin_inter_open hU haU, â† iteratedFDerivWithin_inter_open (f := f) hU haU]
  rw [insert_eq_of_mem ha] at hfU
  exact .symm <| (hfU.ftaylorSeriesWithin (hs.inter hU)).continuousLinearMap_comp g
    |>.eq_iteratedFDerivWithin_of_uniqueDiffOn le_rfl (hs.inter hU) âŸ¨ha, haUâŸ©

/-- Generalizes `ContinuousLinearMap.iteratedFderiv_comp_left`
by weakening a `ContDiff` assumption to `ContDiffAt`.  -/
theorem ContinuousLinearMap.iteratedFDeriv_comp_left' (g : F â†’L[ğ•œ] G) (hf : ContDiffAt ğ•œ n f a)
    {i : â„•} (hi : i â‰¤ n) :
    iteratedFDeriv ğ•œ i (g âˆ˜ f) a = g.compContinuousMultilinearMap (iteratedFDeriv ğ•œ i f a) := by
  simp only [â† iteratedFDerivWithin_univ]
  exact g.iteratedFDerivWithin_comp_left' hf.contDiffWithinAt .univ (mem_univ _) hi

theorem iteratedFDerivWithin_prodMk {f : E â†’ F} {g : E â†’ G} (hf : ContDiffWithinAt ğ•œ n f s a)
    (hg : ContDiffWithinAt ğ•œ n g s a) (hs : UniqueDiffOn ğ•œ s) (ha : a âˆˆ s) {i : â„•} (hi : i â‰¤ n) :
    iteratedFDerivWithin ğ•œ i (fun x â†¦ (f x, g x)) s a =
      (iteratedFDerivWithin ğ•œ i f s a).prod (iteratedFDerivWithin ğ•œ i g s a) := by
  rw [ContinuousMultilinearMap.eq_prod_iff,
    â† ContinuousLinearMap.iteratedFDerivWithin_comp_left' _ (hf.prod hg) hs ha hi,
    â† ContinuousLinearMap.iteratedFDerivWithin_comp_left' _ (hf.prod hg) hs ha hi]
  exact âŸ¨rfl, rflâŸ©

theorem iteratedFDeriv_prodMk {f : E â†’ F} {g : E â†’ G} (hf : ContDiffAt ğ•œ n f a)
    (hg : ContDiffAt ğ•œ n g a) {i : â„•} (hi : i â‰¤ n) :
    iteratedFDeriv ğ•œ i (fun x â†¦ (f x, g x)) a =
      (iteratedFDeriv ğ•œ i f a).prod (iteratedFDeriv ğ•œ i g a) := by
  simp only [â† iteratedFDerivWithin_univ]
  exact iteratedFDerivWithin_prodMk hf.contDiffWithinAt hg.contDiffWithinAt .univ (mem_univ _) hi

theorem ContDiffWithinAt.eventually_hasFTaylorSeriesUpToOn {f : E â†’ F} {s : Set E} {a : E}
    (h : ContDiffWithinAt ğ•œ n f s a) (hs : UniqueDiffOn ğ•œ s) (ha : a âˆˆ s) {m : â„•} (hm : m â‰¤ n) :
    âˆ€á¶  t in (ğ“[s] a).smallSets, HasFTaylorSeriesUpToOn m f (ftaylorSeriesWithin ğ•œ f s) t := by
  rcases h.contDiffOn' hm (by simp) with âŸ¨U, hUo, haU, hfUâŸ©
  have : âˆ€á¶  t in (ğ“[s] a).smallSets, t âŠ† s âˆ© U := by
    rw [eventually_smallSets_subset]
    exact inter_mem_nhdsWithin _ <| hUo.mem_nhds haU
  refine this.mono fun t ht â†¦ .mono ?_ ht
  rw [insert_eq_of_mem ha] at hfU
  refine (hfU.ftaylorSeriesWithin (hs.inter hUo)).congr_series fun k hk x hx â†¦ ?_
  exact iteratedFDerivWithin_inter_open hUo hx.2

theorem iteratedFDerivWithin_comp_of_eventually
    {g : F â†’ G} {f : E â†’ F} {t : Set F} {s : Set E} {a : E}
    (hg : ContDiffWithinAt ğ•œ n g t (f a)) (hf : ContDiffWithinAt ğ•œ n f s a)
    (ht : UniqueDiffOn ğ•œ t) (hs : UniqueDiffOn ğ•œ s) (ha : a âˆˆ s) (hst : âˆ€á¶  x in ğ“[s] a, f x âˆˆ t)
    {i : â„•} (hi : i â‰¤ n) :
    iteratedFDerivWithin ğ•œ i (g âˆ˜ f) s a =
      (ftaylorSeriesWithin ğ•œ g t (f a)).taylorComp (ftaylorSeriesWithin ğ•œ f s a) i := by
  have hat : f a âˆˆ t := hst.self_of_nhdsWithin ha
  have hf_tendsto : Tendsto f (ğ“[s] a) (ğ“[t] (f a)) :=
    tendsto_nhdsWithin_iff.mpr âŸ¨hf.continuousWithinAt, hstâŸ©
  have Hâ‚ : âˆ€á¶  u in (ğ“[s] a).smallSets, u âŠ† s :=
    eventually_smallSets_subset.mpr eventually_mem_nhdsWithin
  have Hâ‚‚ : âˆ€á¶  u in (ğ“[s] a).smallSets, HasFTaylorSeriesUpToOn i f (ftaylorSeriesWithin ğ•œ f s) u :=
    hf.eventually_hasFTaylorSeriesUpToOn hs ha hi
  have Hâ‚ƒ := hf_tendsto.image_smallSets.eventually
    (hg.eventually_hasFTaylorSeriesUpToOn ht hat hi)
  rcases ((hs.frequently_smallSets _).and_eventually (Hâ‚.and <| Hâ‚‚.and Hâ‚ƒ)).exists
    with âŸ¨u, âŸ¨hau, huâŸ©, hus, hfu, hguâŸ©
  refine .symm <| (hgu.comp hfu (mapsTo_image _ _)).eq_iteratedFDerivWithin_of_uniqueDiffOn le_rfl
    hu (mem_of_mem_nhdsWithin ha hau) |>.trans ?_
  refine iteratedFDerivWithin_congr_set (hus.eventuallyLE.antisymm ?_) _
  exact set_eventuallyLE_iff_mem_inf_principal.mpr hau

theorem iteratedFDerivWithin_comp {g : F â†’ G} {f : E â†’ F} {t : Set F} {s : Set E} {a : E}
    (hg : ContDiffWithinAt ğ•œ n g t (f a)) (hf : ContDiffWithinAt ğ•œ n f s a)
    (ht : UniqueDiffOn ğ•œ t) (hs : UniqueDiffOn ğ•œ s) (ha : a âˆˆ s) (hst : MapsTo f s t)
    {i : â„•} (hi : i â‰¤ n) :
    iteratedFDerivWithin ğ•œ i (g âˆ˜ f) s a =
      (ftaylorSeriesWithin ğ•œ g t (f a)).taylorComp (ftaylorSeriesWithin ğ•œ f s a) i :=
  iteratedFDerivWithin_comp_of_eventually hg hf ht hs ha (eventually_mem_nhdsWithin.mono hst) hi

theorem iteratedFDeriv_comp {g : F â†’ G} {f : E â†’ F} {a : E} (hg : ContDiffAt ğ•œ n g (f a))
    (hf : ContDiffAt ğ•œ n f a) {i : â„•} (hi : i â‰¤ n) :
    iteratedFDeriv ğ•œ i (g âˆ˜ f) a =
      (ftaylorSeries ğ•œ g (f a)).taylorComp (ftaylorSeries ğ•œ f a) i := by
  simp only [â† iteratedFDerivWithin_univ, â† ftaylorSeriesWithin_univ]
  exact iteratedFDerivWithin_comp hg.contDiffWithinAt hf.contDiffWithinAt .univ .univ (mem_univ _)
    (mapsTo_univ _ _) hi

namespace OrderedFinpartition

variable {n : â„•} (c : OrderedFinpartition n)

/-- Cover `[0, n)`, `n â‰  0`, by a single subset. -/
@[simps (config := .asFn)]
def single (n : â„•) (hn : n â‰  0) : OrderedFinpartition n where
  length := 1
  partSize _ := n
  partSize_pos _ := hn.bot_lt
  emb _ := id
  emb_strictMono _ := strictMono_id
  parts_strictMono := Subsingleton.strictMono _
  disjoint := subsingleton_univ.pairwise _
  cover x := âŸ¨0, x, rflâŸ©

@[simp]
theorem applyOrderedFinpartition_single (hn : n â‰  0)
    (p : âˆ€ i : Fin (single n hn).length, E [Ã—(single n hn).partSize i]â†’L[ğ•œ] F)
    (m : Fin n â†’ E) (i : Fin (single n hn).length) :
    (single n hn).applyOrderedFinpartition p m i = p i m :=
  rfl

@[simp]
theorem sum_partSize : âˆ‘ i, c.partSize i = n := calc
  âˆ‘ i, c.partSize i = Fintype.card (Î£ i, Fin (c.partSize i)) := by simp
  _ = n := by rw [Fintype.card_congr c.equivSigma, Fintype.card_fin]

@[simp]
theorem length_eq_zero : c.length = 0 â†” n = 0 := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ nonpos_iff_eq_zero.mp <| h â–¸ c.length_leâŸ©
  rw [â† c.sum_partSize, Finset.sum_eq_zero]
  simp [(c.partSize_pos _).ne', h]

@[simp] theorem length_pos_iff : 0 < c.length â†” 0 < n := by simp [pos_iff_ne_zero]

theorem length_eq_one_iff (hn : n â‰  0) : c.length = 1 â†” c = single n hn := by
  refine âŸ¨fun hc â†¦ ?_, fun h â†¦ h â–¸ rflâŸ©
  have hsum := c.sum_partSize
  cases' c with length partSize partSize_pos emb emb_strictMono parts_strictMono disjoint cover
  subst hc
  obtain rfl : partSize = fun _ â†¦ n := by
    rw [funext_iff, Fin.forall_fin_one]
    simpa using hsum
  obtain rfl : emb = fun _ â†¦ id := by
    rw [funext_iff, Fin.forall_fin_one, â† (emb_strictMono 0).range_inj strictMono_id]
    simpa [eq_univ_iff_forall, Fin.exists_fin_one] using cover
  rfl

theorem length_eq_one_iff_exists : c.length = 1 â†” âˆƒ h, c = single n h := by
  refine âŸ¨fun hc â†¦ ?_, fun âŸ¨_, hâŸ© â†¦ h â–¸ rflâŸ©
  suffices n â‰  0 from âŸ¨this, (c.length_eq_one_iff this).mp hcâŸ©
  simp [â† c.length_eq_zero, hc]

theorem partSize_eq_iff_length_eq_one (i : Fin c.length) : c.partSize i = n â†” c.length = 1 := by
  constructor
  Â· intro h
    by_contra h'
    have : Nontrivial (Fin c.length) := by
      rw [Fin.nontrivial_iff_two_le]
      have := i.is_lt
      omega
    rcases exists_ne i with âŸ¨j, hjâŸ©
    refine h.not_lt <| LT.lt.trans_eq ?_ c.sum_partSize
    exact Finset.single_lt_sum hj (Finset.mem_univ _) (Finset.mem_univ _) (c.partSize_pos _)
      (by simp)
  Â· rw [length_eq_one_iff_exists]
    rintro âŸ¨h, rflâŸ©
    rfl

theorem partSize_eq_iff_eq_single (i : Fin c.length) :
    c.partSize i = n â†” c = single n (i.is_lt.trans_le c.length_le).ne_bot := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· rwa [c.partSize_eq_iff_length_eq_one i, length_eq_one_iff] at h
  Â· generalize_proofs at h
    subst h
    rfl


theorem length_eq_iff : c.length = n â†” c = atomic n := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ h â–¸ rflâŸ©
  have Hâ‚€ := c.sum_partSize
  cases' c with length partSize partSize_pos emb emb_strictMono parts_strictMono disjoint cover
  dsimp at *
  subst h
  obtain rfl : partSize = fun _ â†¦ 1 := by
    suffices âˆ€ i âˆˆ Finset.univ, 1 = partSize i by simpa [eq_comm, funext_iff] using this
    rw [â† Finset.sum_eq_sum_iff_of_le]
    Â· simp [Hâ‚€]
    Â· exact fun i _ â†¦ partSize_pos i
  obtain rfl : emb = fun i _ â†¦ i := by
    suffices âˆ€ i, emb i 0 = i by
      ext i j : 2
      convert this i
    rw [â† funext_iff, â† StrictMono.range_inj, Surjective.range_eq, Surjective.range_eq]
    exacts [surjective_id, Finite.surjective_of_injective parts_strictMono.injective,
      parts_strictMono, strictMono_id]
  rfl

theorem length_lt_iff : c.length < n â†” c â‰  atomic n := by
  rw [c.length_le.lt_iff_ne]
  exact c.length_eq_iff.not

theorem norm_compAlongOrderedFinpartitionL_apply_le (f : F [Ã—c.length]â†’L[ğ•œ] G) :
    â€–c.compAlongOrderedFinpartitionL ğ•œ E F G fâ€– â‰¤ â€–fâ€– :=
  (ContinuousLinearMap.le_of_opNorm_le _ c.norm_compAlongOrderedFinpartitionL_le f).trans_eq
    (one_mul _)

theorem norm_compAlongOrderedFinpartition_sub_compAlongOrderedFinpartition_le
    (fâ‚ fâ‚‚ : F [Ã—c.length]â†’L[ğ•œ] G) (gâ‚ gâ‚‚ : âˆ€ i, E [Ã—c.partSize i]â†’L[ğ•œ] F) :
    â€–c.compAlongOrderedFinpartition fâ‚ gâ‚ - c.compAlongOrderedFinpartition fâ‚‚ gâ‚‚â€– â‰¤
      â€–fâ‚â€– * c.length * max â€–gâ‚â€– â€–gâ‚‚â€– ^ (c.length - 1) * â€–gâ‚ - gâ‚‚â€– + â€–fâ‚ - fâ‚‚â€– * âˆ i, â€–gâ‚‚ iâ€– := calc
  _ â‰¤ â€–c.compAlongOrderedFinpartition fâ‚ gâ‚ - c.compAlongOrderedFinpartition fâ‚ gâ‚‚â€– +
      â€–c.compAlongOrderedFinpartition fâ‚ gâ‚‚ - c.compAlongOrderedFinpartition fâ‚‚ gâ‚‚â€– :=
    norm_sub_le_norm_sub_add_norm_sub ..
  _ â‰¤ â€–fâ‚â€– * c.length * max â€–gâ‚â€– â€–gâ‚‚â€– ^ (c.length - 1) * â€–gâ‚ - gâ‚‚â€– + â€–fâ‚ - fâ‚‚â€– * âˆ i, â€–gâ‚‚ iâ€– := by
    gcongr
    Â· refine ((c.compAlongOrderedFinpartitionL ğ•œ E F G fâ‚).norm_image_sub_le gâ‚ gâ‚‚).trans ?_
      simp only [Fintype.card_fin]
      gcongr
      apply norm_compAlongOrderedFinpartitionL_apply_le
    Â· exact c.norm_compAlongOrderedFinpartition_le (fâ‚ - fâ‚‚) gâ‚‚

end OrderedFinpartition

theorem FormalMultilinearSeries.taylorComp_sub_taylorComp_isBigO
    {Î± : Type*} {l : Filter Î±} {pâ‚ pâ‚‚ : Î± â†’ FormalMultilinearSeries ğ•œ F G}
    {qâ‚ qâ‚‚ : Î± â†’ FormalMultilinearSeries ğ•œ E F} {f : Î± â†’ â„} {n : â„•}
    (hp_bdd : âˆ€ k â‰¤ n, l.IsBoundedUnder (Â· â‰¤ Â·) (â€–pâ‚ Â· kâ€–))
    (hpf : âˆ€ k â‰¤ n, (fun x â†¦ pâ‚ x k - pâ‚‚ x k) =O[l] f)
    (hqâ‚_bdd : âˆ€ k â‰¤ n, l.IsBoundedUnder (Â· â‰¤ Â·) (â€–qâ‚ Â· kâ€–))
    (hqâ‚‚_bdd : âˆ€ k â‰¤ n, l.IsBoundedUnder (Â· â‰¤ Â·) (â€–qâ‚‚ Â· kâ€–))
    (hqf : âˆ€ k â‰¤ n, (fun x â†¦ qâ‚ x k - qâ‚‚ x k) =O[l] f) :
    (fun x â†¦ (pâ‚ x).taylorComp (qâ‚ x) n - (pâ‚‚ x).taylorComp (qâ‚‚ x) n) =O[l] f := by
  simp only [FormalMultilinearSeries.taylorComp, â† Finset.sum_sub_distrib]
  refine .sum fun c _ â†¦ ?_
  refine .trans (.of_norm_le fun _ â†¦
    c.norm_compAlongOrderedFinpartition_sub_compAlongOrderedFinpartition_le ..) ?_
  refine .add ?_ ?_
  Â· have Hâ‚ : (pâ‚ Â· c.length) =O[l] (1 : Î± â†’ â„) := (hp_bdd _ c.length_le).isBigO_one â„
    have Hâ‚‚ : âˆ€ m, (qâ‚ Â· (c.partSize m)) =O[l] (1 : Î± â†’ â„) := fun m â†¦
      (hqâ‚_bdd _ <| c.partSize_le _).isBigO_one â„
    have Hâ‚ƒ : âˆ€ m, (qâ‚‚ Â· (c.partSize m)) =O[l] (1 : Î± â†’ â„) := fun m â†¦
      (hqâ‚‚_bdd _ <| c.partSize_le _).isBigO_one â„
    have Hâ‚„ : âˆ€ m, (fun x â†¦ qâ‚ x (c.partSize m) - qâ‚‚ x (c.partSize m)) =O[l] f := fun m â†¦
      hqf _ <| c.partSize_le _
    rw [â† isBigO_pi] at Hâ‚‚ Hâ‚ƒ Hâ‚„
    have Hâ‚… := ((Hâ‚‚.prod_left Hâ‚ƒ).norm_left.pow (c.length - 1)).mul Hâ‚„.norm_left
    simpa [mul_assoc] using Hâ‚.norm_left.mul <| Hâ‚….const_mul_left c.length
  Â· have Hâ‚ : (fun x â†¦ pâ‚ x c.length - pâ‚‚ x c.length) =O[l] f := hpf _ c.length_le
    have Hâ‚‚ : âˆ€ i, (qâ‚‚ Â· (c.partSize i)) =O[l] (1 : Î± â†’ â„) := fun i â†¦
      (hqâ‚‚_bdd _ <| c.partSize_le i).isBigO_one â„
    simpa using Hâ‚.norm_left.mul <| .finsetProd fun i _ â†¦ (Hâ‚‚ i).norm_left
