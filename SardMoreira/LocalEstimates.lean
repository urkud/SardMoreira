import Mathlib.Analysis.Calculus.DiffContOnCl
import Mathlib.Analysis.Calculus.LineDeriv.Basic
import Mathlib.MeasureTheory.Integral.IntervalIntegral.FundThmCalculus
import SardMoreira.LebesgueDensity
import SardMoreira.ContDiff
import SardMoreira.ToMathlib.PR32186

open scoped Topology NNReal ENNReal unitInterval
open Asymptotics Filter MeasureTheory AffineMap Set Metric

theorem UniformSpace.Completion.hasFDerivAt_coe {ğ•œ E : Type*}
    [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E] {a : E} :
    HasFDerivAt ((â†‘) : E â†’ Completion E) (toComplL : E â†’L[ğ•œ] Completion E) a := by
  simpa using (toComplL (ğ•œ := ğ•œ) (E := E)).hasFDerivAt

section NormedField

variable {E F : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
  [NormedAddCommGroup F] [NormedSpace â„ F]

theorem openSegment_subset_ball_left {x y : E} (h : x â‰  y) :
    openSegment â„ x y âŠ† ball x â€–y - xâ€– := by
  rw [openSegment_eq_image_lineMap, â† mapsTo_iff_image_subset]
  intro t ht
  rw [mem_ball, dist_lineMap_left, dist_eq_norm_sub', Real.norm_of_nonneg ht.1.le]
  exact mul_lt_of_lt_one_left (by simpa [sub_eq_zero, eq_comm] using h) ht.2

lemma dist_le_mul_volume_of_norm_lineDeriv_le {f : E â†’ F} {a b : E} {C : â„}
    (hfc : ContinuousOn f (segment â„ a b))
    (hfd : âˆ€ t âˆˆ Ioo (0 : â„) 1, LineDifferentiableAt â„ f (lineMap a b t) (b - a))
    (hf' : âˆ€áµ t : â„, t âˆˆ Ioo (0 : â„) 1 â†’ â€–lineDeriv â„ f (lineMap a b t) (b - a)â€– â‰¤ C) :
    â€–f b - f aâ€– â‰¤
      C * volume.real {t âˆˆ Ioo (0 : â„) 1 | lineDeriv â„ f (lineMap a b t) (b - a) â‰  0} := by
  set g : â„ â†’ F := fun t â†¦ f (lineMap a b t)
  have hgc : ContinuousOn g (Icc 0 1) := by
    refine hfc.comp ?_ ?_
    Â· exact AffineMap.lineMap_continuous.continuousOn
    Â· simp [segment_eq_image_lineMap, mapsTo_image]
  have hdg (t : â„) (ht : t âˆˆ Ioo 0 1) : HasDerivAt g (lineDeriv â„ f (lineMap a b t) (b - a)) t := by
    have := (hfd t ht).hasLineDerivAt.scomp_of_eq (ğ•œ := â„) t ((hasDerivAt_id t).sub_const t)
    simpa [g, lineMap_apply_module', Function.comp_def, sub_smul, add_comm _ a] using this
  suffices dist (g 0) (g 1) â‰¤ C * volume.real {t âˆˆ Ioo 0 1 | deriv g t â‰  0} by
    convert this using 1
    Â· simp [g, dist_eq_norm_sub']
    Â· congr 2 with t
      simp +contextual [(hdg _ _).deriv]
  apply dist_le_mul_volume_of_norm_deriv_le_of_le zero_le_one hgc
  Â· exact fun t ht â†¦ (hdg t ht).differentiableAt.differentiableWithinAt
  Â· exact hf'.mono fun t ht ht_mem â†¦ by simpa only [(hdg t ht_mem).deriv] using ht ht_mem

lemma dist_le_mul_volume_of_norm_fderiv_le {f : E â†’ F} {a b : E} {C : â„} {s : Set E}
    (hs : IsOpen s) (hf : DiffContOnCl â„ f s) (hab : openSegment â„ a b âŠ† s)
    (hC : âˆ€ x âˆˆ s, â€–fderiv â„ f xâ€– â‰¤ C) :
    â€–f b - f aâ€– â‰¤
      C * â€–b - aâ€– * volume.real {t âˆˆ Ioo (0 : â„) 1 | fderiv â„ f (lineMap a b t) â‰  0} := by
  have hmem_s : âˆ€ t âˆˆ Ioo (0 : â„) 1, lineMap a b t âˆˆ s := fun t ht â†¦
    hab <| lineMap_mem_openSegment _ a b ht
  have hCâ‚€ : 0 â‰¤ C := (norm_nonneg _).trans <| hC _ <| hmem_s (1 / 2) (by norm_num)
  have hfc : ContinuousOn f (segment â„ a b) :=
    hf.continuousOn.mono <| segment_subset_closure_openSegment.trans <| closure_mono hab
  have hfd : âˆ€ t âˆˆ Ioo (0 : â„) 1, LineDifferentiableAt â„ f (lineMap a b t) (b - a) := fun t ht â†¦
    (hf.differentiableAt hs <| hmem_s t ht).lineDifferentiableAt
  have hfC : âˆ€ t âˆˆ Ioo (0 : â„) 1, â€–lineDeriv â„ f (lineMap a b t) (b - a)â€– â‰¤ C * â€–b - aâ€– := by
    intro t ht
    rw [DifferentiableAt.lineDeriv_eq_fderiv]
    Â· exact ContinuousLinearMap.le_of_opNorm_le _ (hC _ <| hmem_s t ht) _
    Â· exact hf.differentiableAt hs <| hmem_s t ht
  refine dist_le_mul_volume_of_norm_lineDeriv_le hfc hfd (.of_forall hfC) |>.trans ?_
  gcongr
  Â· refine ne_top_of_le_ne_top ?_ (measure_mono inter_subset_left)
    simp
  Â· simp +contextual [(hf.differentiableAt hs <| hmem_s _ â€¹_â€º).lineDeriv_eq_fderiv]

theorem sub_isBigO_norm_rpow_add_one_of_fderiv {f : E â†’ F} {a : E} {r : â„} (hr : 0 â‰¤ r)
    (hdf : âˆ€á¶  x in ğ“ a, DifferentiableAt â„ f x) (hderiv : fderiv â„ f =O[ğ“ a] (â€–Â· - aâ€– ^ r)) :
    (f Â· - f a) =O[ğ“ a] (â€–Â· - aâ€– ^ (r + 1)) := by
  rcases hderiv.exists_pos with âŸ¨C, hCâ‚€, hCâŸ©
  rw [Asymptotics.IsBigOWith_def] at hC
  rcases eventually_nhds_iff_ball.mp (hdf.and hC) with âŸ¨Îµ, hÎµâ‚€, hÎµâŸ©
  refine .of_bound C ?_
  rw [eventually_nhds_iff_ball]
  refine âŸ¨Îµ, hÎµâ‚€, fun y hy â†¦ ?_âŸ©
  rw [Real.norm_of_nonneg (by positivity), Real.rpow_add_one' (by positivity) (by positivity),
    â† mul_assoc]
  have hsub : closedBall a â€–y - aâ€– âŠ† ball a Îµ :=
    closedBall_subset_ball (mem_ball_iff_norm.mp hy)
  apply (convex_closedBall a â€–y - aâ€–).norm_image_sub_le_of_norm_fderiv_le (ğ•œ := â„)
  Â· exact fun z hz â†¦ (hÎµ z <| hsub hz).1
  Â· intro z hz
    grw [(hÎµ z <| hsub hz).2, Real.norm_of_nonneg (by positivity), mem_closedBall_iff_norm.mp hz]
  Â· simp
  Â· simp [dist_eq_norm_sub]

theorem isBigO_norm_rpow_add_one_of_fderiv_of_apply_eq_zero {f : E â†’ F} {a : E} {r : â„} (hr : 0 â‰¤ r)
    (hdf : âˆ€á¶  x in ğ“ a, DifferentiableAt â„ f x) (hderiv : fderiv â„ f =O[ğ“ a] (â€–Â· - aâ€– ^ r))
    (hfâ‚€ : f a = 0) : f =O[ğ“ a] (â€–Â· - aâ€– ^ (r + 1)) := by
  simpa [hfâ‚€] using sub_isBigO_norm_rpow_add_one_of_fderiv hr hdf hderiv

open UniformSpace (Completion) in
theorem sub_isLittleO_norm_rpow_add_one_of_fderiv_of_density_point [FiniteDimensional â„ E]
    [MeasurableSpace E] [BorelSpace E] {f : E â†’ F} {a : E} {r : â„}
    {Î¼ : Measure E} [Î¼.IsAddHaarMeasure] {s : Set E}
    (hr : 0 â‰¤ r) (hdf : âˆ€á¶  x in ğ“ a, DifferentiableAt â„ f x)
    (hderiv : fderiv â„ f =O[ğ“ a] (â€–Â· - aâ€– ^ r))
    (hs : fderiv â„ f =á¶ [ğ“[s] a] 0)
    (hmeas : Tendsto (fun r â†¦ Î¼ (s âˆ© closedBall a r) / Î¼ (closedBall a r)) (ğ“[>] 0) (ğ“ 1)) :
    (f Â· - f a) =o[ğ“ a] (â€–Â· - aâ€– ^ (r + 1)) := by
  wlog hF : CompleteSpace F generalizing F
  Â· set e : F â†’L[â„] Completion F := Completion.toComplL
    set g := e âˆ˜ f
    have hdg_eq : fderiv â„ g =á¶ [ğ“ a] (e âˆ˜L fderiv â„ f Â·) :=
      hdf.mono fun x hx â†¦ (e.hasFDerivAt.comp _ hx.hasFDerivAt).fderiv
    have hdg : âˆ€á¶  x in ğ“ a, DifferentiableAt â„ g x :=
      hdf.mono fun x hx â†¦ e.differentiableAt.comp _ hx
    have hg_deriv : fderiv â„ g =O[ğ“ a] fun x â†¦ â€–x - aâ€– ^ r := by
      calc
        fderiv â„ g =á¶ [ğ“ a] (e âˆ˜L fderiv â„ f Â·) := hdg_eq
        _ =O[ğ“ a] (â€–eâ€– * â€–fderiv â„ f Â·â€–) :=
          .of_norm_le fun _ â†¦ ContinuousLinearMap.opNorm_comp_le _ _
        _ =O[ğ“ a] fderiv â„ f := by
          refine .of_norm_right <| .const_mul_left (isBigO_refl _ _) _
        _ =O[ğ“ a] (â€–Â· - aâ€– ^ r) := by
          exact hderiv
    have hgâ‚€ : fderiv â„ g =á¶ [ğ“[s] a] 0 := by
      filter_upwards [mem_nhdsWithin_of_mem_nhds hdg_eq, hs] with x hxâ‚ hxâ‚‚
      simp [hxâ‚, hxâ‚‚]
    refine IsBigO.trans_isLittleO (.of_norm_right ?_) (this hdg hg_deriv hgâ‚€ inferInstance)
    simp_rw [g, Function.comp_apply, â† map_sub, e, Completion.coe_toComplL, Completion.norm_coe]
    exact (isBigO_refl _ _).norm_right
  wlog hsm : MeasurableSet s generalizing s
  Â· -- TODO: I'm getting a timeout without this line. Test with the latest Mathlib
    have aux : MeasurableSingletonClass (E â†’L[â„] F) :=
      OpensMeasurableSpace.toMeasurableSingletonClass
    apply @this (toMeasurable Î¼ s âˆ© {x | fderiv â„ f x = 0})
    Â· refine hmeas.congr' ?_
      rw [EventuallyEq, eventually_nhdsWithin_iff] at hs
      rcases Metric.eventually_nhds_iff_ball.mp hs with âŸ¨r, hrâ‚€, hrâŸ©
      filter_upwards [Ioo_mem_nhdsGT hrâ‚€] with Î´ âŸ¨hÎ´â‚€, hÎ´râŸ©
      rw [inter_assoc, Measure.measure_toMeasurable_inter_of_sFinite, â† inter_assoc,
        inter_right_comm, inter_eq_self_of_subset_left (_ : s âˆ© _ âŠ† _)]
      Â· refine fun y hy â†¦ hr _ (closedBall_subset_ball hÎ´r hy.2) hy.1
      Â· exact (measurableSet_eq.preimage (measurable_fderiv _ _)).inter measurableSet_closedBall
    Â· exact eventually_mem_nhdsWithin.mono fun x hx â†¦ hx.2
    Â· refine measurableSet_toMeasurable _ _ |>.inter ?_
      refine measurableSet_eq.preimage (measurable_fderiv _ _)
  rw [isLittleO_iff]
  intro c hc
  lift c to â„â‰¥0 using hc.le
  rcases hderiv.exists_pos with âŸ¨C, hCâ‚€, hCâŸ©
  rw [isBigOWith_iff] at hC
  lift C to â„â‰¥0 using hCâ‚€.le
  norm_cast at hc hCâ‚€
  rcases exists_pos_forall_measure_le_exists_mem_sphere_dist_lt_volume_lineMap_mem_lt (E := E)
    (show c / C / 2 â‰  0 by positivity) with âŸ¨Î´, hÎ´â‚€, hÎ´âŸ©
  specialize hÎ´ Î¼
  replace hmeas : âˆ€á¶  r in ğ“[>] 0, Î¼ (sá¶œ âˆ© closedBall a r) â‰¤ Î´ * Î¼ (closedBall a r) := by
    refine hmeas.eventually_const_lt (show 1 - Î´ < (1 : â„â‰¥0âˆ) by simpa [ENNReal.sub_lt_self_iff])
      |>.mono fun r hr â†¦ ?_
    replace hr := ENNReal.mul_lt_of_lt_div hr
    have : Î¼ (closedBall a r âˆ© s) â‰  âˆ :=
      measure_ne_top_of_subset inter_subset_left measure_closedBall_lt_top.ne
    rw [inter_comm, â† diff_eq, â† ENNReal.add_le_add_iff_left this, measure_inter_add_diff _ hsm,
      â† tsub_le_iff_right, inter_comm]
    rw [ENNReal.sub_mul, one_mul] at hr
    exacts [hr.le, fun _ _ â†¦ measure_closedBall_lt_top.ne]
  rw [eventually_nhds_iff_ball]
  rw [EventuallyEq, eventually_nhdsWithin_iff] at hs
  rcases eventually_nhds_iff_ball.mp (hdf.and <| hs.and hC) with âŸ¨Îµ, hÎµâ‚€, hÎµâŸ©
  choose hdf hdfs hdfr using hÎµ
  rw [(nhdsGT_basis (0 : â„)).eventually_iff] at hmeas
  rcases hmeas with âŸ¨Îµ', hÎµâ‚€', hÎµ'âŸ©
  use min Îµ Îµ', by positivity
  intro y hy
  rcases eq_or_ne y a with rfl | hya
  Â· simp; positivity
  obtain âŸ¨z, hz_mem, hzy, hz_volâŸ© : âˆƒ z âˆˆ sphere a â€–y - aâ€–, dist z y < â†‘(c / C / 2) * â€–y - aâ€– âˆ§
      volume {t : â„ | 0 â‰¤ t âˆ§ lineMap a z t âˆˆ sá¶œ âˆ© ball a â€–y - aâ€–} < â†‘(c / C / 2) := by
    refine hÎ´ â€–y - aâ€– (by simpa [sub_eq_zero]) a (sá¶œ âˆ© ball a â€–y - aâ€–) ?_ y (by simp)
    have : Nontrivial E := âŸ¨âŸ¨_, _, hyaâŸ©âŸ©
    grw [â† Measure.addHaar_closedBall_eq_addHaar_ball, â† hÎµ', ball_subset_closedBall]
    grw [min_le_right] at hy
    simpa [sub_eq_zero, hya, dist_eq_norm_sub] using hy
  have hsub : closedBall a â€–y - aâ€– âŠ† ball a Îµ := by
    apply closedBall_subset_ball
    grw [mem_ball_iff_norm, min_le_left] at hy
    exact hy
  have hz_norm : â€–z - aâ€– = â€–y - aâ€– := by simpa using hz_mem
  have hyz : â€–f y - f zâ€– â‰¤ (c / 2) * â€–y - aâ€– ^ (r + 1) := calc
    â€–f y - f zâ€– â‰¤ C * â€–y - aâ€– ^ r * â€–y - zâ€– := by
      apply (convex_closedBall a â€–y - aâ€–).norm_image_sub_le_of_norm_fderiv_le (ğ•œ := â„)
      Â· exact fun w hw â†¦ hdf w <| hsub hw
      Â· intro w hw
        grw [hdfr _ (hsub hw), Real.norm_of_nonneg (by positivity), mem_closedBall_iff_norm.mp hw]
      Â· exact sphere_subset_closedBall hz_mem
      Â· simp [dist_eq_norm_sub]
    _ â‰¤ (c / 2) * â€–y - aâ€– ^ (r + 1) := by
      grw [â† dist_eq_norm_sub' z y, hzy, Real.rpow_add_one' (by positivity) (by positivity)]
      apply le_of_eq
      push_cast
      field_simp
  have hza : â€–f z - f aâ€– â‰¤ (c / 2) * â€–y - aâ€– ^ (r + 1) := by
    grw [dist_le_mul_volume_of_norm_fderiv_le (C := C * â€–y - aâ€– ^ r) _ _
      (openSegment_subset_ball_left _)]
    Â· have H :
          volume.real {t : â„ | t âˆˆ Ioo 0 1 âˆ§ fderiv â„ f ((lineMap a z) t) â‰  0} < (c / C / 2) := by
        rw [Measure.real]
        apply ENNReal.toReal_lt_of_lt_ofReal
        norm_cast
        rw [ENNReal.ofReal_coe_nnreal]
        refine lt_of_le_of_lt ?_ hz_vol
        gcongr 2 with t
        rintro âŸ¨âŸ¨htâ‚€, htâ‚âŸ©, htâŸ©
        have : (lineMap a z) t âˆˆ ball a â€–y - aâ€– := by
          -- TODO: Part of the proof of `openSegment_subset_ball_left`. Move to a lemma?
          rw [mem_ball, dist_lineMap_left, Real.norm_of_nonneg htâ‚€.le, dist_comm, hz_mem]
          exact mul_lt_of_lt_one_left (by simpa [sub_eq_zero]) htâ‚
        refine âŸ¨htâ‚€.le, ?_, thisâŸ©
        contrapose! ht
        apply hdfs
        Â· grw [â† hsub, â† ball_subset_closedBall]
          exact this
        Â· simpa using ht
      grw [H, hz_norm, Real.rpow_add_one' (by positivity) (by positivity)]
      apply le_of_eq
      field_simp
    Â· intro w hw
      grw [hdfr, Real.norm_of_nonneg (by positivity), mem_ball_iff_norm.mp hw, hz_norm]
      grw [â† hsub, â† ball_subset_closedBall, â† hz_norm]
      exact hw
    Â· exact isOpen_ball
    Â· apply DifferentiableOn.diffContOnCl_ball (U := ball a Îµ)
      Â· exact fun w hw â†¦ (hdf w hw).differentiableWithinAt
      Â· grw [hz_norm, hsub]
    Â· rintro rfl
      simpa [sub_eq_zero, hya] using hz_norm.symm
  grw [norm_sub_le_norm_sub_add_norm_sub _ (f z), hyz, hza, Real.norm_of_nonneg (by positivity)]
  apply le_of_eq
  field_simp
  ring

theorem isLittleO_norm_rpow_add_one_of_fderiv_of_density_point_of_apply_eq_zero
   [FiniteDimensional â„ E] [MeasurableSpace E] [BorelSpace E] {f : E â†’ F} {a : E} {r : â„}
    {Î¼ : Measure E} [Î¼.IsAddHaarMeasure] {s : Set E}
    (hr : 0 â‰¤ r) (hdf : âˆ€á¶  x in ğ“ a, DifferentiableAt â„ f x)
    (hderiv : fderiv â„ f =O[ğ“ a] (â€–Â· - aâ€– ^ r)) (hs : âˆ€á¶  x in ğ“[s] a, fderiv â„ f x = 0)
    (hmeas : Tendsto (fun r â†¦ Î¼ (s âˆ© closedBall a r) / Î¼ (closedBall a r)) (ğ“[>] 0) (ğ“ 1))
    (hfâ‚€ : f a = 0) :
    f =o[ğ“ a] (â€–Â· - aâ€– ^ (r + 1)) := by
  simpa [hfâ‚€]
    using sub_isLittleO_norm_rpow_add_one_of_fderiv_of_density_point hr hdf hderiv hs hmeas
