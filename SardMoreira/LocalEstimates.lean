import Mathlib.Analysis.Calculus.DiffContOnCl
import Mathlib.Analysis.Calculus.LineDeriv.Basic
import Mathlib.MeasureTheory.Integral.IntervalIntegral.FundThmCalculus

open scoped Topology NNReal unitInterval
open Asymptotics Filter MeasureTheory AffineMap Set

lemma MeasureTheory.Measure.ae_ne {Î± : Type*} {_ : MeasurableSpace Î±} {Î¼ : Measure Î±}
    [NoAtoms Î¼] (a : Î±) : âˆ€áµ x âˆ‚Î¼, x â‰  a :=
  (countable_singleton a).ae_notMem Î¼

section NormedField

variable {E F : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
  [NormedAddCommGroup F] [NormedSpace â„ F]

theorem lineMap_mem_openSegment (a b : E) {t : â„} (ht : t âˆˆ Ioo 0 1) :
    lineMap a b t âˆˆ openSegment â„ a b :=
  (openSegment_eq_image_lineMap _ _ _).superset <| mem_image_of_mem _ ht

theorem DifferentiableAt.lineDifferentiableAt {f : E â†’ F} {a b : E} (hf : DifferentiableAt â„ f a) :
    LineDifferentiableAt â„ f a b :=
  hf.hasFDerivAt.hasLineDerivAt _ |>.lineDifferentiableAt

lemma dist_le_integral_of_norm_deriv_le_of_le {f : â„ â†’ E} {B : â„ â†’ â„} {a b : â„} (hab : a â‰¤ b)
    (hfc : ContinuousOn f (Set.Icc a b)) (hfd : DifferentiableOn â„ f (Set.Ioo a b))
    (hfB : âˆ€áµ t, t âˆˆ Ioo a b â†’ â€–deriv f tâ€– â‰¤ B t)
    (hBi : IntervalIntegrable B volume a b) : dist (f a) (f b) â‰¤ âˆ« t in a..b, B t := by
  wlog hE : CompleteSpace E generalizing E
  Â· set g : â„ â†’ UniformSpace.Completion E := (â†‘) âˆ˜ f with hg
    have hgc : ContinuousOn g (Icc a b) :=
      (UniformSpace.Completion.continuous_coe E).comp_continuousOn hfc
    have hgd : DifferentiableOn â„ g (Ioo a b) :=
      UniformSpace.Completion.toComplL.differentiable.comp_differentiableOn hfd
    have hdg : âˆ€ t âˆˆ Set.Ioo a b, deriv g t = deriv f t := by
      intro t ht
      have : HasFDerivAt (ğ•œ := â„) (â†‘) UniformSpace.Completion.toComplL (f t) := by
        rw [â† UniformSpace.Completion.coe_toComplL (ğ•œ := â„)]
        exact (UniformSpace.Completion.toComplL (E := E) (ğ•œ := â„)).hasFDerivAt
      have hdft : HasDerivAt f (deriv f t) t := hfd.hasDerivAt <| Ioo_mem_nhds ht.1 ht.2
      rw [hg, (this.comp_hasDerivAt t hdft).deriv, UniformSpace.Completion.coe_toComplL]
    have hgn : âˆ€áµ t, t âˆˆ Ioo a b â†’ â€–deriv g tâ€– â‰¤ B t :=
      hfB.mono fun t htB ht â†¦ by
        simpa only [hdg t ht, UniformSpace.Completion.norm_coe] using htB ht
    simpa [g] using this hgc hgd hgn inferInstance
  have hfB' : (â€–deriv f Â·â€–) â‰¤áµ[volume.restrict (uIoc a b)] B := by
    rwa [uIoc_of_le hab, â† Measure.restrict_congr_set Ioo_ae_eq_Ioc, EventuallyLE,
        ae_restrict_iff' measurableSet_Ioo]
  rw [dist_eq_norm_sub', â† intervalIntegral.integral_eq_sub_of_hasDeriv_right (f' := deriv f)]
  Â· apply intervalIntegral.norm_integral_le_of_norm_le hab _ hBi
    rwa [â† ae_restrict_iff' measurableSet_Ioc, â† uIoc_of_le hab]
  Â· rwa [uIcc_of_le hab]
  Â· rw [min_eq_left hab, max_eq_right hab]
    intro t ht
    exact hfd.hasDerivAt (isOpen_Ioo.mem_nhds ht) |>.hasDerivWithinAt
  Â· apply hBi.mono_fun (aestronglyMeasurable_deriv _ _)
    exact hfB'.trans <| .of_forall fun _ â†¦ le_abs_self _

lemma dist_le_mul_volume_of_norm_deriv_le_of_le {f : â„ â†’ E} {a b C : â„} (hab : a â‰¤ b)
    (hfc : ContinuousOn f (Icc a b)) (hfd : DifferentiableOn â„ f (Ioo a b))
    (hnorm : âˆ€áµ t, t âˆˆ Ioo a b â†’ â€–deriv f tâ€– â‰¤ C) :
    dist (f a) (f b) â‰¤ C * volume.real {x âˆˆ Ioo a b | deriv f x â‰  0} := by
  set s := toMeasurable volume {x | deriv f x â‰  0}
  have hsm : MeasurableSet s := by measurability
  calc
    dist (f a) (f b) â‰¤ âˆ« t in a..b, indicator s (fun _ â†¦ C) t := by
      apply dist_le_integral_of_norm_deriv_le_of_le hab hfc hfd
      Â· refine hnorm.mono fun t ht ht_mem â†¦ ?_
        apply le_indicator_apply
        Â· exact fun ht' â†¦ ht ht_mem
        Â· simp only [s, norm_le_zero_iff]
          exact not_imp_comm.2 fun h â†¦ subset_toMeasurable _ _ h
      Â· rw [intervalIntegrable_iff_integrableOn_Ioo_of_le hab]
        refine (integrableOn_const ?_ ?_).indicator hsm <;> simp
    _ = C * volume.real {x âˆˆ Ioo a b | deriv f x â‰  0} := by
      rw [intervalIntegral.integral_of_le hab, Measure.restrict_congr_set Ioo_ae_eq_Ioc.symm,
        integral_indicator hsm, Measure.restrict_restrict hsm,
        setIntegral_const, smul_eq_mul, mul_comm]
      simp only [s, Measure.real,
        Measure.measure_toMeasurable_inter_of_sFinite measurableSet_Ioo]
      simp only [inter_def, mem_setOf_eq, and_comm]

lemma dist_le_mul_volume_of_norm_lineDeriv_le {f : E â†’ F} {a b : E} {C : â„}
    (hfc : ContinuousOn f (segment â„ a b))
    (hfd : âˆ€ t âˆˆ Ioo (0 : â„) 1, LineDifferentiableAt â„ f (lineMap a b t) (b - a))
    (hf' : âˆ€áµ t : â„, t âˆˆ Ioo (0 : â„) 1 â†’ â€–lineDeriv â„ f (lineMap a b t) (b - a)â€– â‰¤ C) :
    â€–f b - f aâ€– â‰¤
      C * volume.real {t âˆˆ Ioo (0 : â„) 1 | lineDeriv â„ f (lineMap a b t) (b - a) â‰  0} := by
  set g : â„ â†’ F := fun t â†¦ f (lineMap a b t)
  have hgc : ContinuousOn g (Icc 0 1) := by
    refine hfc.comp ?_ ?_
    Â· exact AffineMap.lineMap_continuous.continuousOn
    Â· simp [segment_eq_image_lineMap, mapsTo_image]
  have hdg (t : â„) (ht : t âˆˆ Ioo 0 1) : HasDerivAt g (lineDeriv â„ f (lineMap a b t) (b - a)) t := by
    have := (hfd t ht).hasLineDerivAt.scomp_of_eq (ğ•œ := â„) t ((hasDerivAt_id t).sub_const t)
    simpa [g, lineMap_apply_module', Function.comp_def, sub_smul, add_comm _ a] using this
  suffices dist (g 0) (g 1) â‰¤ C * volume.real {t âˆˆ Ioo 0 1 | deriv g t â‰  0} by
    convert this using 1
    Â· simp [g, dist_eq_norm_sub']
    Â· congr 2 with t
      simp +contextual [(hdg _ _).deriv]
  apply dist_le_mul_volume_of_norm_deriv_le_of_le zero_le_one hgc
  Â· exact fun t ht â†¦ (hdg t ht).differentiableAt.differentiableWithinAt
  Â· exact hf'.mono fun t ht ht_mem â†¦ by simpa only [(hdg t ht_mem).deriv] using ht ht_mem

lemma dist_le_mul_volume_of_norm_fderiv_le {f : E â†’ F} {a b : E} {C : â„} {s : Set E}
    (hs : IsOpen s) (hf : DiffContOnCl â„ f s) (hab : openSegment â„ a b âŠ† s)
    (hC : âˆ€ x âˆˆ s, â€–fderiv â„ f xâ€– â‰¤ C) :
    â€–f b - f aâ€– â‰¤
      C * â€–b - aâ€– * volume.real {t âˆˆ Ioo (0 : â„) 1 | fderiv â„ f (lineMap a b t) â‰  0} := by
  have hmem_s : âˆ€ t âˆˆ Ioo (0 : â„) 1, lineMap a b t âˆˆ s := fun t ht â†¦
    hab <| lineMap_mem_openSegment a b ht
  have hCâ‚€ : 0 â‰¤ C := (norm_nonneg _).trans <| hC _ <| hmem_s (1 / 2) (by norm_num)
  have hfc : ContinuousOn f (segment â„ a b) :=
    hf.continuousOn.mono <| segment_subset_closure_openSegment.trans <| closure_mono hab
  have hfd : âˆ€ t âˆˆ Ioo (0 : â„) 1, LineDifferentiableAt â„ f (lineMap a b t) (b - a) := fun t ht â†¦
    (hf.differentiableAt hs <| hmem_s t ht).lineDifferentiableAt
  have hfC : âˆ€ t âˆˆ Ioo (0 : â„) 1, â€–lineDeriv â„ f (lineMap a b t) (b - a)â€– â‰¤ C * â€–b - aâ€– := by
    intro t ht
    rw [DifferentiableAt.lineDeriv_eq_fderiv]
    Â· exact ContinuousLinearMap.le_of_opNorm_le _ (hC _ <| hmem_s t ht) _
    Â· exact hf.differentiableAt hs <| hmem_s t ht
  refine dist_le_mul_volume_of_norm_lineDeriv_le hfc hfd (.of_forall hfC) |>.trans ?_
  gcongr
  Â· refine ne_top_of_le_ne_top ?_ (measure_mono inter_subset_left)
    simp
  Â· contrapose!
    simp +contextual [(hf.differentiableAt hs <| hmem_s _ â€¹_â€º).lineDeriv_eq_fderiv]

