import Mathlib
import SardMoreira.ContDiffMoreiraHolder
import SardMoreira.ImplicitFunction
import SardMoreira.LinearAlgebra
import SardMoreira.ChartEstimates
import SardMoreira.WithRPowDist
import SardMoreira.OuterMeasureDeriv
import SardMoreira.ToMathlib.PR33029
import SardMoreira.ToMathlib.PR32993

open scoped unitInterval NNReal Topology ENNReal Pointwise
open MeasureTheory Measure Metric

local notation "dim" => Module.finrank ‚Ñù

-- TODO: generalize to semilinear maps
protected noncomputable def ContinuousLinearMap.finrank {R M N : Type*} [Semiring R]
    [AddCommMonoid M] [Module R M] [TopologicalSpace M]
    [AddCommMonoid N] [Module R N] [TopologicalSpace N]
    (f : M ‚ÜíL[R] N) : ‚Ñï :=
  Module.finrank R f.range

theorem ContinuousLinearMap.finrank_comp_eq_left_of_surjective {R M N P : Type*} [Semiring R]
    [AddCommMonoid M] [Module R M] [TopologicalSpace M]
    [AddCommMonoid N] [Module R N] [TopologicalSpace N]
    [AddCommMonoid P] [Module R P] [TopologicalSpace P]
    (g : N ‚ÜíL[R] P) {f : M ‚ÜíL[R] N} (hf : Function.Surjective f) :
    (g ‚àòL f).finrank = g.finrank := by
  -- Since $f$ is surjective, the image of $g \circ f$ is the same as the image of $g$.
  have h_range : (g.comp f).range = g.range :=
    SetLike.coe_injective <| hf.range_comp g
  rw [ContinuousLinearMap.finrank, ContinuousLinearMap.finrank, h_range]

theorem ContinuousLinearMap.finrank_comp_eq_right_of_injective {R M N P : Type*} [Semiring R]
    [AddCommMonoid M] [Module R M] [TopologicalSpace M]
    [AddCommMonoid N] [Module R N] [TopologicalSpace N]
    [AddCommMonoid P] [Module R P] [TopologicalSpace P]
    {g : N ‚ÜíL[R] P} (hg : Function.Injective g) (f : M ‚ÜíL[R] N) :
    (g ‚àòL f).finrank = f.finrank := by
  -- Since $g$ is injective, the range of $g \circ f$ is isomorphic to the range of $f$.
  have h_iso : (g.comp f).range ‚âÉ‚Çó[R] f.range := by
    symm;
    refine' { Equiv.ofBijective ( fun x => ‚ü® g x, by aesop ‚ü© ) ‚ü® fun x y hxy => _, fun x => _ ‚ü© with .. } <;> aesop;
  exact h_iso.finrank_eq

@[simp]
theorem ContinuousLinearEquiv.finrank_comp_left {R M N N' : Type*} [Semiring R]
    [AddCommMonoid M] [Module R M] [TopologicalSpace M]
    [AddCommMonoid N] [Module R N] [TopologicalSpace N]
    [AddCommMonoid N'] [Module R N'] [TopologicalSpace N']
    (e : N ‚âÉL[R] N') (f : M ‚ÜíL[R] N) : (e ‚àòL f : M ‚ÜíL[R] N').finrank = f.finrank := by
  apply ContinuousLinearMap.finrank_comp_eq_right_of_injective
  exact e.injective

@[simp]
theorem ContinuousLinearEquiv.finrank_comp_right {R M M' N : Type*} [Semiring R]
    [AddCommMonoid M] [Module R M] [TopologicalSpace M]
    [AddCommMonoid N] [Module R N] [TopologicalSpace N]
    [AddCommMonoid M'] [Module R M'] [TopologicalSpace M']
    (f : M ‚ÜíL[R] N) (e : M' ‚âÉL[R] M) : (f ‚àòL e : M' ‚ÜíL[R] N).finrank = f.finrank := by
  apply ContinuousLinearMap.finrank_comp_eq_left_of_surjective
  exact e.surjective

theorem LipschitzWith.hausdorffMeasure_image_null {X Y : Type*} [EMetricSpace X] [EMetricSpace Y]
    [MeasurableSpace X] [BorelSpace X] [MeasurableSpace Y] [BorelSpace Y] {K : NNReal} {f : X ‚Üí Y}
    (h : LipschitzWith K f) {d : ‚Ñù} (hd : 0 ‚â§ d) {s : Set X} (hs : ŒºH[d] s = 0) :
    ŒºH[d] (f '' s) = 0 := by
  grw [‚Üê nonpos_iff_eq_zero, h.hausdorffMeasure_image_le hd, hs, mul_zero]

/-- Moreira's upper estimate on the Hausdorff dimension of the image of the set of points $x$
such that `fderiv ‚Ñù f x` has rank at most `p < min n m`,
provided that `f` is a $$C^{k+(\alpha)}$$-map
from an `n`-dimensional space to an `m`-dimensional space.

Note that the estimate does not depend on `m`. -/
noncomputable def sardMoreiraBound (n k : ‚Ñï) (Œ± : I) (p : ‚Ñï) : ‚Ñù‚â•0 :=
  p + (n - p) / (k + ‚ü®Œ±, Œ±.2.1‚ü©)

theorem mul_sardMoreiraBound {n k p : ‚Ñï} (hk : k ‚â† 0) (hpn : p ‚â§ n) (Œ± : I) :
    (k + Œ± : ‚Ñù) * sardMoreiraBound n k Œ± p = (k + Œ±) * p + (n - p) := by
  rw [sardMoreiraBound]
  have := Œ±.2.1
  simp [field, @NNReal.coe_sub n p (mod_cast hpn)]

theorem monotone_sardMoreiraBound (n : ‚Ñï) {k : ‚Ñï} (hk : k ‚â† 0) (Œ± : I) :
    Monotone (sardMoreiraBound n k Œ±) := by
  apply monotone_nat_of_le_succ
  intro p
  rcases Œ± with ‚ü®Œ±, hŒ±‚ÇÄ, hŒ±‚ÇÅ‚ü©
  simp only [sardMoreiraBound, field]
  rw [‚Üê NNReal.coe_le_coe]
  push_cast [tsub_add_eq_tsub_tsub]
  grw [@NNReal.coe_sub_def _ 1, ‚Üê le_max_left, ‚Üê sub_nonneg]
  push_cast
  linarith only [hŒ±‚ÇÄ, show (1 : ‚Ñù) ‚â§ k by norm_cast; grind]

@[gcongr]
theorem sardMoreiraBound_le_sardMoreiraBound {m n k l p q : ‚Ñï} (hl : l ‚â† 0) (hmn : m ‚â§ n)
    (hlk : l ‚â§ k) (hpq : p ‚â§ q) (Œ± : I) :
    sardMoreiraBound m k Œ± p ‚â§ sardMoreiraBound n l Œ± q := by
  grw [‚Üê monotone_sardMoreiraBound n hl Œ± hpq]
  unfold sardMoreiraBound
  gcongr

variable {E F G : Type*}
  [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
  [NormedAddCommGroup F] [NormedSpace ‚Ñù F]
  [NormedAddCommGroup G] [NormedSpace ‚Ñù G]
  {k p : ‚Ñï} {Œ± : I}

namespace Moreira2001

theorem hausdorffMeasure_image_le_mul_aux {X : Type*} [MetricSpace X]
    [MeasurableSpace E] [BorelSpace E]
    [MeasurableSpace F] [BorelSpace F]
    [FiniteDimensional ‚Ñù E] [FiniteDimensional ‚Ñù F]
    [MeasurableSpace X] [BorelSpace X]
    {f : E √ó F ‚Üí X} {s : Set (E √ó F)} {n : ‚Ñï} (hk : k ‚â† 0) (hn : dim E + dim F ‚â§ n)
    {cE cF : ‚Ñù‚â•0}
    (hcE : ‚àÄ x ‚àà s, ‚àÄ·∂† y in ùìù (x, x), y.1.2 = y.2.2 ‚Üí dist (f y.1) (f y.2) ‚â§ cE * dist y.1 y.2)
    (hcF : ‚àÄ x ‚àà s, ‚àÄ·∂† y in ùìù x.2, dist (f (x.1, y)) (f x) ‚â§ cF * dist y x.2 ^ (k + Œ± : ‚Ñù)) :
    ŒºH[sardMoreiraBound n k Œ± (dim E)] (f '' s) ‚â§
      ((2 * (cE + cF)) ^ (sardMoreiraBound n k Œ± (dim E) : ‚Ñù) /
        (ŒºH[dim E] (ball (0 : E) 1) * ŒºH[dim F] (ball (0 : F) 1))) *
        ŒºH[dim E].prod ŒºH[dim F] s := by
  set C : ‚Ñù‚â•0‚àû := (2 * (cE + cF)) ^ (sardMoreiraBound n k Œ± (dim E) : ‚Ñù) /
    (ŒºH[dim E] (ball (0 : E) 1) * ŒºH[dim F] (ball (0 : F) 1))
  have hŒ±‚ÇÄ : 0 ‚â§ (Œ± : ‚Ñù) := Œ±.2.1
  set Œ≤ : ‚Ñù := (k + Œ±)‚Åª¬π
  have hŒ≤inv : Œ≤‚Åª¬π = k + Œ± := inv_inv _
  have hŒ≤‚ÇÄ : 0 < Œ≤ := by positivity
  have hŒ≤‚ÇÅ : Œ≤ ‚â§ 1 := by
    suffices (1 : ‚Ñù) ‚â§ k + Œ± by simpa [field]
    rw [‚Üê Nat.one_le_iff_ne_zero] at hk
    rify at hk
    linear_combination hk + hŒ±‚ÇÄ
  set e : WithRPowDist E Œ≤ hŒ≤‚ÇÄ hŒ≤‚ÇÅ √ó F ‚Üí E √ó F := Prod.map WithRPowDist.val id
  have hec : Continuous e := by fun_prop
  set t : Set (WithRPowDist E Œ≤ hŒ≤‚ÇÄ hŒ≤‚ÇÅ √ó F) := e ‚Åª¬π' s
  set g : WithRPowDist E Œ≤ hŒ≤‚ÇÄ hŒ≤‚ÇÅ √ó F ‚Üí X := f ‚àò e
  set Œº : Measure (WithRPowDist E Œ≤ hŒ≤‚ÇÄ hŒ≤‚ÇÅ √ó F) :=
    (ŒºH[dim E].withRPowDist Œ≤ hŒ≤‚ÇÄ hŒ≤‚ÇÅ).prod ŒºH[dim F]
  have hŒº (s) : Œº s = ŒºH[dim E].prod ŒºH[dim F] (Prod.map .mk id ‚Åª¬π' s) := by
    simp only [Œº]
    rw [withRPowDist, ‚Üê Measure.map_id (Œº := ŒºH[dim F]),
        map_prod_map _ _ WithRPowDist.measurable_mk measurable_id, Measure.map_id]
    exact MeasurableEquiv.map_apply (WithRPowDist.measurableEquiv.symm.prodCongr (.refl F)) s
  suffices ŒºH[sardMoreiraBound n k Œ± (dim E)] (g '' t) ‚â§ C * Œº t by
    simp only [hŒº, g, Set.image_comp] at this
    convert this using 3
    ext ‚ü®x, y‚ü©
    rcases @WithRPowDist.surjective_val _ Œ≤ hŒ≤‚ÇÄ hŒ≤‚ÇÅ x with ‚ü®x, rfl‚ü©
    simp [e, and_comm, ‚Üê WithRPowDist.ext_iff, t]
  apply hasudorffMeasure_image_le_mul (holderExp := k + Œ±) (dimDom := (k + Œ±) * dim E + dim F)
  case holderExp_pos => positivity
  case hŒº_dim =>
    intro x r
    rw [‚Üê closedBall_prod_same, hŒº, Set.preimage_prod_map_prod,
      WithRPowDist.preimage_mk_closedBall, Set.preimage_id, Measure.prod_prod,
      addHaar_closedBall, addHaar_closedBall, mul_mul_mul_comm, hŒ≤inv, ‚Üê Real.rpow_mul_natCast,
      ENNReal.rpow_add_of_nonneg, ‚Üê ENNReal.coe_rpow_of_nonneg]
    ¬∑ norm_cast
    all_goals positivity
  case hŒºball‚ÇÄ =>
    apply_rules [mul_ne_zero, IsOpen.measure_ne_zero, isOpen_ball] <;> simp
  case hŒºball => finiteness
  case hdim =>
    grw [mul_sardMoreiraBound hk (by grind), ‚Üê hn]
    simp
  case hdimDom => positivity
  case hsC => right; finiteness
  case h =>
    intro x hx Œµ hŒµ
    have hec : Continuous e := by fun_prop
    replace hcE := (hec.prodMap hec).tendsto (x, x) |>.eventually <| hcE (e x) hx
    specialize hcF (e x) hx
    rw [Metric.eventually_nhds_iff_ball] at hcE hcF
    rcases hcE with ‚ü®rE, hrE‚ÇÄ, hrE‚ü©
    rcases hcF with ‚ü®rF, hrF‚ÇÄ, hrF‚ü©
    rw [eventually_nhdsWithin_iff]
    filter_upwards [ball_mem_nhds _ (lt_min hrE‚ÇÄ hrF‚ÇÄ)] with y hy hyt
    grw [‚Üê le_self_add]
    rw [edist_nndist, edist_nndist, ‚Üê ENNReal.coe_rpow_of_nonneg _ (by positivity)]
    norm_cast
    rw [‚Üê NNReal.coe_le_coe]
    push_cast
    calc
      dist (g y) (g x) ‚â§ dist (g y) (g (x.1, y.2)) + dist (g (x.1, y.2)) (g x) :=
        dist_triangle ..
      _ ‚â§ cE * dist y.1 x.1 ^ (k + Œ± : ‚Ñù) + cF * dist y.2 x.2 ^ (k + Œ± : ‚Ñù) := by
        simp only [mem_ball, Prod.dist_eq, lt_inf_iff, sup_lt_iff] at hy
        gcongr
        ¬∑ refine hrE (y, (x.1, y.2)) ?_ ?_ |>.trans_eq ?_
          ¬∑ simp [Prod.dist_eq, hy]
          ¬∑ simp [e]
          ¬∑ simp (disch := positivity) [Prod.dist_eq, e, hŒ≤inv]
        ¬∑ apply hrF
          simp [e, hy]
      _ ‚â§ (cE + cF) * dist y x ^ (k + Œ± : ‚Ñù) := by
        rw [add_mul]
        gcongr <;> simp [Prod.dist_eq]

variable (E F) in
noncomputable def boundCoeff (n k : ‚Ñï) (Œ± : I) : ‚Ñù‚â•0‚àû := by
  borelize E
  borelize F
  exact 4 ^ (sardMoreiraBound n k Œ± (dim E) : ‚Ñù) /
    (ŒºH[dim E] (ball (0 : E) 1) * ŒºH[dim F] (ball (0 : F) 1))

protected theorem hausdorffMeasure_image_le_mul {X : Type*} [MetricSpace X]
    [MeasurableSpace E] [BorelSpace E]
    [MeasurableSpace F] [BorelSpace F]
    [FiniteDimensional ‚Ñù E] [FiniteDimensional ‚Ñù F]
    [MeasurableSpace X] [BorelSpace X]
    {f : E √ó F ‚Üí X} {s : Set (E √ó F)} {n : ‚Ñï} (hk : k ‚â† 0) (hn : dim E + dim F ‚â§ n)
    {cE cF : ‚Ñù‚â•0} (hcE‚ÇÄ : cE ‚â† 0) (hcF‚ÇÄ : cF ‚â† 0)
    (hcE : ‚àÄ x ‚àà s, ‚àÄ·∂† y in ùìù (x, x), y.1.2 = y.2.2 ‚Üí dist (f y.1) (f y.2) ‚â§ cE * dist y.1 y.2)
    (hcF : ‚àÄ x ‚àà s, ‚àÄ·∂† y in ùìù x.2, dist (f (x.1, y)) (f x) ‚â§ cF * dist y x.2 ^ (k + Œ± : ‚Ñù)) :
    ŒºH[sardMoreiraBound n k Œ± (dim E)] (f '' s) ‚â§
      boundCoeff E F n k Œ± * cE ^ dim E * cF ^ ((n - dim E) / (k + Œ±) : ‚Ñù) *
        (ŒºH[dim E].prod ŒºH[dim F] s) := by
  have := Œ±.2.1
  set c := cF / cE
  set e : (E √ó F) ‚âÉ‚Çú (E √ó F) := .prodCongr (.smulOfNeZero c (by positivity)) (.refl F)
  set t := e ‚Åª¬π' s
  set g : E √ó F ‚Üí X := f ‚àò e
  have hcE' : ‚àÄ x ‚àà t, ‚àÄ·∂† y in ùìù (x, x), y.1.2 = y.2.2 ‚Üí
      dist (g y.1) (g y.2) ‚â§ ‚Üë(cE * c) * dist y.1 y.2 := by
    intro x hx
    specialize hcE (e x) hx
    refine ((map_continuous e).prodMap (map_continuous e)).tendsto (x, x)
      |>.eventually hcE |>.mono fun y hy hy_eq ‚Ü¶ ?_
    refine (hy hy_eq).trans_eq ?_
    simp [e, NNReal.smul_def, Prod.map, dist_smul‚ÇÄ, hy_eq, mul_assoc]
    simp [Prod.dist_eq, hy_eq]
  have hcF' : ‚àÄ x ‚àà t, ‚àÄ·∂† y in ùìù x.2, dist (g (x.1, y)) (g x) ‚â§ cF * dist y x.2 ^ (k + Œ± : ‚Ñù) := by
    intro x hx
    exact hcF (e x) hx
  have hgt : g '' t = f '' s := by simp only [t, g, Set.image_comp, e.image_preimage]
  rw [‚Üê hgt]
  refine hausdorffMeasure_image_le_mul_aux hk hn hcE' hcF' |>.trans_eq ?_
  have : ŒºH[dim E].prod ŒºH[dim F] t = ŒºH[dim E].prod ŒºH[dim F] s / c ^ dim E := by
    have : ŒºH[dim E].prod ŒºH[dim F] = (c ^ dim E ‚Ä¢ ŒºH[dim E].prod ŒºH[dim F]).map e := by
      refine Measure.prod_eq fun s t hs ht ‚Ü¶ ?_
      rw [e.measurableEmbedding.map_apply, Measure.coe_nnreal_smul_apply]
      simp (disch := first | positivity | finiteness)
        [e, Set.preimage_prod_map_prod, Set.preimage_smul‚ÇÄ,
          ‚Üê ENNReal.inv_pow, mul_assoc, ENNReal.mul_inv_cancel_left]
    conv_rhs => rw [this]
    rw [e.measurableEmbedding.map_apply, Measure.coe_nnreal_smul_apply, ENNReal.coe_pow,
      mul_div_assoc, ENNReal.mul_div_cancel]
    ¬∑ positivity
    ¬∑ finiteness
  simp (disch := positivity) only [this, ‚Üê mul_assoc, c, mul_div_cancel‚ÇÄ, ‚Üê two_mul]
  rw [‚Üê mul_div_assoc, ENNReal.mul_div_right_comm, ENNReal.div_right_comm, boundCoeff]
  congr 1
  norm_num1
  rw [‚Üê ENNReal.mul_div_right_comm, ‚Üê ENNReal.mul_div_right_comm]
  borelize E; borelize F
  congr 1
  rw [‚Üê ENNReal.coe_pow, div_pow, ENNReal.coe_div, ENNReal.coe_pow, ENNReal.coe_pow,
    ENNReal.mul_rpow_of_nonneg, mul_div_assoc, mul_assoc]
  ¬∑ congr 1
    rw [‚Üê ENNReal.div_mul, ‚Üê ENNReal.rpow_natCast, ‚Üê ENNReal.rpow_sub, mul_comm]
    ¬∑ congr 2
      have : (dim E : ‚Ñù‚â•0) ‚â§ n := by grw [‚Üê hn]; simp
      simp [sardMoreiraBound, NNReal.coe_sub this]
    ¬∑ positivity
    ¬∑ finiteness
    ¬∑ left; positivity
    ¬∑ left; finiteness
  ¬∑ positivity
  ¬∑ positivity

theorem hausdorffMeasure_image_null_of_isBigO {X : Type*} [MetricSpace X]
    [MeasurableSpace E] [BorelSpace E]
    [MeasurableSpace F] [BorelSpace F]
    [MeasurableSpace X] [BorelSpace X]
    [FiniteDimensional ‚Ñù E] [FiniteDimensional ‚Ñù F]
    {f : E √ó F ‚Üí X} {s : Set (E √ó F)} {n : ‚Ñï} {cE : NNReal} (hk : k ‚â† 0)
    (hn : dim E + dim F ‚â§ n)
    (hcE : ‚àÄ x ‚àà s, ‚àÄ·∂† y in ùìù (x, x), y.1.2 = y.2.2 ‚Üí dist (f y.1) (f y.2) ‚â§ cE * dist y.1 y.2)
    (h_isBigO : ‚àÄ x ‚àà s,
      (fun y ‚Ü¶ dist (f (x.1, y)) (f x)) =O[ùìù x.2] (fun y ‚Ü¶ ‚Äñy - x.2‚Äñ ^ (k + Œ± : ‚Ñù)))
    (hs : ŒºH[dim E].prod ŒºH[dim F] s = 0) :
    ŒºH[sardMoreiraBound n k Œ± (dim E)] (f '' s) = 0 := by
  wlog H : ‚àÉ cF : ‚Ñù‚â•0, 0 < cF ‚àß ‚àÄ x ‚àà s, ‚àÄ·∂† y in ùìù x.2,
    dist (f (x.1, y)) (f x) ‚â§ cF * (dist y x.2) ^ (k + Œ± : ‚Ñù) generalizing s
  ¬∑ set t : ‚Ñï ‚Üí Set (E √ó F) := fun N ‚Ü¶
      {x ‚àà s | ‚àÄ·∂† y in ùìù x.2, dist (f (x.1, y)) (f x) ‚â§ (N + 1) * (dist y x.2) ^ (k + Œ± : ‚Ñù)}
    rw [‚Üê nonpos_iff_eq_zero]
    calc ŒºH[sardMoreiraBound n k Œ± (dim E)] (f '' s)
      _ ‚â§ ŒºH[sardMoreiraBound n k Œ± (dim E)] (f '' ‚ãÉ N, t N) := by
        gcongr
        intro x hx
        rcases (h_isBigO x hx).exists_nonneg with ‚ü®C, hC‚ÇÄ, hC‚ü©
        rcases exists_nat_gt C with ‚ü®N, hN‚ü©
        refine Set.mem_iUnion_of_mem N ?_
        use hx
        rw [Asymptotics.IsBigOWith_def] at hC
        refine hC.mono fun y hy ‚Ü¶ ?_
        rw [Real.norm_of_nonneg (by positivity)] at hy
        grw [hy, dist_eq_norm_sub, hN, Real.norm_of_nonneg (by positivity)]
        gcongr
        simp
      _ ‚â§ ‚àë' N, ŒºH[sardMoreiraBound n k Œ± (dim E)] (f '' t N) := by
        grw [Set.image_iUnion, measure_iUnion_le]
      _ = 0 := by
        rw [ENNReal.tsum_eq_zero]
        intro N
        apply this
        ¬∑ exact fun x hx ‚Ü¶ hcE x hx.1
        ¬∑ exact fun x hx ‚Ü¶ h_isBigO x hx.1
        ¬∑ exact measure_mono_null (Set.sep_subset _ _) hs
        ¬∑ exact ‚ü®N + 1, by positivity, fun x hx ‚Ü¶ mod_cast hx.2‚ü©
  rcases H with ‚ü®cF, hcF‚ÇÄ, hcF‚ü©
  wlog hcE‚ÇÄ : cE ‚â† 0 generalizing cE
  ¬∑ refine @this (cE + 1) (fun x hx ‚Ü¶ ?_) (by positivity)
    grw [‚Üê le_self_add]
    exact hcE x hx
  simpa [hs] using Moreira2001.hausdorffMeasure_image_le_mul hk hn hcE‚ÇÄ hcF‚ÇÄ.ne' hcE hcF

theorem hausdorffMeasure_image_null_of_isLittleO {X : Type*} [MetricSpace X]
    [MeasurableSpace E] [BorelSpace E]
    [MeasurableSpace F] [BorelSpace F]
    [MeasurableSpace X] [BorelSpace X]
    [FiniteDimensional ‚Ñù E] [FiniteDimensional ‚Ñù F]
    {f : E √ó F ‚Üí X} {s : Set (E √ó F)} {n : ‚Ñï} {cE : NNReal} (hk : k ‚â† 0) (hnp : dim E < n)
    (hn : dim E + dim F ‚â§ n)
    (hcE : ‚àÄ x ‚àà s, ‚àÄ·∂† y in ùìù (x, x), y.1.2 = y.2.2 ‚Üí dist (f y.1) (f y.2) ‚â§ cE * dist y.1 y.2)
    (h_isLittleO : ‚àÄ x ‚àà s, (fun y ‚Ü¶ dist (f (x.1, y)) (f x)) =o[ùìù x.2]
      (fun y ‚Ü¶ ‚Äñy - x.2‚Äñ ^ (k + Œ± : ‚Ñù))) :
    ŒºH[sardMoreiraBound n k Œ± (dim E)] (f '' s) = 0 := by
  wlog H : ‚àÉ N : ‚Ñï, s ‚äÜ ball 0 N generalizing s
  ¬∑ rw [‚Üê nonpos_iff_eq_zero]
    calc ŒºH[sardMoreiraBound n k Œ± (dim E)] (f '' s)
      _ = ŒºH[sardMoreiraBound n k Œ± (dim E)] (f '' ‚ãÉ N : ‚Ñï, s ‚à© ball 0 N) := by
        rw [‚Üê Set.inter_iUnion, iUnion_ball_nat, Set.inter_univ]
      _ ‚â§ ‚àë' N : ‚Ñï, ŒºH[sardMoreiraBound n k Œ± (dim E)] (f '' (s ‚à© ball 0 N)) := by
        rw [Set.image_iUnion]
        apply measure_iUnion_le
      _ = 0 := by
        rw [ENNReal.tsum_eq_zero]
        intro N
        apply this
        ¬∑ exact fun x hx ‚Ü¶ hcE x hx.1
        ¬∑ exact fun x hx ‚Ü¶ h_isLittleO x hx.1
        ¬∑ exact ‚ü®N, Set.inter_subset_right‚ü©
  rcases H with ‚ü®N, hN‚ü©
  wlog hcE‚ÇÄ : cE ‚â† 0 generalizing cE
  ¬∑ refine @this (cE + 1) (fun x hx ‚Ü¶ ?_) (by positivity)
    grw [‚Üê le_self_add]
    exact hcE x hx
  have Hbound : ‚àÄ cF : ‚Ñù‚â•0, cF ‚â† 0 ‚Üí
      ŒºH[sardMoreiraBound n k Œ± (dim E)] (f '' s) ‚â§
        boundCoeff E F n k Œ± * cE ^ dim E * cF ^ ((n - dim E) / (k + Œ±) : ‚Ñù) *
          (ŒºH[dim E].prod ŒºH[dim F] s) := by
    intro cF hcF‚ÇÄ
    apply Moreira2001.hausdorffMeasure_image_le_mul hk hn hcE‚ÇÄ hcF‚ÇÄ hcE
    intro x hx
    simp (disch := positivity) only [Asymptotics.isLittleO_iff, ‚Üê dist_eq_norm_sub,
      Real.norm_of_nonneg] at h_isLittleO
    exact h_isLittleO x hx (by positivity)
  suffices
      Filter.Tendsto
        (fun cF : ‚Ñù‚â•0 ‚Ü¶ boundCoeff E F n k Œ± * cE ^ dim E * cF ^ ((n - dim E) / (k + Œ±) : ‚Ñù) *
          (ŒºH[dim E].prod ŒºH[dim F] s))
        (ùìù[‚â†] 0) (ùìù 0) by
    rw [‚Üê nonpos_iff_eq_zero]
    exact ge_of_tendsto this <| eventually_mem_nhdsWithin.mono Hbound
  refine Filter.Tendsto.mono_left ?_ nhdsWithin_le_nhds
  apply Continuous.tendsto'
  ¬∑ have : (ŒºH[dim E].prod ŒºH[dim F]) s ‚â† ‚ä§ := by
      grw [‚Üê lt_top_iff_ne_top, hN]
      exact measure_ball_lt_top
    have : boundCoeff E F n k Œ± * cE ^ dim E ‚â† ‚ä§ := by
      rw [boundCoeff]
      apply_rules [ENNReal.mul_ne_top, ENNReal.div_ne_top, ENNReal.inv_ne_top.mpr,
        ENNReal.rpow_ne_top_of_nonneg, mul_ne_zero, IsOpen.measure_ne_zero, isOpen_ball,
        ENNReal.pow_ne_top]
      ¬∑ positivity
      ¬∑ simp
      ¬∑ simp
      ¬∑ simp
      ¬∑ simp
    fun_prop (disch := assumption)
  ¬∑ suffices (0 : ‚Ñù) < (n - dim E) / (k + Œ±) by simp [this]
    refine div_pos (sub_pos_of_lt <| mod_cast hnp) ?_
    have := Œ±.2.1; positivity

theorem hausdorffMeasure_image_piProd_fst_null_of_isBigO_isLittleO
    [MeasurableSpace E] [BorelSpace E]
    [MeasurableSpace F] [BorelSpace F]
    [MeasurableSpace G] [BorelSpace G]
    [FiniteDimensional ‚Ñù E] [FiniteDimensional ‚Ñù F]
    {f : E √ó F ‚Üí G} {s : Set (E √ó F)} {n : ‚Ñï} (hk : k ‚â† 0) (hnp : dim E < n)
    (hn : dim E + dim F ‚â§ n)
    (h_contDiff : ‚àÄ x ‚àà s, ContDiffAt ‚Ñù 1 f x)
    (h_isBigO : ‚àÄ x ‚àà s, (fun y ‚Ü¶ f (x.1, y) - f x) =O[ùìù x.2] (fun y ‚Ü¶ ‚Äñy - x.2‚Äñ ^ (k + Œ± : ‚Ñù)))
    (h_isLittleO : ‚àÄ·µê x ‚àÇ(ŒºH[dim E].prod ŒºH[dim F]), x ‚àà s ‚Üí
      (fun y ‚Ü¶ f (x.1, y) - f x) =o[ùìù x.2] (fun y ‚Ü¶ ‚Äñy - x.2‚Äñ ^ (k + Œ± : ‚Ñù))) :
    ŒºH[sardMoreiraBound n k Œ± (dim E)] (Pi.prod Prod.fst f '' s) = 0 := by
  set g := Pi.prod Prod.fst f
  set d := sardMoreiraBound n k Œ± (dim E)
  have hgf (x y) : dist (g x) (g y) = max (‚Äñx.1 - y.1‚Äñ) (‚Äñf x - f y‚Äñ) := by
    simp [g, dist_eq_norm_sub]
  wlog H : ‚àÉ cE : ‚Ñù‚â•0, cE ‚â† 0 ‚àß ‚àÄ x ‚àà s,
    ‚àÄ·∂† y in ùìù (x, x), y.1.2 = y.2.2 ‚Üí dist (g y.1) (g y.2) ‚â§ cE * dist y.1 y.2 generalizing s
  ¬∑ set t : ‚Ñï ‚Üí Set (E √ó F) := fun N ‚Ü¶
      {x | ‚àÄ·∂† y in ùìù (x, x), y.1.2 = y.2.2 ‚Üí dist (g y.1) (g y.2) ‚â§ (N + 1) * dist y.1 y.2}
    rw [‚Üê nonpos_iff_eq_zero]
    calc
      ŒºH[d] (g '' s) ‚â§ ŒºH[d] (g '' ‚ãÉ N, s ‚à© t N) := by
        gcongr
        intro x hx
        rcases (h_contDiff x hx).hasStrictFDerivAt (by simp) |>.isBigO_sub.bound with ‚ü®C, hC‚ü©
        rcases exists_nat_gt C with ‚ü®N, hN‚ü©
        refine Set.mem_iUnion_of_mem N ‚ü®hx, ?_‚ü©
        refine hC.mono fun y hy hy_eq ‚Ü¶ ?_
        grw [hgf, hy, max_le_iff, hN, dist_eq_norm_sub]
        constructor
        ¬∑ grw [‚Üê le_add_of_nonneg_left]
          ¬∑ simp [Prod.norm_def]
          ¬∑ positivity
        ¬∑ gcongr
          simp
      _ ‚â§ ‚àë' N, ŒºH[d] (g '' (s ‚à© t N)) := by
        simp only [Set.image_iUnion]
        apply measure_iUnion_le
      _ = 0 := by
        rw [ENNReal.tsum_eq_zero]
        intro N
        apply this
        ¬∑ exact fun x hx ‚Ü¶ h_contDiff x hx.1
        ¬∑ exact fun x hx ‚Ü¶ h_isBigO x hx.1
        ¬∑ grw [Set.inter_subset_left]
          exact h_isLittleO
        ¬∑ exact ‚ü®N + 1, by positivity, fun x hx ‚Ü¶ hx.2‚ü©
  rcases H with ‚ü®cE, hcE‚ÇÄ, hcE‚ü©
  set t : Set (E √ó F) :=
    {x | (fun y ‚Ü¶ g (x.1, y) - g x) =o[ùìù x.2] (fun y ‚Ü¶ ‚Äñy - x.2‚Äñ ^ (k + Œ± : ‚Ñù))}
  have ht : ŒºH[d] (g '' (s ‚à© t)) = 0 :=
    hausdorffMeasure_image_null_of_isLittleO hk hnp hn (fun x hx ‚Ü¶ hcE x hx.1) fun x hx ‚Ü¶ by
      simpa [t, dist_eq_norm_sub] using hx.2
  have ht' : ŒºH[d] (g '' (s \ t)) = 0 := by
    apply hausdorffMeasure_image_null_of_isBigO hk hn (fun x hx ‚Ü¶ hcE x hx.1)
    ¬∑ intro x hx
      refine .trans ?_ (h_isBigO x hx.1)
      refine .of_norm_norm ?_
      simp only [‚Üê dist_eq_norm_sub, hgf]
      simp [Asymptotics.isBigO_refl]
    ¬∑ refine measure_mono_null ?_ h_isLittleO
      rintro x ‚ü®hxs, hxt‚ü© hxs'
      specialize hxs' hxs
      apply hxt
      refine Asymptotics.IsBigO.trans_isLittleO ?_ hxs'
      refine .of_norm_norm ?_
      simp only [‚Üê dist_eq_norm_sub, hgf]
      simp [Asymptotics.isBigO_refl]
  rw [‚Üê Set.inter_union_diff s t, Set.image_union]
  exact measure_union_null ht ht'

theorem hausdorffMeasure_image_piProd_fst_null_of_fderiv_comp_inr_zero
    [MeasurableSpace E] [BorelSpace E] [MeasurableSpace G] [BorelSpace G]
    [Nontrivial F] [FiniteDimensional ‚Ñù E] [FiniteDimensional ‚Ñù F]
    {f : E √ó F ‚Üí G} {s : Set (E √ó F)} (hf : ‚àÄ x ‚àà s, ContDiffMoreiraHolderAt k Œ± f x) (hk : k ‚â† 0)
    (hs : ‚àÄ x ‚àà s, fderiv ‚Ñù f x ‚àòL .inr ‚Ñù E F = 0) :
    ŒºH[sardMoreiraBound (dim E + dim F) k Œ± (dim E)]
      (Pi.prod Prod.fst f '' s) = 0 := by
  rcases Nat.exists_add_one_eq.mpr (pos_iff_ne_zero.mpr hk) with ‚ü®k, rfl‚ü©
  suffices ‚àÄ œà ‚àà (Atlas.main k Œ± s).charts,
      ŒºH[sardMoreiraBound (dim E + dim F) (k + 1) Œ± (dim E)]
        ((Pi.prod Prod.fst f ‚àò œà) '' œà.set) = 0 by
    rw [‚Üê measure_biUnion_null_iff] at this
    ¬∑ refine measure_mono_null ?_ this
      simp only [Set.image_comp, ‚Üê Set.image_iUnion‚ÇÇ]
      gcongr
      refine (Atlas.main k Œ± s).subset_biUnion_isLargeAt.trans ?_
      gcongr
      apply Set.sep_subset
    ¬∑ apply Atlas.countable
  intro œà hœà
  set g := Pi.prod Prod.fst (f ‚àò œà)
  suffices ŒºH[sardMoreiraBound (dim E + dim F) (k + 1) Œ± (dim E)] (g '' œà.set) = 0 by
    simpa [g] using this
  apply hausdorffMeasure_image_piProd_fst_null_of_isBigO_isLittleO
  ¬∑ simp
  ¬∑ simp [Module.finrank_pos]
  ¬∑ grw [œà.finrank_le]
  ¬∑ intro x hx
    refine .comp _ ?_ (œà.contDiffAt hx)
    exact hf _ (œà.mapsTo hx) |>.contDiffAt.of_le (by simp)
  ¬∑ intro x hx
    push_cast
    apply Atlas.isBigO_main_sub_of_fderiv_zero_right hœà hx
    ¬∑ filter_upwards [eventually_mem_nhdsWithin] with x hx using hf _ hx
    ¬∑ filter_upwards [eventually_mem_nhdsWithin] using hs
  ¬∑ push_cast
    filter_upwards [Besicovitch.ae_tendsto_measure_sectr_inter_closedBall_div
      (ŒºH[dim E]) (ŒºH[dim œà.Dom]) (measurableSet_closure (s := œà.set))] with x hx hœàx
    apply Atlas.isLittleO_main_sub_of_fderiv_zero_right hœà hœàx
    ¬∑ filter_upwards [eventually_mem_nhdsWithin] with y hy using hf _ hy
    ¬∑ filter_upwards [eventually_mem_nhdsWithin] using hs
    ¬∑ convert hx
      simp [Set.indicator_of_mem (subset_closure hœàx)]

theorem hausdorffMeasure_image_piProd_fst_null_of_finrank_eq
    [MeasurableSpace E] [BorelSpace E] [MeasurableSpace G] [BorelSpace G]
    [Nontrivial F] [FiniteDimensional ‚Ñù E] [FiniteDimensional ‚Ñù F]
    {f : E √ó F ‚Üí G} {s : Set (E √ó F)} (hf : ‚àÄ x ‚àà s, ContDiffMoreiraHolderAt k Œ± f x) (hk : k ‚â† 0)
    (hs : ‚àÄ x ‚àà s, dim (fderiv ‚Ñù (Pi.prod Prod.fst f) x).range = dim E) :
    ŒºH[sardMoreiraBound (dim E + dim F) k Œ± (dim E)]
      (Pi.prod Prod.fst f '' s) = 0 := by
  apply hausdorffMeasure_image_piProd_fst_null_of_fderiv_comp_inr_zero hf hk
  intro x hx
  rw [‚Üê ContinuousLinearMap.coe_inj, ContinuousLinearMap.coe_comp, ContinuousLinearMap.coe_inr,
    ContinuousLinearMap.coe_zero, ‚Üê LinearMap.finrank_range_prod_fst_iff_comp_inr_eq_zero,
    ‚Üê hs x hx]
  suffices fderiv ‚Ñù (Pi.prod Prod.fst f) x = .prod (.fst ‚Ñù E F) (fderiv ‚Ñù f x) by
    -- TODO: introduce&use `ContinuousLinearMap.rank`/`ContinuousLinearMap.finrank`?
    generalize H : fderiv ‚Ñù (Pi.prod Prod.fst f) x = f'
    rw [H] at this
    subst f'
    rfl
  unfold Pi.prod
  rw [DifferentiableAt.fderiv_prodMk (by fun_prop), fderiv_fst]
  exact hf _ hx |>.differentiableAt hk

theorem hausdorffMeasure_image_nhdsWithin_null_of_finrank_eq
    [CompleteSpace F] [MeasurableSpace F] [BorelSpace F]
    (hp_dom : p < dim E) (hk : k ‚â† 0) {f : E ‚Üí F} {s : Set E}
    (hf : ‚àÄ x ‚àà s, ContDiffMoreiraHolderAt k Œ± f x)
    (hs : ‚àÄ x ‚àà s, (fderiv ‚Ñù f x).finrank = p) {a : E} (ha : a ‚àà s) :
    ‚àÉ t ‚àà ùìù[s] a, ŒºH[sardMoreiraBound (dim E) k Œ± p] (f '' t) = 0 := by
  have : FiniteDimensional ‚Ñù E := .of_finrank_pos (by grind)
  obtain ‚ü®Ker, Range, Coker, eDom, eCod, hfin‚ÇÅ, hfin‚ÇÇ, hdimKer, hdimRange, haeDom, hinv,
      hcdmh, hfst‚ü© :
      ‚àÉ (Ker : Submodule ‚Ñù E) (Range Coker : Submodule ‚Ñù F)
        (eDom : OpenPartialHomeomorph E (Range √ó Ker)) (eCod : F ‚âÉL[‚Ñù] (Range √ó Coker)),
        FiniteDimensional ‚Ñù Ker ‚àß FiniteDimensional ‚Ñù Range ‚àß
        dim Ker = dim E - p ‚àß dim Range = p ‚àß a ‚àà eDom.source ‚àß
        (fderiv ‚Ñù eDom a).IsInvertible ‚àß
        (‚àÄ x ‚àà s, ContDiffMoreiraHolderAt k Œ± eDom x) ‚àß
        (‚àÄ x, (eDom x).1 = (eCod (f x)).1) := by
    have hker : (fderiv ‚Ñù f a).ker.ClosedComplemented := .of_finiteDimensional _
    have hrange : (fderiv ‚Ñù f a).range.ClosedComplemented := .of_finiteDimensional _
    use (fderiv ‚Ñù f a).ker, (fderiv ‚Ñù f a).range, hrange.choose.ker
    have hdf := (hf a ha).contDiffAt.hasStrictFDerivAt (by simpa [Nat.one_le_iff_ne_zero])
    set eDom := hdf.implicitToOpenPartialHomeomorphOfComplementedKerRange _ _ hker hrange
    refine ‚ü®eDom,
      .equivOfRightInverse hrange.choose (Submodule.subtypeL _) hrange.choose_spec,
      inferInstance, inferInstance, ?_, ?_, ?_, ?_, ?_, ?_‚ü©
    ¬∑ rw [‚Üê (fderiv ‚Ñù f a).finrank_range_add_finrank_ker, ‚Üê hs a ha, ContinuousLinearMap.finrank,
        Nat.add_sub_cancel_left]
    ¬∑ exact hs a ha
    ¬∑ simp [eDom]
    ¬∑ simpa using hdf.implicitFunctionDataOfComplementedKerRange _ _ hker hrange
        |>.isInvertible_fderiv_prodFun
    ¬∑ intro x hx
      rw [hdf.coe_implicitToOpenPartialHomeomorphOfComplementedKerRange hker hrange]
      exact .prodMk (.comp (ContinuousLinearMap.contDiffMoreiraHolderAt _) (hf x hx) hk)
        (ContinuousLinearMap.contDiffMoreiraHolderAt _)
    ¬∑ intro x
      simp [eDom]
  have : Nontrivial Ker := by
    apply Module.nontrivial_of_finrank_pos (R := ‚Ñù)
    grind
  set g : (Range √ó Ker) ‚Üí Coker := fun x ‚Ü¶ (eCod <| f <| eDom.symm x).2
  set t := eDom.target ‚à© eDom.symm ‚Åª¬π' {x ‚àà s | (fderiv ‚Ñù eDom x).IsInvertible}
  have hgt : ‚àÄ x ‚àà t, ContDiffMoreiraHolderAt k Œ± g x := by
    intro x hx
    refine .comp .snd (eCod.contDiffMoreiraHolderAt.comp (.comp ?_ ?_ hk) hk) hk
    ¬∑ exact hf _ hx.2.1
    ¬∑ exact eDom.contDiffMoreiraHolderAt_symm hx.1 hx.2.2 (hcdmh _ hx.2.1)
  have hg_eqOn : eDom.target.EqOn (Pi.prod Prod.fst g) (eCod ‚àò f ‚àò eDom.symm) := by
    intro x hx
    ext <;> simp [‚Üê hfst, hx, g]
  have hgdim : ‚àÄ x ‚àà t, (fderiv ‚Ñù (Pi.prod Prod.fst g) x).finrank = dim Range := by
    intro x hx
    have hd : DifferentiableAt ‚Ñù eDom.symm x :=
      eDom.contDiffMoreiraHolderAt_symm hx.1 hx.2.2 (hcdmh _ hx.2.1) |>.differentiableAt hk
    rw [hdimRange, hg_eqOn.eventuallyEq_of_mem (eDom.open_target.mem_nhds hx.1) |>.fderiv_eq,
      fderiv_comp, eCod.fderiv, eCod.finrank_comp_left, fderiv_comp,
      ContinuousLinearMap.finrank_comp_eq_left_of_surjective, hs _ hx.2.1]
    ¬∑ exact eDom.surjective_fderiv_symm hx.1 hx.2.2
    ¬∑ exact hf _ hx.2.1 |>.differentiableAt hk
    ¬∑ exact hd
    ¬∑ exact eCod.differentiableAt
    ¬∑ exact hf _ hx.2.1 |>.differentiableAt hk |>.comp _ hd
  refine ‚ü®eDom.symm '' t, ?_, ?_‚ü©
  ¬∑ convert_to eDom.symm '' t ‚àà Filter.map eDom.symm (ùìù[t] (eDom a))
    ¬∑ rw [eDom.nhdsWithin_target_inter (eDom.mapsTo haeDom),
        eDom.symm.map_nhdsWithin_preimage_eq (eDom.mapsTo haeDom),
        Set.setOf_and, eDom.leftInvOn haeDom, Set.setOf_mem_eq, nhdsWithin_inter_of_mem']
      apply mem_nhdsWithin_of_mem_nhds
      exact (hcdmh _ ha).contDiffAt.eventually_isInvertible_fderiv hinv (by positivity)
    ¬∑ exact Filter.image_mem_map self_mem_nhdsWithin
  ¬∑ have := hausdorffMeasure_image_piProd_fst_null_of_finrank_eq hgt hk hgdim
    rw [hdimKer, hdimRange, Nat.add_sub_cancel' hp_dom.le] at this
    convert (eCod.symm.lipschitz.hausdorffMeasure_image_null (by positivity) this) using 2
    rw [Set.image_image, Set.image_image]
    apply Set.EqOn.image_eq
    intro x hx
    simp only [hg_eqOn hx.1, Function.comp_apply, eCod.symm_apply_apply]

theorem hausdorffMeasure_image_null_of_finrank_eq [MeasurableSpace F] [BorelSpace F]
    [CompleteSpace F] (hp_dom : p < dim E) (hk : k ‚â† 0) {f : E ‚Üí F} {s : Set E}
    (hf : ‚àÄ x ‚àà s, ContDiffMoreiraHolderAt k Œ± f x)
    (hs : ‚àÄ x ‚àà s, dim (fderiv ‚Ñù f x).range = p) :
    ŒºH[sardMoreiraBound (dim E) k Œ± p] (f '' s) = 0 := by
  have : FiniteDimensional ‚Ñù E := .of_finrank_pos (by grind)
  rw [‚Üê coe_toOuterMeasure, ‚Üê OuterMeasure.comap_apply]
  refine measure_null_of_locally_null _ fun x hx ‚Ü¶ ?_
  apply hausdorffMeasure_image_nhdsWithin_null_of_finrank_eq <;> assumption

end Moreira2001

open UniformSpace in
theorem hausdorffMeasure_sardMoreiraBound_image_null_of_finrank_le
    [MeasurableSpace F] [BorelSpace F]
    (hp_dom : p < dim E) (hk : k ‚â† 0) {f : E ‚Üí F} {s : Set E}
    (hf : ‚àÄ x ‚àà s, ContDiffMoreiraHolderAt k Œ± f x)
    (hs : ‚àÄ x ‚àà s, dim (fderiv ‚Ñù f x).range ‚â§ p) :
    ŒºH[sardMoreiraBound (dim E) k Œ± p] (f '' s) = 0 := by
  wlog hF : CompleteSpace F generalizing F
  ¬∑ borelize (Completion F)
    set e : F ‚Üí‚Çó·µ¢[‚Ñù] Completion F := Completion.toCompl‚Çó·µ¢
    rw [‚Üê e.isometry.hausdorffMeasure_image, Set.image_image]
    apply this
    ¬∑ exact fun x hx ‚Ü¶ (hf x hx).continuousLinearMap_comp e.toContinuousLinearMap
    ¬∑ intro x hx
      grw [fderiv_comp', ‚Üê hs x hx]
      ¬∑ change dim (LinearMap.range ((fderiv ‚Ñù e (f x)).toLinearMap ‚àò‚Çó
          (fderiv ‚Ñù f x).toLinearMap)) ‚â§ _
        rw [LinearMap.range_comp, ‚Üê LinearMap.range_domRestrict, LinearMap.finrank_range_of_inj]
        simp [LinearMap.domRestrict, e, Function.Injective,
          show fderiv ‚Ñù (‚Üë) (f x) = e.toContinuousLinearMap from e.toContinuousLinearMap.fderiv]
      ¬∑ exact e.toContinuousLinearMap.differentiableAt
      ¬∑ exact (hf x hx).differentiableAt hk
    ¬∑ infer_instance
    ¬∑ left; positivity
  -- Apply the Moreira2001 theorem to each of the sets where the rank is exactly `p' ‚â§ p`.
  have h_apply : ‚àÄ p' ‚â§ p,
      ŒºH[sardMoreiraBound (dim E) k Œ± p'] (f '' {x ‚àà s | dim (fderiv ‚Ñù f x).range = p'}) = 0 := by
    intro p' hp'
    apply Moreira2001.hausdorffMeasure_image_null_of_finrank_eq
    ¬∑ grind
    ¬∑ exact hk
    ¬∑ exact fun x hx ‚Ü¶ hf x hx.1
    ¬∑ simp
  -- Since $s$ is the union of the sets where the rank is exactly $p'$ for $p' \leq p$,
  -- we can use the countable subadditivity of the Hausdorff measure.
  have h_union : f '' s = ‚ãÉ p' ‚â§ p, f '' {x ‚àà s | dim (fderiv ‚Ñù f x).range = p'} := by
    ext y
    simp only [Set.mem_image, Set.mem_iUnion, Set.mem_setOf_eq, exists_prop]
    exact ‚ü®fun ‚ü®x, hx, hx'‚ü© ‚Ü¶ ‚ü®_, hs x hx, x, ‚ü®hx, rfl‚ü©, hx'‚ü©,
      fun ‚ü®i, hi, x, hx, hx'‚ü© ‚Ü¶ ‚ü®x, hx.1, hx'‚ü©‚ü©
  simp only [h_union, measure_iUnion_null_iff]
  intro p' hp'
  rw [‚Üê nonpos_iff_eq_zero, ‚Üê h_apply p' hp']
  apply hausdorffMeasure_mono
  exact monotone_sardMoreiraBound _ hk _ hp'

theorem dimH_image_le_sardMoreiraBound_of_finrank_le
    (hp_dom : p < dim E) (hk : k ‚â† 0) {f : E ‚Üí F} {s : Set E}
    (hf : ‚àÄ x ‚àà s, ContDiffMoreiraHolderAt k Œ± f x)
    (hs : ‚àÄ x ‚àà s, dim (fderiv ‚Ñù f x).range ‚â§ p) :
    dimH (f '' s) ‚â§ sardMoreiraBound (dim E) k Œ± p := by
  borelize F
  apply dimH_le_of_hausdorffMeasure_ne_top
  simp [hausdorffMeasure_sardMoreiraBound_image_null_of_finrank_le hp_dom hk hf hs]
