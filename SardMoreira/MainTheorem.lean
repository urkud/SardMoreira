import Mathlib
import SardMoreira.ContDiffMoreiraHolder
import SardMoreira.ImplicitFunction
import SardMoreira.LinearAlgebra
import SardMoreira.ChartEstimates
import SardMoreira.WithRPowDist

open scoped unitInterval NNReal Topology ENNReal
open MeasureTheory Measure Metric

local notation "dim" => Module.finrank ‚Ñù

-- TODO: generalize to semilinear maps
protected noncomputable def ContinuousLinearMap.finrank {R M N : Type*} [Semiring R]
    [AddCommMonoid M] [Module R M] [TopologicalSpace M]
    [AddCommMonoid N] [Module R N] [TopologicalSpace N]
    (f : M ‚ÜíL[R] N) : ‚Ñï :=
  Module.finrank R (LinearMap.range f)

theorem ContinuousLinearMap.finrank_comp_eq_left_of_surjective {R M N P : Type*} [Semiring R]
    [AddCommMonoid M] [Module R M] [TopologicalSpace M]
    [AddCommMonoid N] [Module R N] [TopologicalSpace N]
    [AddCommMonoid P] [Module R P] [TopologicalSpace P]
    (g : N ‚ÜíL[R] P) {f : M ‚ÜíL[R] N} (hf : Function.Surjective f) :
    (g ‚àòL f).finrank = g.finrank := by
  -- Since $f$ is surjective, the image of $g \circ f$ is the same as the image of $g$.
  have h_range : LinearMap.range (g.comp f) = LinearMap.range g :=
    SetLike.coe_injective <| hf.range_comp g
  rw [ContinuousLinearMap.finrank, ContinuousLinearMap.finrank, h_range]

theorem ContinuousLinearMap.finrank_comp_eq_right_of_injective {R M N P : Type*} [Semiring R]
    [AddCommMonoid M] [Module R M] [TopologicalSpace M]
    [AddCommMonoid N] [Module R N] [TopologicalSpace N]
    [AddCommMonoid P] [Module R P] [TopologicalSpace P]
    {g : N ‚ÜíL[R] P} (hg : Function.Injective g) (f : M ‚ÜíL[R] N) :
    (g ‚àòL f).finrank = f.finrank := by
  -- Since $g$ is injective, the range of $g \circ f$ is isomorphic to the range of $f$.
  have h_iso : LinearMap.range (g.comp f) ‚âÉ‚Çó[R] LinearMap.range f := by
    symm;
    refine' { Equiv.ofBijective ( fun x => ‚ü® g x, by aesop ‚ü© ) ‚ü® fun x y hxy => _, fun x => _ ‚ü© with .. } <;> aesop;
  exact h_iso.finrank_eq

@[simp]
theorem ContinuousLinearEquiv.finrank_comp_left {R M N N' : Type*} [Semiring R]
    [AddCommMonoid M] [Module R M] [TopologicalSpace M]
    [AddCommMonoid N] [Module R N] [TopologicalSpace N]
    [AddCommMonoid N'] [Module R N'] [TopologicalSpace N']
    (e : N ‚âÉL[R] N') (f : M ‚ÜíL[R] N) : (e ‚àòL f : M ‚ÜíL[R] N').finrank = f.finrank := by
  apply ContinuousLinearMap.finrank_comp_eq_right_of_injective
  exact e.injective

@[simp]
theorem ContinuousLinearEquiv.finrank_comp_right {R M M' N : Type*} [Semiring R]
    [AddCommMonoid M] [Module R M] [TopologicalSpace M]
    [AddCommMonoid N] [Module R N] [TopologicalSpace N]
    [AddCommMonoid M'] [Module R M'] [TopologicalSpace M']
    (f : M ‚ÜíL[R] N) (e : M' ‚âÉL[R] M) : (f ‚àòL e : M' ‚ÜíL[R] N).finrank = f.finrank := by
  apply ContinuousLinearMap.finrank_comp_eq_left_of_surjective
  exact e.surjective

theorem LipschitzWith.hausdorffMeasure_image_null {X Y : Type*} [EMetricSpace X] [EMetricSpace Y]
    [MeasurableSpace X] [BorelSpace X] [MeasurableSpace Y] [BorelSpace Y] {K : NNReal} {f : X ‚Üí Y}
    (h : LipschitzWith K f) {d : ‚Ñù} (hd : 0 ‚â§ d) {s : Set X} (hs : ŒºH[d] s = 0) :
    ŒºH[d] (f '' s) = 0 := by
  grw [‚Üê nonpos_iff_eq_zero, h.hausdorffMeasure_image_le hd, hs, mul_zero]

/-- Moreira's upper estimate on the Hausdorff dimension of the image of the set of points $x$
such that `fderiv ‚Ñù f x` has rank at most `p < min n m`,
provided that `f` is a $$C^{k+(\alpha)}$$-map
from an `n`-dimensional space to an `m`-dimensional space.

Note that the estimate does not depend on `m`. -/
noncomputable def sardMoreiraBound (n k : ‚Ñï) (Œ± : I) (p : ‚Ñï) : ‚Ñù‚â•0 :=
  p + (n - p) / (k + ‚ü®Œ±, Œ±.2.1‚ü©)

theorem monotone_sardMoreiraBound (n : ‚Ñï) {k : ‚Ñï} (hk : k ‚â† 0) (Œ± : I) :
    Monotone (sardMoreiraBound n k Œ±) := by
  apply monotone_nat_of_le_succ
  intro p
  rcases Œ± with ‚ü®Œ±, hŒ±‚ÇÄ, hŒ±‚ÇÅ‚ü©
  simp only [sardMoreiraBound, field]
  rw [‚Üê NNReal.coe_le_coe]
  push_cast [tsub_add_eq_tsub_tsub]
  grw [@NNReal.coe_sub_def _ 1, ‚Üê le_max_left, ‚Üê sub_nonneg]
  push_cast
  linarith only [hŒ±‚ÇÄ, show (1 : ‚Ñù) ‚â§ k by norm_cast; grind]

@[gcongr]
theorem sardMoreiraBound_le_sardMoreiraBound {m n k l p q : ‚Ñï} (hl : l ‚â† 0) (hmn : m ‚â§ n)
    (hlk : l ‚â§ k) (hpq : p ‚â§ q) (Œ± : I) :
    sardMoreiraBound m k Œ± p ‚â§ sardMoreiraBound n l Œ± q := by
  grw [‚Üê monotone_sardMoreiraBound n hl Œ± hpq]
  unfold sardMoreiraBound
  gcongr

variable {E F G : Type*}
  [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
  [NormedAddCommGroup F] [NormedSpace ‚Ñù F]
  [NormedAddCommGroup G] [NormedSpace ‚Ñù G]
  {k p : ‚Ñï} {Œ± : I}

namespace Moreira2001

theorem mkMetric'Pre_image_piProd_fst_null_of_isBigO_of_null
    [MeasurableSpace E] [BorelSpace E]
    [MeasurableSpace F] [BorelSpace F]
    [MeasurableSpace G] [BorelSpace G]
    [FiniteDimensional ‚Ñù E] [FiniteDimensional ‚Ñù F] [FiniteDimensional ‚Ñù G]
    {f : E √ó F ‚Üí G} {s : Set (E √ó F)} {n : ‚Ñï} (hsm : MeasurableSet s) (hk : k ‚â† 0) (hnp : dim E < n)
    (hn : dim E + dim F ‚â§ n)
    (h_contDiff : ‚àÉ U ‚àà ùìùÀ¢ s, ContDiffOn ‚Ñù 1 f U)
    (h_isBigO : ‚àÄ x ‚àà s, (fun y ‚Ü¶ f (x.1, y) - f x) =O[ùìù x.2] (fun y ‚Ü¶ ‚Äñy - x.2‚Äñ ^ (k + Œ± : ‚Ñù)))
    (hŒº‚ÇÄ : ŒºH[dim E].prod (ŒºH[dim F]) s = 0) {r : ‚Ñù‚â•0‚àû} (hr : 0 < r) :
    (OuterMeasure.mkMetric'.pre (fun s ‚Ü¶ EMetric.diam s ^ (sardMoreiraBound n k Œ± (dim E) : ‚Ñù)) r)
      (Pi.prod Prod.fst f '' s) = 0 := by

  sorry

theorem mkMetric'Pre_image_piProd_fst_null_of_isLittleO
    [MeasurableSpace E] [BorelSpace E]
    [MeasurableSpace F] [BorelSpace F]
    [MeasurableSpace G] [BorelSpace G]
    [FiniteDimensional ‚Ñù E] [FiniteDimensional ‚Ñù F] [FiniteDimensional ‚Ñù G]
    {Œº : Measure (E √ó F)} [Œº.IsAddHaarMeasure]
    {f : E √ó F ‚Üí G} {s : Set (E √ó F)} {n : ‚Ñï} (hsm : MeasurableSet s) (hk : k ‚â† 0) (hnp : dim E < n)
    (hn : dim E + dim F ‚â§ n)
    (h_contDiff : ‚àÉ U ‚àà ùìùÀ¢ s, ContDiffOn ‚Ñù 1 f U)
    (h_isBigO : ‚àÄ x ‚àà s, (fun y ‚Ü¶ f (x.1, y) - f x) =O[ùìù x.2] (fun y ‚Ü¶ ‚Äñy - x.2‚Äñ ^ (k + Œ± : ‚Ñù)))
    (hŒº‚ÇÄ : Œº s = 0) {r : ‚Ñù‚â•0‚àû} (hr : 0 < r) :
    (OuterMeasure.mkMetric'.pre (fun s ‚Ü¶ EMetric.diam s ^ (sardMoreiraBound n k Œ± (dim E) : ‚Ñù)) r)
      (Pi.prod Prod.fst f '' s) = 0 := by
  sorry

theorem hausdorffMeasure_image_piProd_fst_null_of_isBigO_isLittleO
    [MeasurableSpace E] [BorelSpace E]
    [MeasurableSpace F] [BorelSpace F]
    [MeasurableSpace G] [BorelSpace G]
    [FiniteDimensional ‚Ñù E] [FiniteDimensional ‚Ñù F] [FiniteDimensional ‚Ñù G]
    {f : E √ó F ‚Üí G} {s : Set (E √ó F)} {n : ‚Ñï} (hk : k ‚â† 0) (hnp : dim E < n)
    (hn : dim E + dim F ‚â§ n)
    (h_contDiff : ‚àÄ x ‚àà s, ContDiffAt ‚Ñù 1 f x)
    (h_isBigO : ‚àÄ x ‚àà s, (fun y ‚Ü¶ f (x.1, y) - f x) =O[ùìù x.2] (fun y ‚Ü¶ ‚Äñy - x.2‚Äñ ^ (k + Œ± : ‚Ñù)))
    (h_isLittleO : ‚àÄ·µê x ‚àÇ(ŒºH[dim E].prod ŒºH[dim F]), x ‚àà s ‚Üí
      (fun y ‚Ü¶ f (x.1, y) - f x) =o[ùìù x.2] (fun y ‚Ü¶ ‚Äñy - x.2‚Äñ ^ (k + Œ± : ‚Ñù))) :
    ŒºH[sardMoreiraBound n k Œ± (dim E)] (Pi.prod Prod.fst f '' s) = 0 := by
  generalize hd : (sardMoreiraBound n k Œ± (dim E) : ‚Ñù) = d
  generalize hg : Pi.prod Prod.fst f = g
  suffices ‚àÄ r, 0 < r ‚Üí (OuterMeasure.mkMetric'.pre (fun s ‚Ü¶ EMetric.diam s ^ d) r) (g '' s) = 0 by
    simpa only [hausdorffMeasure, ‚Üê toOuterMeasure_apply, mkMetric_toOuterMeasure,
      OuterMeasure.mkMetric, OuterMeasure.mkMetric', OuterMeasure.iSup_apply, ENNReal.iSup_eq_zero]
  intro r hr
  sorry
  -- wlog hs : Bornology.IsBounded s generalizing s
  -- ¬∑ rw [‚Üê Set.inter_univ s, ‚Üê iUnion_ball_nat 0, Set.inter_iUnion, Set.image_iUnion,
  --     measure_iUnion_null_iff]
  --   intro N
  --   apply this
  --   ¬∑ exact hsm.inter measurableSet_ball
  --   ¬∑ refine h_contDiff.imp fun U ‚Ü¶ And.imp_left <| Filter.le_def.mp ?_ _
  --     gcongr
  --     exact Set.inter_subset_left
  --   ¬∑ exact fun x hx ‚Ü¶ (h_isBigO x hx.1)


theorem hausdorffMeasure_image_piProd_fst_null_of_fderiv_comp_inr_zero
    [MeasurableSpace E] [BorelSpace E] [MeasurableSpace G] [BorelSpace G]
    [Nontrivial F] [FiniteDimensional ‚Ñù E] [FiniteDimensional ‚Ñù F] [FiniteDimensional ‚Ñù G]
    {f : E √ó F ‚Üí G} {s U : Set (E √ó F)} (hf : ContDiffMoreiraHolderOn k Œ± f s U) (hk : k ‚â† 0)
    (hs : ‚àÄ x ‚àà s, fderiv ‚Ñù f x ‚àòL .inr ‚Ñù E F = 0) :
    ŒºH[sardMoreiraBound (dim E + dim F) k Œ± (dim E)]
      (Pi.prod Prod.fst f '' s) = 0 := by
  rcases Nat.exists_add_one_eq.mpr hk.bot_lt with ‚ü®k, rfl‚ü©
  suffices ‚àÄ œà ‚àà (Atlas.main k Œ± s).charts,
      ŒºH[sardMoreiraBound (dim E + dim F) (k + 1) Œ± (dim E)]
        ((Pi.prod Prod.fst f ‚àò œà) '' œà.set) = 0 by
    rw [‚Üê measure_biUnion_null_iff] at this
    ¬∑ refine measure_mono_null ?_ this
      simp only [Set.image_comp, ‚Üê Set.image_iUnion‚ÇÇ]
      gcongr
      refine (Atlas.main k Œ± s).subset_biUnion_isLargeAt.trans ?_
      gcongr
      apply Set.sep_subset
    ¬∑ apply Atlas.countable
  intro œà hœà
  set g := Pi.prod Prod.fst (f ‚àò œà)
  suffices ŒºH[sardMoreiraBound (dim E + dim F) (k + 1) Œ± (dim E)] (g '' œà.set) = 0 by
    simpa [g] using this
  apply hausdorffMeasure_image_piProd_fst_null_of_isBigO_isLittleO
  ¬∑ simp
  ¬∑ simp [Module.finrank_pos]
  ¬∑ grw [œà.finrank_le]
  ¬∑ intro x hx
    refine .comp _ ?_ (œà.contDiffAt hx)
    exact hf.contDiffMoreiraHolderAt (œà.mapsTo hx) |>.contDiffAt.of_le (by simp)
  ¬∑ intro x hx
    push_cast
    apply Atlas.isBigO_main_sub_of_fderiv_zero_right hœà hx
    ¬∑ filter_upwards [eventually_mem_nhdsWithin] with x hx using hf.contDiffMoreiraHolderAt hx
    ¬∑ filter_upwards [eventually_mem_nhdsWithin] using hs
  ¬∑ push_cast
    filter_upwards [Besicovitch.ae_tendsto_measure_sectr_inter_closedBall_div
      (ŒºH[dim E]) (ŒºH[dim œà.Dom]) (measurableSet_closure (s := œà.set))] with x hx hœàx
    apply Atlas.isLittleO_main_sub_of_fderiv_zero_right hœà hœàx
    ¬∑ filter_upwards [eventually_mem_nhdsWithin] with y hy using hf.contDiffMoreiraHolderAt hy
    ¬∑ filter_upwards [eventually_mem_nhdsWithin] using hs
    ¬∑ convert hx
      simp [Set.indicator_of_mem (subset_closure hœàx)]

theorem hausdorffMeasure_image_piProd_fst_null_of_finrank_eq
    [MeasurableSpace E] [BorelSpace E] [MeasurableSpace G] [BorelSpace G]
    [Nontrivial F] [FiniteDimensional ‚Ñù E] [FiniteDimensional ‚Ñù F] [FiniteDimensional ‚Ñù G]
    {f : E √ó F ‚Üí G} {s U : Set (E √ó F)} (hf : ContDiffMoreiraHolderOn k Œ± f s U) (hk : k ‚â† 0)
    (hs : ‚àÄ x ‚àà s, dim (LinearMap.range <| fderiv ‚Ñù (Pi.prod Prod.fst f) x) = dim E) :
    ŒºH[sardMoreiraBound (dim E + dim F) k Œ± (dim E)]
      (Pi.prod Prod.fst f '' s) = 0 := by
  apply hausdorffMeasure_image_piProd_fst_null_of_fderiv_comp_inr_zero hf hk
  intro x hx
  rw [‚Üê ContinuousLinearMap.finrank_range_prod_fst_iff_comp_inr_eq_zero, ‚Üê hs x hx]
  suffices fderiv ‚Ñù (Pi.prod Prod.fst f) x = .prod (.fst ‚Ñù E F) (fderiv ‚Ñù f x) by
    -- TODO: introduce&use `ContinuousLinearMap.rank`/`ContinuousLinearMap.finrank`?
    generalize H : fderiv ‚Ñù (Pi.prod Prod.fst f) x = f'
    rw [H] at this
    subst f'
    rfl
  unfold Pi.prod
  rw [DifferentiableAt.fderiv_prodMk (by fun_prop), fderiv_fst]
  exact hf.contDiffMoreiraHolderAt hx |>.differentiableAt hk

theorem hausdorffMeasure_image_nhdsWithin_null_of_finrank_eq [MeasurableSpace F] [BorelSpace F]
    (hp_dom : p < dim E) (hp_cod : p < dim F) (hk : k ‚â† 0) {f : E ‚Üí F} {s U : Set E}
    (hf : ContDiffMoreiraHolderOn k Œ± f s U)
    (hs : ‚àÄ x ‚àà s, dim (LinearMap.range <| fderiv ‚Ñù f x) = p) {x : E} (hx : x ‚àà s) :
    ‚àÉ t ‚àà ùìù[s] x, ŒºH[sardMoreiraBound (dim E) k Œ± p] (f '' t) = 0 := by
  have : FiniteDimensional ‚Ñù E := .of_finrank_pos (by grind)
  have : FiniteDimensional ‚Ñù F := .of_finrank_pos (Nat.zero_lt_of_lt hp_cod)
  have hker := ContinuousLinearMap.ker_closedComplemented_of_finiteDimensional_range (fderiv ‚Ñù f x)
  have hrange := Submodule.ClosedComplemented.of_finiteDimensional (LinearMap.range (fderiv ‚Ñù f x))
  rcases hf.exists_openPartialHomeomorph_conj_piProd_fst hk hx hker hrange
    with ‚ü®epq, epr, g, hx_epq, hepqU, hepq, hepq_symm, heqOn‚ü©
  use s ‚à© epq.source, inter_mem_nhdsWithin _ (epq.open_source.mem_nhds hx_epq)
  set t := epq.target ‚à© epq.symm ‚Åª¬π' s
  have heqOn_g : Set.EqOn (Prod.snd ‚àò epr ‚àò f ‚àò epq.symm) g epq.target := heqOn.comp_left
  have hg : ContDiffMoreiraHolderOn k Œ± g t epq.target := by
    refine .congr_eqOn ?_ heqOn_g
    refine .continuousLinearMap_comp ?_
      (.snd ‚Ñù (LinearMap.range (fderiv ‚Ñù f x)) (LinearMap.ker hrange.choose))
    refine .continuousLinearMap_comp ?_ epr.toContinuousLinearMap
    refine hf.comp hepq_symm (epq.symm_mapsTo.mono_right hepqU) ?_ hk
    exact (Set.mapsTo_preimage _ _).mono_left Set.inter_subset_right
  have hrange_ker :
      dim (LinearMap.range (fderiv ‚Ñù f x)) + dim (LinearMap.ker (fderiv ‚Ñù f x)) = dim E :=
    LinearMap.finrank_range_add_finrank_ker (fderiv ‚Ñù f x : E ‚Üí‚Çó[‚Ñù] F)
  have : Nontrivial ‚Ü•(LinearMap.ker (fderiv ‚Ñù f x)) := by
    apply Module.nontrivial_of_finrank_pos (R := ‚Ñù)
    rwa [‚Üê hrange_ker, hs x hx, lt_add_iff_pos_right] at hp_dom
  have := hausdorffMeasure_image_piProd_fst_null_of_finrank_eq hg hk ?_
  ¬∑ rw [hrange_ker, hs x hx] at this
    refine measure_mono_null (Set.mapsTo_iff_image_subset.mp ?_)
      (epr.symm.lipschitz.hausdorffMeasure_image_null (by positivity) this)
    rintro a ‚ü®has, ha‚ü©
    rw [epr.image_symm_eq_preimage, Set.mem_preimage, Set.mem_image]
    refine ‚ü®epq a, ‚ü®epq.mapsTo ha, ?_‚ü©, ?_‚ü©
    ¬∑ simp [*]
    ¬∑ rw [‚Üê heqOn (epq.mapsTo ha)]
      simp [ha]
  ¬∑ intro a ha
    simp only [‚Üê ContinuousLinearMap.finrank.eq_1] at *
    rw [‚Üê (heqOn.eventuallyEq_of_mem <| epq.open_target.mem_nhds ha.1).fderiv_eq, epr.comp_fderiv,
      epr.finrank_comp_left, fderiv_comp, ContinuousLinearMap.finrank_comp_eq_left_of_surjective,
      hs x hx, hs _ ha.2]
    ¬∑ apply Function.LeftInverse.surjective (g := fderiv ‚Ñù epq (epq.symm a))
      rw [Function.leftInverse_iff_comp, ‚Üê ContinuousLinearMap.coe_comp',
        ‚Üê ContinuousLinearMap.coe_id' (R‚ÇÅ := ‚Ñù), DFunLike.coe_fn_eq]
      have : fderiv ‚Ñù (epq.symm ‚àò epq) (epq.symm a) = .id ‚Ñù E := by
        rw [(epq.leftInvOn.eqOn.eventuallyEq_of_mem _).fderiv_eq, fderiv_id]
        exact epq.open_source.mem_nhds <| epq.symm_mapsTo ha.1
      rwa [fderiv_comp, epq.rightInvOn ha.1] at this
      ¬∑ rw [epq.rightInvOn ha.1]
        exact hepq_symm.contDiffMoreiraHolderAt ha |>.differentiableAt hk
      ¬∑ exact hepq.contDiffMoreiraHolderAt ‚ü®epq.symm_mapsTo ha.1, ha.2‚ü© |>.differentiableAt hk
    ¬∑ exact hf.contDiffMoreiraHolderAt ha.2 |>.differentiableAt hk
    ¬∑ exact hepq_symm.contDiffMoreiraHolderAt ha |>.differentiableAt hk

theorem hausdorffMeasure_image_null_of_finrank_eq [MeasurableSpace F] [BorelSpace F]
    (hp_dom : p < dim E)
    (hp_cod : p < dim F) (hk : k ‚â† 0) {f : E ‚Üí F} {s U : Set E}
    (hf : ContDiffMoreiraHolderOn k Œ± f s U)
    (hs : ‚àÄ x ‚àà s, dim (LinearMap.range <| fderiv ‚Ñù f x) = p) :
    ŒºH[sardMoreiraBound (dim E) k Œ± p] (f '' s) = 0 := by
  have : FiniteDimensional ‚Ñù E := .of_finrank_pos (by grind)
  rw [‚Üê coe_toOuterMeasure, ‚Üê OuterMeasure.comap_apply]
  refine measure_null_of_locally_null _ fun x hx ‚Ü¶ ?_
  apply hausdorffMeasure_image_nhdsWithin_null_of_finrank_eq <;> assumption

end Moreira2001

theorem hausdorffMeasure_sardMoreiraBound_image_null_of_finrank_le
    [MeasurableSpace F] [BorelSpace F]
    (hp_dom : p < dim E)
    (hp_cod : p < dim F) (hk : k ‚â† 0) {f : E ‚Üí F} {s U : Set E}
    (hf : ContDiffMoreiraHolderOn k Œ± f s U)
    (hs : ‚àÄ x ‚àà s, dim (LinearMap.range <| fderiv ‚Ñù f x) ‚â§ p) :
    ŒºH[sardMoreiraBound (dim E) k Œ± p] (f '' s) = 0 := by
  -- Apply the Moreira2001 theorem to each of the sets where the rank is exactly `p' ‚â§ p`.
  have h_apply : ‚àÄ p' ‚â§ p,
      ŒºH[sardMoreiraBound (dim E) k Œ± p']
        (f '' {x ‚àà s | dim (LinearMap.range (fderiv ‚Ñù f x)) = p'}) = 0 := by
    intro p' hp'
    apply Moreira2001.hausdorffMeasure_image_null_of_finrank_eq
      (by grind) (by grind) hk (U := U)
    ¬∑ exact hf.subset_left Set.inter_subset_left
    ¬∑ simp
  -- Since $s$ is the union of the sets where the rank is exactly $p'$ for $p' \leq p$,
  -- we can use the countable subadditivity of the Hausdorff measure.
  have h_union :
      f '' s = ‚ãÉ p' ‚â§ p, f '' {x ‚àà s | dim (LinearMap.range (fderiv ‚Ñù f x)) = p'} := by
    ext y
    simp only [Set.mem_image, Set.mem_iUnion, Set.mem_setOf_eq, exists_prop]
    exact ‚ü®fun ‚ü®x, hx, hx'‚ü© ‚Ü¶ ‚ü®_, hs x hx, x, ‚ü®hx, rfl‚ü©, hx'‚ü©,
      fun ‚ü®i, hi, x, hx, hx'‚ü© ‚Ü¶ ‚ü®x, hx.1, hx'‚ü©‚ü©
  simp only [h_union, measure_iUnion_null_iff]
  intro p' hp'
  rw [‚Üê nonpos_iff_eq_zero, ‚Üê h_apply p' hp']
  apply hausdorffMeasure_mono
  exact monotone_sardMoreiraBound _ hk _ hp'

theorem dimH_image_le_sardMoreiraBound_of_finrank_le
    (hp_dom : p < dim E)
    (hp_cod : p < dim F) (hk : k ‚â† 0) {f : E ‚Üí F} {s U : Set E}
    (hf : ContDiffMoreiraHolderOn k Œ± f s U)
    (hs : ‚àÄ x ‚àà s, dim (LinearMap.range <| fderiv ‚Ñù f x) ‚â§ p) :
    dimH (f '' s) ‚â§ sardMoreiraBound (dim E) k Œ± p := by
  borelize F
  apply dimH_le_of_hausdorffMeasure_ne_top
  simp [hausdorffMeasure_sardMoreiraBound_image_null_of_finrank_le hp_dom hp_cod hk hf hs]
