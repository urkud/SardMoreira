import Mathlib.MeasureTheory.Covering.Besicovitch
import Mathlib
import SardMoreira.UnifDoublingCover

namespace MeasureTheory.Measure

open scoped ENNReal NNReal Topology
open Metric Set Filter Fin MeasureTheory TopologicalSpace Besicovitch.TauPackage

attribute [norm_cast] ENNReal.ofReal_coe_nnreal

theorem EMetric.diam_metricClosedBall_le {X : Type*} [PseudoMetricSpace X]
    (x : X) (r : â„) : EMetric.diam (Metric.closedBall x r) â‰¤ 2 * ENNReal.ofReal r := by
  rcases lt_or_ge r 0 with hr | hr
  Â· simp [ENNReal.ofReal_of_nonpos hr.le, Metric.closedBall_of_neg hr]
  lift r to â„â‰¥0 using hr
  grw [â† Metric.emetric_closedBall_nnreal, EMetric.diam_closedBall, ENNReal.ofReal_coe_nnreal]

universe u

variable {Î± : Type*} [MetricSpace Î±] {Î² : Type u} [MeasurableSpace Î±]

class ClosedBallCoveringMeasure {Î± : Type*} [MetricSpace Î±] [MeasurableSpace Î±]
    (Î¼ : Measure Î±) : Prop where
  exists_closedBall_covering_tsum_measure_le {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) (f : Î± â†’ Set â„) (s : Set Î±)
    (hf : âˆ€ x âˆˆ s, âˆ€ Î´ > 0, (f x âˆ© Ioo 0 Î´).Nonempty) :
    âˆƒ (t : Set Î±) (r : Î± â†’ â„), t.Countable âˆ§ t âŠ† s âˆ§ (âˆ€ x âˆˆ t, r x âˆˆ f x) âˆ§
      (s âŠ† â‹ƒ x âˆˆ t, closedBall x (r x)) âˆ§ (âˆ‘' x : t, Î¼ (closedBall x (r x))) â‰¤ Î¼ s + Îµ

theorem exists_closedBall_covering_tsum_measure_le (Î¼ : Measure Î±) [ClosedBallCoveringMeasure Î¼]
    {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) (f : Î± â†’ Set â„) (s : Set Î±)
    (hf : âˆ€ x âˆˆ s, âˆ€ Î´ > 0, (f x âˆ© Ioo 0 Î´).Nonempty) :
    âˆƒ (t : Set Î±) (r : Î± â†’ â„), t.Countable âˆ§ t âŠ† s âˆ§ (âˆ€ x âˆˆ t, r x âˆˆ f x) âˆ§
      (s âŠ† â‹ƒ x âˆˆ t, closedBall x (r x)) âˆ§ (âˆ‘' x : t, Î¼ (closedBall x (r x))) â‰¤ Î¼ s + Îµ :=
  ClosedBallCoveringMeasure.exists_closedBall_covering_tsum_measure_le hÎµ f s hf

instance [SecondCountableTopology Î±] [OpensMeasurableSpace Î±] [HasBesicovitchCovering Î±]
    (Î¼ : Measure Î±) [SFinite Î¼] [Î¼.OuterRegular] : ClosedBallCoveringMeasure Î¼ :=
  âŸ¨Besicovitch.exists_closedBall_covering_tsum_measure_le Î¼âŸ©

open IsUnifLocDoublingMeasure in
instance instClosedBallCoveringMeasureOfIsUnifLocDoublingMeasure [BorelSpace Î±] [SecondCountableTopology Î±]
    (Î¼ : Measure Î±) [IsLocallyFiniteMeasure Î¼] [IsUnifLocDoublingMeasure Î¼] :
    ClosedBallCoveringMeasure Î¼ where
  exists_closedBall_covering_tsum_measure_le := by
    intro Îµ hÎµâ‚€ f s hf
    -- TODO: we do all the same steps for the Besicovitch instance in Mathlib.
    -- Factor out common parts into a constructor.
    rcases s.exists_isOpen_le_add Î¼ (ENNReal.half_pos hÎµâ‚€).ne' with âŸ¨U, hUs, hUo, hÎ¼UâŸ©
    set t : Set (Î± Ã— â„) :=
      {(c, r) : Î± Ã— â„ | c âˆˆ s âˆ§ r âˆˆ f c âˆ© Ioo 0 (scalingScaleOf Î¼ 3) âˆ§ closedBall c r âŠ† U}
    obtain âŸ¨u, hus, huc, hud, hÎ¼âŸ© : âˆƒ u âŠ† t, u.Countable âˆ§
        u.PairwiseDisjoint (fun a â†¦ closedBall a.1 a.2) âˆ§
        Î¼ (s \ â‹ƒ a âˆˆ u, closedBall a.1 a.2) = 0 := by
      apply Vitali.exists_disjoint_covering_ae Î¼ s t (scalingConstantOf Î¼ 3) Prod.snd Prod.fst
      Â· exact fun _ _ â†¦ Subset.rfl
      Â· exact fun x hx â†¦ measure_mul_le_scalingConstantOf_mul _ (by simp) hx.2.1.2.2.le
      Â· intro x hx
        grw [â† ball_subset_interior_closedBall, nonempty_ball]
        exact hx.2.1.2.1
      Â· exact fun _ _ â†¦ isClosed_closedBall
      Â· intro x hx Îµ hÎµ
        rcases Metric.nhds_basis_closedBall.mem_iff.mp (hUo.mem_nhds (hUs hx)) with âŸ¨Î´, hÎ´â‚€, hÎ´UâŸ©
        rcases hf x hx (Îµ âŠ“ Î´ âŠ“ scalingScaleOf Î¼ 3)
          (lt_min (lt_min hÎµ hÎ´â‚€) (scalingScaleOf_pos _ _)) with âŸ¨r, hrf, hrâ‚€, hrÎµâŸ©
        rw [lt_min_iff, lt_min_iff] at hrÎµ
        refine âŸ¨(x, r), âŸ¨hx, âŸ¨hrf, hrâ‚€, hrÎµ.2âŸ©, ?_âŸ©, hrÎµ.1.1.le, rflâŸ©
        grw [hrÎµ.1.2, hÎ´U]
    rcases exists_closedBall_covering_tsum_measure_le_of_measure_zero Î¼ (ENNReal.half_pos hÎµâ‚€).ne'
      f _ hÎ¼ (fun x hx Î´ hÎ´ â†¦ hf x hx.1 Î´ hÎ´) with âŸ¨v, r', hvc, hv_sub, hrf, hs_sub, hv_tsumâŸ©
    set goodR : Î± â†’ â„ := Function.extend (fun x : u â†¦ x.1.1) (fun cr â†¦ cr.1.2) r'
    have hinj : u.InjOn Prod.fst := by
      rintro âŸ¨x, Î´â‚âŸ© hâ‚ âŸ¨xâ‚‚, Î´â‚‚âŸ© hâ‚‚ (rfl : x = xâ‚‚)
      apply (hud.eq_or_disjoint hâ‚ hâ‚‚).resolve_right ?_
      rw [Set.not_disjoint_iff]
      use x
      simp [(hus hâ‚).2.1.2.1.le, (hus hâ‚‚).2.1.2.1.le]
    have hgoodR_fst_u : âˆ€ x âˆˆ u, goodR x.1 = x.2 := by
      intro x hx
      lift x to u using hx
      simp only [goodR]
      apply hinj.injective.extend_apply
    have hgoodR_not_u : âˆ€ x âˆ‰ Prod.fst '' u, goodR x = r' x := by
      intro x hx
      apply Function.extend_apply'
      simpa using hx
    refine âŸ¨Prod.fst '' u âˆª v, goodR, ?_, ?_, ?_, ?_, ?_âŸ©
    Â· exact huc.image _ |>.union hvc
    Â· rintro x (âŸ¨y, hy, rflâŸ© | hx)
      Â· exact (hus hy).1
      Â· exact (hv_sub hx).1
    Â· intro x hx
      rcases em (x âˆˆ Prod.fst '' u) with âŸ¨y, hy, rflâŸ© | hx'
      Â· rw [hgoodR_fst_u y hy]
        exact (hus hy).2.1.1
      Â· rw [hgoodR_not_u x hx']
        exact hrf _ (hx.resolve_left hx')
    Â· intro x hxs
      simp only [mem_iUnion, exists_prop, mem_union, mem_image]
      by_cases hx : x âˆˆ â‹ƒ a âˆˆ u, closedBall a.1 a.2
      Â· rw [mem_iUnionâ‚‚] at hx
        rcases hx with âŸ¨y, hyu, hyâŸ©
        refine âŸ¨y.1, .inl âŸ¨y, hyu, rflâŸ©, ?_âŸ©
        rwa [hgoodR_fst_u y hyu]
      Â· have := hs_sub âŸ¨hxs, hxâŸ©
        rcases mem_iUnionâ‚‚.mp this with âŸ¨c, hc, hcxâŸ©
        refine âŸ¨c, .inr hc, ?_âŸ©
        rwa [hgoodR_not_u]
        rintro âŸ¨y, hy, rflâŸ©
        refine (hv_sub hc).2 <| mem_iUnionâ‚‚_of_mem hy ?_
        simp [(hus hy).2.1.2.1.le]
    Â· rw [tsum_congr_set_coe (fun x â†¦ Î¼ (closedBall x (goodR x))) Set.union_diff_self.symm]
      grw [ENNReal.tsum_union_le (fun x â†¦ Î¼ (closedBall x (goodR x)))]
      rw [tsum_image (fun x â†¦ Î¼ (closedBall x (goodR x))) hinj]
      simp only [hgoodR_fst_u _ (Subtype.prop _),
        fun x : â†‘(v \ Prod.fst '' u) â†¦ hgoodR_not_u x x.2.2]
      grw [â† measure_biUnion huc hud (fun _ _ â†¦ measurableSet_closedBall),
        ENNReal.tsum_mono_subtype (fun x â†¦ Î¼ (closedBall x (r' x))) diff_subset, â† Îµ.add_halves,
        â† add_assoc, hv_tsum, â† hÎ¼U]
      gcongr
      refine iUnionâ‚‚_subset fun x hx â†¦ ?_
      exact (hus hx).2.2

lemma outerMeasure_le_mul' {Î¼ : Measure Î±} [ClosedBallCoveringMeasure Î¼]
    {Î½ : OuterMeasure Î±} {C : â„â‰¥0âˆ} {s : Set Î±} (hsC : Î¼ s â‰  0 âˆ¨ C â‰  âˆ) (hCs : C â‰  0 âˆ¨ Î¼ s â‰  âˆ)
    (h : âˆ€ x âˆˆ s, âˆƒá¶  Îµr : â„â‰¥0âˆ Ã— â„ in ğ“[>] 0 Ã—Ë¢ ğ“[>] 0,
      Î½ (s âˆ© closedBall x Îµr.2) â‰¤ (C + Îµr.1) * Î¼ (closedBall x Îµr.2)) :
    Î½ s â‰¤ C * Î¼ s := by
  -- Thus it suffices to prove `Î½ s â‰¤ C' * (Î¼ s + Îµ)` for all `C' > C` and `Îµ > 0`
  suffices âˆ€ Îµ > 0, Î½ s â‰¤ (C + Îµ) * (Î¼ s + Îµ) by
    have H : Tendsto (fun Îµ â†¦ (C + Îµ) * (Î¼ s + Îµ)) (ğ“ 0) (ğ“ (C * Î¼ s)) := by
      apply ENNReal.Tendsto.mul <;>
        first | assumption | simpa using tendsto_const_nhds.add (tendsto_id (x := ğ“ (0 : â„â‰¥0âˆ)))
    exact ge_of_tendsto_of_frequently H <| (frequently_gt_nhds _).mono this
  -- Now we use `exists_closedBall_covering_tsum_measure_le`,
  -- to obtain a covering by countably many balls such that
  -- `Î½ (s âˆ© closedBall x (r x)) â‰¤ (C + Îµ) * Î¼ (closedBall x (r x))` for each ball
  -- and `âˆ‘' x : t, Î¼ (closedBall x.1 (r x)) â‰¤ Î¼ s + Îµ`
  intro Îµ hÎµ
  obtain âŸ¨t, r, htc, -, hÎ½Î¼, hsr, hÎ¼âŸ© : âˆƒ (t : Set Î±) (r : Î± â†’ â„), Set.Countable t âˆ§ t âŠ† s âˆ§
      (âˆ€ x âˆˆ t, Î½ (s âˆ© closedBall x (r x)) â‰¤ (C + Îµ) * Î¼ (closedBall x (r x))) âˆ§
      s âŠ† â‹ƒ x âˆˆ t, closedBall x (r x) âˆ§ âˆ‘' x : t, Î¼ (closedBall x.1 (r x)) â‰¤ Î¼ s + Îµ := by
    refine exists_closedBall_covering_tsum_measure_le Î¼ hÎµ.ne'
      (fun x â†¦ {r | Î½ (s âˆ© closedBall x r) â‰¤ (C + Îµ) * Î¼ (closedBall x r)}) s ?_
    intro x hx Î´ hÎ´
    simp only [((nhdsGT_basis_of_exists_gt âŸ¨(1 : â„â‰¥0âˆ), one_posâŸ©).prod
      (nhdsGT_basis (0 : â„))).frequently_iff] at h
    rcases h x hx (Îµ, Î´) âŸ¨hÎµ, hÎ´âŸ© with âŸ¨âŸ¨Îµ', râŸ©, âŸ¨âŸ¨-, hÎµ'âŸ©, hrâŸ©, hleâŸ©
    refine âŸ¨r, ?_, hrâŸ©
    simp_all only [mem_setOf_eq]
    refine hle.trans ?_
    gcongr
  have := htc.to_subtype
  calc
    Î½ s â‰¤ Î½ (â‹ƒ x : t, s âˆ© closedBall x (r x)) := by
      rw [â† inter_iUnion]
      gcongr
      simpa
    _ â‰¤ âˆ‘' x : t, Î½ (s âˆ© closedBall x (r x)) := measure_iUnion_le _
    _ â‰¤ âˆ‘' x : t, (C + Îµ) * Î¼ (closedBall x (r x)) := by gcongr with i; exact hÎ½Î¼ i i.2
    _ â‰¤ (C + Îµ) * (Î¼ s + Îµ) := by rw [ENNReal.tsum_mul_left]; gcongr

lemma outerMeasure_le_mul {Î¼ : Measure Î±} [SigmaFinite Î¼] [ClosedBallCoveringMeasure Î¼]
    {Î½ : OuterMeasure Î±} {C : â„â‰¥0âˆ} {s : Set Î±} (hsC : Î¼ s â‰  0 âˆ¨ C â‰  âˆ)
    (h : âˆ€ x âˆˆ s, âˆƒá¶  Îµr : â„â‰¥0âˆ Ã— â„ in ğ“[>] 0 Ã—Ë¢ ğ“[>] 0,
      Î½ (s âˆ© closedBall x Îµr.2) â‰¤ (C + Îµr.1) * Î¼ (closedBall x Îµr.2)) :
    Î½ s â‰¤ C * Î¼ s := by
  rcases eq_or_ne C 0 with rfl | hC
  Â· calc
    Î½ s = Î½ (â‹ƒ n, s âˆ© spanningSets Î¼ n) := by simp [â† inter_iUnion]
    _ â‰¤ âˆ‘' n, Î½ (s âˆ© spanningSets Î¼ n) := measure_iUnion_le _
    _ â‰¤ âˆ‘' n, 0 * Î¼ (s âˆ© spanningSets Î¼ n) := by
      gcongr with n
      refine outerMeasure_le_mul' (by simp) (.inr ?_) fun x hx â†¦ (h x hx.1).mono ?_
      Â· exact (measure_mono inter_subset_right).trans_lt (measure_spanningSets_lt_top ..) |>.ne
      Â· exact fun _ â†¦ le_trans <| by gcongr; apply inter_subset_left
    _ = 0 * Î¼ s := by simp
  Â· exact outerMeasure_le_mul' hsC (.inl hC) h

/-- Suppose that `Î½ (s âˆ© closedBall x r) = O(Î¼ (closedBall x r))` at all points of a set `s`
and `Î½ (s âˆ© closedBall x r) = o(Î¼ (closedBall x r))` at a.e. points of the set.
Then `Î½ s = 0`.

The actual statement can't use `Asymptotics.IsBigO` and `Asymptotics.IsLittleO`,
because the LHS and the RHS are in `â„â‰¥0âˆ`, not `â„`.

Note that we do not assume measurability of `s` or `C`. -/
lemma outerMeasure_null_of_forall_le_mul_ae_null {Î¼ : Measure Î±} [SigmaFinite Î¼]
    [ClosedBallCoveringMeasure Î¼]
    {Î½ : OuterMeasure Î±} {C : Î± â†’ â„â‰¥0} {s : Set Î±} (hC : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ C x = 0)
    (h : âˆ€ x âˆˆ s, âˆƒá¶  Îµr : â„â‰¥0âˆ Ã— â„ in ğ“[>] 0 Ã—Ë¢ ğ“[>] 0,
      Î½ (s âˆ© closedBall x Îµr.2) â‰¤ (C x + Îµr.1) * Î¼ (closedBall x Îµr.2)) :
    Î½ s = 0 := by
  grw [â† nonpos_iff_eq_zero, measure_le_inter_add_diff (t := {x | C x = 0})]
  apply add_nonpos
  Â· calc
      Î½ (s âˆ© {x | C x = 0}) â‰¤ 0 * Î¼ (s âˆ© {x | C x = 0}) := by
        refine outerMeasure_le_mul (by simp) fun x hx â†¦ ?_
        grw [inter_subset_left]
        simpa [hx.2.out] using h x hx.1
      _ = 0 := zero_mul _
  Â· set t := s \ {x | C x = 0}
    have hÎ¼t : Î¼ t = 0 := by simpa [t, ae_iff] using hC
    calc
      Î½ t = Î½ (â‹ƒ n : â„•, {x âˆˆ t | C x â‰¤ n}) := by
        congr with x
        simp [exists_nat_ge]
      _ â‰¤ âˆ‘' n : â„•, Î½ {x âˆˆ t | C x â‰¤ n} := measure_iUnion_le _
      _ â‰¤ âˆ‘' n : â„•, n * Î¼ {x âˆˆ t | C x â‰¤ n} := by
        gcongr with n
        apply outerMeasure_le_mul
        Â· simp
        Â· intro x hx
          simp only [t]
          grw [sep_subset, diff_subset, â† (mod_cast hx.2 : (C x : â„â‰¥0âˆ) â‰¤ n)]
          exact h x hx.1.1
      _ â‰¤ âˆ‘' n : â„•, n * Î¼ t := by gcongr; apply Set.sep_subset
      _ â‰¤ 0 := by simp [hÎ¼t]

lemma outerMeasure_null_of_null_of_forall_exists_le_mul {Î¼ : Measure Î±} [SigmaFinite Î¼]
    [ClosedBallCoveringMeasure Î¼] {Î½ : OuterMeasure Î±} {s : Set Î±} (hs : Î¼ s = 0)
    (h : âˆ€ x âˆˆ s, âˆƒ C : â„â‰¥0, âˆƒá¶  r : â„ in ğ“[>] 0, Î½ (s âˆ© closedBall x r) â‰¤ C * Î¼ (closedBall x r)) :
    Î½ s = 0 := by
  -- TODO: move 2nd part of the proof here instead of calling the result
  choose! C hC using h
  apply outerMeasure_null_of_forall_le_mul_ae_null (Î¼ := Î¼) (C := C)
  Â· exact measure_mono_null (by simp +contextual [subset_def]) hs
  Â· intro x hx
    refine .filter_mono ?_ curry_le_prod
    rw [frequently_curry_iff]
    refine .of_forall fun Îµ â†¦ (hC x hx).mono fun r hr â†¦ ?_
    grw [hr, â† le_self_add]

/-- Let `f : Î± â†’ Î²` be a map from a space with Besicovitch property to any space.
Let `Î¼` be a Ïƒ-finite outer regular measure on `Î±`, let `Î½` be an outer measure on `Î²`,
let `s` be a set in the domain, let `C` be a constant such that `Î¼ s â‰  0` or `C â‰  âˆ`.
Suppose that for each `x âˆˆ s` and a positive `Îµ`,
for a set of positive `r` that accumulates to zero,
we have `Î½ (f '' (s âˆ© Metric.closedBall x r)) â‰¤ (C + Îµ) * Î¼ (Metric.closedBall x r)`.
Then `Î½ (f '' s) â‰¤ C * Î¼ s`.

Briefly speaking, this means that `Î½ (f '' s) â‰¤ C * Î¼ s`
provided that a similar estimate holds for sufficiently small ball around each point `x âˆˆ s`.

See also `Besicovitch.measure_image_le_mul`.
-/
lemma outerMeasure_image_le_mul {f : Î± â†’ Î²} {Î¼ : Measure Î±} [SigmaFinite Î¼]
    [ClosedBallCoveringMeasure Î¼]
    {Î½ : OuterMeasure Î²} {C : â„â‰¥0âˆ} {s : Set Î±} (hsC : Î¼ s â‰  0 âˆ¨ C â‰  âˆ)
    (h : âˆ€ x âˆˆ s, âˆƒá¶  Îµr : â„â‰¥0âˆ Ã— â„ in ğ“[>] 0 Ã—Ë¢ ğ“[>] 0,
      Î½ (f '' (s âˆ© closedBall x Îµr.2)) â‰¤ (C + Îµr.1) * Î¼ (closedBall x Îµr.2)) :
    Î½ (f '' s) â‰¤ C * Î¼ s := by
  simp only [â† OuterMeasure.comap_apply] at *
  exact outerMeasure_le_mul hsC h

/-- Let `f : Î± â†’ Î²` be a map from a space with Besicovitch property to any space.
Let `Î¼` be a Ïƒ-finite outer regular measure on `Î±`, let `Î½` be a measure on `Î²`,
let `s` be a set in the domain, let `C` be a constant such that `Î¼ s â‰  0` or `C â‰  âˆ`.
Suppose that for each `x âˆˆ s` and a positive `Îµ`,
for a set of positive `r` that accumulates to zero,
we have `Î½ (f '' (s âˆ© Metric.closedBall x r)) â‰¤ (C + Îµ) * Î¼ (Metric.closedBall x r)`.
Then `Î½ (f '' s) â‰¤ C * Î¼ s`.

Briefly speaking, this means that `Î½ (f '' s) â‰¤ C * Î¼ s`
provided that a similar estimate holds for sufficiently small ball around each point `x âˆˆ s`.

See also `Besicovitch.outerMeasure_image_le_mul`.
-/
lemma measure_image_le_mul {_ : MeasurableSpace Î²} {f : Î± â†’ Î²} {Î¼ : Measure Î±} [SigmaFinite Î¼]
    [ClosedBallCoveringMeasure Î¼] {Î½ : Measure Î²} {C : â„â‰¥0âˆ} {s : Set Î±} (hsC : Î¼ s â‰  0 âˆ¨ C â‰  âˆ)
    (h : âˆ€ x âˆˆ s, âˆƒá¶  Îµr : â„â‰¥0âˆ Ã— â„ in ğ“[>] 0 Ã—Ë¢ ğ“[>] 0,
      Î½ (f '' (s âˆ© closedBall x Îµr.2)) â‰¤ (C + Îµr.1) * Î¼ (closedBall x Îµr.2)) :
    Î½ (f '' s) â‰¤ C * Î¼ s :=
  outerMeasure_image_le_mul hsC h

-- TODO: some of the nonnegativity/positivity assumptions can be omitted,
-- because in the other case, the goal is trivial.
lemma hasudorffMeasure_image_le_mul' {X : Type*} [MetricSpace X] [MeasurableSpace X] [BorelSpace X]
    {f : Î± â†’ X} {Î¼ : Measure Î±} [ClosedBallCoveringMeasure Î¼] {C : â„â‰¥0âˆ}
    {s : Set Î±} (hsC : Î¼ s â‰  0 âˆ¨ C â‰  âˆ) {dimDom holderExp dimImg : â„} {Î¼Ball : â„â‰¥0âˆ}
    (holderExp_pos : 0 < holderExp)
    (hÎ¼_dim : âˆ€ x (r : â„â‰¥0), Î¼ (closedBall x r) = r ^ dimDom * Î¼Ball)
    (hÎ¼ballâ‚€ : Î¼Ball â‰  0) (hÎ¼ball : Î¼Ball â‰  âˆ)
    (hdim : dimDom â‰¤ holderExp * dimImg)
    (hdimDom : 0 < dimDom)
    (h : âˆ€ x âˆˆ s, âˆ€ Îµ > 0,
      âˆ€á¶  y in ğ“[s] x, edist (f y) (f x) â‰¤ (C + Îµ) * edist y x ^ holderExp) :
    Î¼H[dimImg] (f '' s) â‰¤ ((2 * C) ^ dimImg / Î¼Ball) * Î¼ s := by
  rcases s.eq_empty_or_nonempty with rfl | hsne
  Â· simp
  have hdimImg_pos : 0 < dimImg := by
    grw [â† mul_pos_iff_of_pos_left holderExp_pos, â† hdim]
    exact hdimDom
  suffices âˆ€ d, 0 < d â†’
      OuterMeasure.mkMetric'.pre
        (fun s â†¦ EMetric.diam s ^ dimImg) d (f '' s) â‰¤ ((2 * C) ^ dimImg / Î¼Ball) * Î¼ s by
    simpa only [hausdorffMeasure, â† toOuterMeasure_apply, mkMetric_toOuterMeasure,
      OuterMeasure.mkMetric, OuterMeasure.mkMetric', OuterMeasure.iSup_apply, iSup_le_iff]
  intro d hd
  cases C with
  | top =>
    convert le_top
    simp [ENNReal.top_rpow_of_pos, hdimImg_pos, ENNReal.mul_eq_top, hÎ¼ball, ENNReal.div_eq_top,
      hsC.neg_resolve_right rfl]
    infer_instance
  | coe C =>
    clear hsC
    have : SigmaFinite Î¼ := by
      refine âŸ¨âŸ¨âŸ¨(closedBall hsne.choose Â·), fun _ â†¦ mem_univ _, fun i â†¦ ?_,
        iUnion_closedBall_nat _âŸ©âŸ©âŸ©
      simp only [hÎ¼_dim, â† NNReal.coe_natCast]
      refine ENNReal.mul_lt_top ((ENNReal.rpow_lt_top_iff_of_pos ?_).mpr ?_) hÎ¼ball.lt_top
      Â· assumption
      Â· simp
    apply outerMeasure_image_le_mul
    Â· simp [ENNReal.div_eq_top, hdimImg_pos, hdimImg_pos.not_gt, hÎ¼ballâ‚€, hÎ¼ball,
        ENNReal.mul_eq_top]
    intro x hx
    specialize h x hx
    refine .filter_mono ?_ curry_le_prod
    rw [frequently_curry_iff]
    refine Eventually.frequently <| eventually_mem_nhdsWithin.mono fun Îµ (hÎµ : 0 < Îµ) â†¦ ?_
    obtain âŸ¨Îµ', hÎµ'â‚€, hÎµ'âŸ© : âˆƒ Îµ' > (0 : â„â‰¥0),
        (2 * (C + Îµ') : â„â‰¥0âˆ) ^ dimImg â‰¤ (((2 * C) ^ dimImg / Î¼Ball) + Îµ) * Î¼Ball := by
      apply Eventually.exists_gt
      refine (ContinuousAt.tendsto ?_).eventually_le_const ?_
      Â· refine ENNReal.continuous_rpow_const.continuousAt.comp ?_
        refine ENNReal.continuousAt_const_mul (by simp) |>.comp ?_
        fun_prop
      Â· rw [â† ENNReal.div_lt_iff]
        Â· simp
          apply ENNReal.lt_add_right
          Â· finiteness
          Â· positivity
        Â· exact .inl hÎ¼ballâ‚€
        Â· exact .inl hÎ¼ball
    rw [(nhdsGT_basis _).frequently_iff]
    intro r' hrâ‚€'
    obtain âŸ¨Î´, hÎ´â‚€, hÎ´â‚, hÎ´r, hÎ´CÎµ, hÎ´âŸ© : âˆƒ Î´ > (0 : â„â‰¥0), Î´ â‰¤ 1 âˆ§ Î´ < r' âˆ§
        (âˆ€ y âˆˆ s âˆ© closedBall x Î´, dist (f y) (f x) â‰¤ (C + Îµ') * dist y x ^ holderExp) âˆ§
        2 * (C + Îµ') * Î´ ^ holderExp â‰¤ d := by
      apply Eventually.exists_gt
      apply_rules [Eventually.and]
      Â· exact eventually_le_nhds one_pos
      Â· exact (ContinuousAt.tendsto <| by fun_prop).eventually_lt_const (by simpa)
      Â· specialize h Îµ' (mod_cast hÎµ'â‚€)
        rw [Metric.nhdsWithin_basis_ball.eventually_iff] at h
        rcases h with âŸ¨Î´, hÎ´â‚€, hÎ´âŸ©
        lift Î´ to â„â‰¥0 using hÎ´â‚€.le
        filter_upwards [eventually_lt_nhds (b := Î´) (mod_cast hÎ´â‚€)] with Î´' hÎ´' y hy
        specialize hÎ´ âŸ¨closedBall_subset_ball (mod_cast hÎ´') hy.2, hy.1âŸ©
        simp (disch := positivity)
          only [dist_nndist, edist_nndist, â† ENNReal.coe_rpow_of_nonneg] at hÎ´ âŠ¢
        exact mod_cast hÎ´
      Â· refine (ContinuousAt.tendsto ?_).eventually_le_const ?_
        Â· refine ENNReal.continuousAt_const_mul (by left; finiteness) |>.comp ?_
          fun_prop
        Â· simp (disch := positivity) [ENNReal.zero_rpow_of_pos, hd]
    refine âŸ¨Î´, âŸ¨hÎ´â‚€, hÎ´râŸ©, ?_âŸ©
    have hmaps : MapsTo f (s âˆ© closedBall x Î´) (closedBall (f x) ((C + Îµ') * Î´ ^ holderExp)) := by
      intro y hy
      grw [mem_closedBall, hÎ´CÎµ y hy, mem_closedBall.mp hy.2]
    have hdiam : EMetric.diam (f '' (s âˆ© closedBall x Î´)) â‰¤ 2 * (C + Îµ') * Î´ ^ holderExp := by
      grw [hmaps.image_subset, EMetric.diam_metricClosedBall_le,
        â† ENNReal.coe_rpow_of_nonneg _ (by positivity)]
      norm_cast
      rw [â† mul_assoc]
    grw [OuterMeasure.mkMetric'.pre_le, hdiam, hÎ¼_dim, ENNReal.mul_rpow_of_nonneg,
      â† ENNReal.rpow_mul, â† mul_assoc, mul_right_comm]
    gcongr 1
    Â· apply ENNReal.rpow_le_rpow_of_exponent_ge
      Â· exact mod_cast hÎ´â‚
      Â· exact hdim
    Â· positivity
    Â· grw [hdiam, hÎ´]

lemma hasudorffMeasure_image_le_mul {X : Type*} [MetricSpace X] [MeasurableSpace X] [BorelSpace X]
    [OpensMeasurableSpace Î±] [T1Space Î±]
    {f : Î± â†’ X} {Î¼ : Measure Î±} [ClosedBallCoveringMeasure Î¼] {C : â„â‰¥0âˆ}
    {s : Set Î±} (hsC : Î¼ s â‰  0 âˆ¨ C â‰  âˆ) {dimDom holderExp dimImg : â„} {Î¼Ball : â„â‰¥0âˆ}
    (holderExp_pos : 0 < holderExp)
    (hÎ¼_dim : âˆ€ x (r : â„â‰¥0), Î¼ (closedBall x r) = r ^ dimDom * Î¼Ball)
    (hÎ¼ballâ‚€ : Î¼Ball â‰  0) (hÎ¼ball : Î¼Ball â‰  âˆ)
    (hdim : dimDom â‰¤ holderExp * dimImg)
    (hdimDom : 0 â‰¤ dimDom)
    (h : âˆ€ x âˆˆ s, âˆ€ Îµ > 0,
      âˆ€á¶  y in ğ“[s] x, edist (f y) (f x) â‰¤ (C + Îµ) * edist y x ^ holderExp) :
    Î¼H[dimImg] (f '' s) â‰¤ ((2 * C) ^ dimImg / Î¼Ball) * Î¼ s := by
  rcases s.eq_empty_or_nonempty with rfl | hsne
  Â· simp
  rcases hdimDom.eq_or_lt with rfl | hdimDom
  Â· rw [mul_nonneg_iff_of_pos_left holderExp_pos] at hdim
    rcases hsne.exists_eq_singleton_or_nontrivial with âŸ¨a, rflâŸ© | âŸ¨a, ha, b, hb, hneâŸ©
    Â· rw [image_singleton, â† closedBall_zero (x := a), â† NNReal.coe_zero, hÎ¼_dim]
      rcases hdim.eq_or_lt with rfl | hdimImg_pos
      Â· simp [ENNReal.inv_mul_cancel, *]
      Â· have := MeasureTheory.Measure.noAtoms_hausdorff X hdimImg_pos
        simp
    Â· have := calc
        Î¼Ball + Î¼Ball = Î¼ {a} + Î¼ {b} := by
          simp only [â† closedBall_zero, â† NNReal.coe_zero, hÎ¼_dim]
          simp
        _ = Î¼ {a, b} := by
          rw [â† singleton_union, measure_union]
          Â· simpa
          Â· exact measurableSet_singleton b
        _ â‰¤ Î¼ (closedBall a (nndist a b)) := by
          gcongr
          simp [insert_subset_iff, dist_comm]
        _ = Î¼Ball := by rw [hÎ¼_dim]; simp
      refine absurd ?_ this.not_gt
      exact ENNReal.lt_add_right hÎ¼ball hÎ¼ballâ‚€
  Â· exact hasudorffMeasure_image_le_mul' hsC holderExp_pos hÎ¼_dim hÎ¼ballâ‚€ hÎ¼ball hdim hdimDom h
