import Mathlib
import SardMoreira.ImplicitFunction

noncomputable section

open scoped unitInterval Topology NNReal
open Asymptotics Filter Set Metric Function

local notation "dim" => Module.finrank â„

@[simps! -fullyApplied apply_coe symm_apply_coe_coe]
def Submodule.continuousEquivSubtypeMap {R M : Type*} [Semiring R] [AddCommMonoid M]
    [Module R M] [TopologicalSpace M] (p : Submodule R M) (q : Submodule R p) :
    q â‰ƒL[R] q.map p.subtype where
  toLinearEquiv := p.equivSubtypeMap q
  continuous_toFun := .codRestrict (continuous_subtype_val.comp continuous_subtype_val) _
  continuous_invFun := .codRestrict (.codRestrict continuous_subtype_val _) _

@[simps!]
def Submodule.topContinuousEquiv {R M : Type*} [Semiring R] [AddCommMonoid M]
    [Module R M] [TopologicalSpace M] :
    (âŠ¤ : Submodule R M) â‰ƒL[R] M where
  toLinearEquiv := topEquiv
  continuous_toFun := by fun_prop
  continuous_invFun := by fun_prop

theorem ContinuousLinearEquiv.map_nhdsWithin_eq {R M N : Type*} [Semiring R]
    [AddCommMonoid M] [Module R M] [TopologicalSpace M]
    [AddCommMonoid N] [Module R N] [TopologicalSpace N]
    (e : M â‰ƒL[R] N) (s : Set M) (x : M) :
    (ğ“[s] x).map e = ğ“[e '' s] (e x) :=
  e.toHomeomorph.isInducing.map_nhdsWithin_eq _ _

theorem ContinuousLinearEquiv.map_nhdsWithin_preimage_eq {R M N : Type*} [Semiring R]
    [AddCommMonoid M] [Module R M] [TopologicalSpace M]
    [AddCommMonoid N] [Module R N] [TopologicalSpace N]
    (e : M â‰ƒL[R] N) (s : Set N) (x : M) :
    (ğ“[e â»Â¹' s] x).map e = ğ“[s] (e x) := by
  rw [e.map_nhdsWithin_eq, e.surjective.image_preimage]

namespace ImplicitFunctionData

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [CompleteSpace E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [CompleteSpace F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G] [CompleteSpace G]

theorem differentiableAt_implicitFunction (Ï† : ImplicitFunctionData ğ•œ E F G) :
    DifferentiableAt ğ•œ (Ï†.implicitFunction (Ï†.leftFun Ï†.pt)) (Ï†.rightFun Ï†.pt) :=
  Ï†.hasStrictFDerivAt.to_localInverse.comp (Ï†.rightFun Ï†.pt)
    ((hasStrictFDerivAt_const _ _).prodMk (hasStrictFDerivAt_id _))
    |>.hasFDerivAt |>.differentiableAt

theorem fderiv_implicitFunction_apply_eq_iff (Ï† : ImplicitFunctionData ğ•œ E F G) {x : G} {y : E} :
    fderiv ğ•œ (Ï†.implicitFunction (Ï†.leftFun Ï†.pt)) (Ï†.rightFun Ï†.pt) x = y â†”
      Ï†.leftDeriv y = 0 âˆ§ Ï†.rightDeriv y = x := by
  unfold implicitFunction Function.curry toOpenPartialHomeomorph
  simp only [â† HasStrictFDerivAt.localInverse_def]
  rw [Ï†.hasStrictFDerivAt.to_localInverse.comp (Ï†.rightFun Ï†.pt)
    ((hasStrictFDerivAt_const _ _).prodMk (hasStrictFDerivAt_id _)) |>.hasFDerivAt |>.fderiv]
  simp [ContinuousLinearEquiv.symm_apply_eq, @eq_comm _ (Ï†.leftDeriv _),
    @eq_comm _ (Ï†.rightDeriv _)]

@[simp]
theorem leftDeriv_fderiv_implicitFunction (Ï† : ImplicitFunctionData ğ•œ E F G) (x : G) :
    Ï†.leftDeriv (fderiv ğ•œ (Ï†.implicitFunction (Ï†.leftFun Ï†.pt)) (Ï†.rightFun Ï†.pt) x) = 0 := by
  exact Ï†.fderiv_implicitFunction_apply_eq_iff.mp rfl |>.left

@[simp]
theorem rightDeriv_fderiv_implicitFunction (Ï† : ImplicitFunctionData ğ•œ E F G) (x : G) :
    Ï†.rightDeriv (fderiv ğ•œ (Ï†.implicitFunction (Ï†.leftFun Ï†.pt)) (Ï†.rightFun Ï†.pt) x) = x := by
  exact Ï†.fderiv_implicitFunction_apply_eq_iff.mp rfl |>.right

theorem map_implicitFunction_nhdsWithin_preimage (Ï† : ImplicitFunctionData ğ•œ E F G)
    (s : Set E) :
    (ğ“[Ï†.implicitFunction (Ï†.leftFun Ï†.pt) â»Â¹' s] (Ï†.rightFun Ï†.pt)).map
      (Ï†.implicitFunction (Ï†.leftFun Ï†.pt)) = ğ“[s âˆ© Ï†.leftFun â»Â¹' {Ï†.leftFun Ï†.pt}] Ï†.pt := by
  have H : Ï†.implicitFunction (Ï†.leftFun Ï†.pt) =
      Ï†.toOpenPartialHomeomorph.symm âˆ˜ (Ï†.leftFun Ï†.pt, Â·) := rfl
  rw [H, â† Filter.map_map, (isInducing_prodMkRight _).map_nhdsWithin_eq, â† singleton_prod,
    OpenPartialHomeomorph.map_nhdsWithin_eq, â† prodFun_apply, â† toOpenPartialHomeomorph_coe,
    Ï†.toOpenPartialHomeomorph.leftInvOn Ï†.pt_mem_toOpenPartialHomeomorph_source,
    OpenPartialHomeomorph.image_source_inter_eq']
  Â· conv_rhs =>
      rw [â† Ï†.toOpenPartialHomeomorph.nhdsWithin_source_inter
        Ï†.pt_mem_toOpenPartialHomeomorph_source]
    congr 1
    ext x
    suffices x âˆˆ Ï†.toOpenPartialHomeomorph.source â†’ Ï†.leftFun x = Ï†.leftFun Ï†.pt â†’
        (Ï†.toOpenPartialHomeomorph.symm (Ï†.leftFun Ï†.pt, Ï†.rightFun x) âˆˆ s â†” x âˆˆ s) by
      simpa [@and_comm (_ = _)]
    intro hxs hx_eq
    rw [â† hx_eq, â† prodFun_apply, â† toOpenPartialHomeomorph_coe,
      Ï†.toOpenPartialHomeomorph.leftInvOn hxs]
  Â· exact Ï†.toOpenPartialHomeomorph.mapsTo Ï†.pt_mem_toOpenPartialHomeomorph_source

end ImplicitFunctionData

@[simp]
theorem ContinuousLinearMap.range_eq_bot {R M N : Type*} [Semiring R]
    [AddCommMonoid M] [Module R M] [TopologicalSpace M]
    [AddCommMonoid N] [Module R N] [TopologicalSpace N]
    {f : M â†’L[R] N} :
    LinearMap.range f = âŠ¥ â†” f = 0 :=
  (f : M â†’â‚—[R] N).range_eq_bot.trans <| by norm_cast -- TODO: make `simp` solve it too

@[simp]
theorem ContinuousLinearMap.ker_prodMap {R M N M' N' : Type*} [Semiring R]
    [AddCommMonoid M] [Module R M] [TopologicalSpace M]
    [AddCommMonoid N] [Module R N] [TopologicalSpace N]
    [AddCommMonoid M'] [Module R M'] [TopologicalSpace M']
    [AddCommMonoid N'] [Module R N'] [TopologicalSpace N']
    (f : M â†’L[R] N) (g : M' â†’L[R] N') :
    LinearMap.ker (f.prodMap g) = (LinearMap.ker f).prod (LinearMap.ker g) := by
  ext âŸ¨_, _âŸ©; simp

@[simp]
theorem ContinuousLinearMap.range_prodMap {R M N M' N' : Type*} [Semiring R]
    [AddCommMonoid M] [Module R M] [TopologicalSpace M]
    [AddCommMonoid N] [Module R N] [TopologicalSpace N]
    [AddCommMonoid M'] [Module R M'] [TopologicalSpace M']
    [AddCommMonoid N'] [Module R N'] [TopologicalSpace N']
    (f : M â†’L[R] N) (g : M' â†’L[R] N') :
    LinearMap.range (f.prodMap g) = (LinearMap.range f).prod (LinearMap.range g) := by
  ext âŸ¨_, _âŸ©; simp

@[simp]
theorem ContinuousLinearMap.finrank_range_add_finrank_ker {R M N : Type*} [DivisionRing R]
    [AddCommGroup M] [Module R M] [TopologicalSpace M] [FiniteDimensional R M]
    [AddCommGroup N] [Module R N] [TopologicalSpace N]
    (f : M â†’L[R] N) :
    Module.finrank R (LinearMap.range f) + Module.finrank R (LinearMap.ker f) =
      Module.finrank R M :=
  f.toLinearMap.finrank_range_add_finrank_ker

@[simp]
theorem ContinuousLinearMap.range_id {R M : Type*} [Semiring R]
    [AddCommMonoid M] [Module R M] [TopologicalSpace M] :
    LinearMap.range (ContinuousLinearMap.id R M) = âŠ¤ := by
  ext; simp

@[simp]
theorem ContinuousLinearMap.snd_comp_inr {R M N : Type*} [Semiring R]
    [AddCommMonoid M] [Module R M] [TopologicalSpace M]
    [AddCommMonoid N] [Module R N] [TopologicalSpace N] :
    snd R M N âˆ˜L inr R M N = .id R N :=
  rfl

namespace Submodule

variable {R M N : Type*} [Semiring R] [AddCommMonoid M] [Module R M]
  [AddCommMonoid N] [Module R N]

def prodEquiv
    (s : Submodule R M) (t : Submodule R N) : s.prod t â‰ƒâ‚—[R] s Ã— t :=
  { (Equiv.Set.prod (s : Set M) (t : Set N)) with
    map_add' _ _ := rfl
    map_smul' _ _ := rfl }

@[simp]
theorem rank_prod_eq_lift [StrongRankCondition R] (s : Submodule R M) (t : Submodule R N)
    [Module.Free R s] [Module.Free R t] :
    Module.rank R (s.prod t) = (Module.rank R s).lift + (Module.rank R t).lift := by
  simp [(s.prodEquiv t).rank_eq]

@[simp]
theorem finrank_prod [StrongRankCondition R] (s : Submodule R M) (t : Submodule R N)
    [Module.Free R s] [Module.Free R t] [Module.Finite R s] [Module.Finite R t] :
    Module.finrank R (s.prod t) = Module.finrank R s + Module.finrank R t := by
  simp [(s.prodEquiv t).finrank_eq]

end Submodule

namespace Moreira2001

variable {E F G : Type*}
  [NormedAddCommGroup E] [NormedSpace â„ E] [FiniteDimensional â„ E]
  [NormedAddCommGroup F] [NormedSpace â„ F] [FiniteDimensional â„ F]
  [NormedAddCommGroup G] [NormedSpace â„ G] [FiniteDimensional â„ G]
  {k : â„•} {Î± : I} {s : Set (E Ã— F)} {a : E Ã— F}  {f : E Ã— F â†’ â„}

-- This def almost hits the max heartbeats limit. In fact, I've adjusted the proof to avoid it.
-- Idk what makes the proof so slow.
@[irreducible]
def chartImplicitData (f : E Ã— F â†’ â„) (a : E Ã— F)
    (hfa : ContDiffMoreiraHolderAt k Î± f a) (hk : k â‰  0) (hdf : fderiv â„ f a âˆ˜L .inr â„ E F â‰  0) :
    ImplicitFunctionData â„ (E Ã— F) â„ (E Ã— LinearMap.ker (fderiv â„ f a âˆ˜L .inr â„ E F)) where
  leftFun := f
  leftDeriv := fderiv â„ f a
  left_has_deriv := hfa.contDiffAt.hasStrictFDerivAt <| by
    simpa only [Nat.one_le_cast, Nat.one_le_iff_ne_zero]
  rightFun := _
  rightDeriv := .prodMap (.id _ _) (Submodule.ClosedComplemented.of_finiteDimensional _).choose
  right_has_deriv := ContinuousLinearMap.hasStrictFDerivAt _
  pt := a
  left_range := by
    refine IsSimpleOrder.eq_bot_or_eq_top _ |>.resolve_left ?_
    rw [ContinuousLinearMap.range_eq_bot]
    contrapose! hdf
    rw [hdf, ContinuousLinearMap.zero_comp]
  right_range := by
    have : LinearMap.range (Submodule.ClosedComplemented.of_finiteDimensional <|
        LinearMap.ker (fderiv â„ f a âˆ˜L .inr â„ E F)).choose = âŠ¤ :=
      LinearMap.range_eq_of_proj (Exists.choose_spec (_ : Submodule.ClosedComplemented _))
    rw [ContinuousLinearMap.range_prodMap, this]
    simp
  isCompl_ker := by
    have H : (LinearMap.ker (fderiv â„ f a âˆ˜L .inr â„ E F)).ClosedComplemented :=
      .of_finiteDimensional _
    constructor
    Â· suffices âˆ€ x y, fderiv â„ f a (x, y) = 0 â†’ x = 0 â†’ H.choose y = 0 â†’ y = 0 by
        simpa +contextual only [Subtype.forall, LinearMap.mem_ker, ContinuousLinearMap.coe_comp',
          comp_apply, ContinuousLinearMap.inr_apply, ContinuousLinearMap.ker_prodMap,
          Submodule.disjoint_def, Submodule.mem_prod, ContinuousLinearMap.coe_id', id_eq, and_imp,
          Prod.forall, Prod.mk_eq_zero, true_and]
      rintro _ y hdf rfl hy
      lift y to LinearMap.ker (fderiv â„ f a âˆ˜L .inr â„ E F) using by simp [hdf]
      simpa only [H.choose_spec, ZeroMemClass.coe_eq_zero] using hy
    Â· rw [Submodule.codisjoint_iff_exists_add_eq]
      rintro âŸ¨x, yâŸ©
      obtain âŸ¨z, hzâŸ© : âˆƒ z : F, fderiv â„ f a (x, z) = 0 := by
        have : LinearMap.range (fderiv â„ f a âˆ˜L .inr â„ _ _) = âŠ¤ := by
          refine IsSimpleOrder.eq_bot_or_eq_top _ |>.resolve_left ?_
          rwa [ContinuousLinearMap.range_eq_bot]
        rw [Submodule.eq_top_iff'] at this
        refine this (-fderiv â„ f a (x, 0)) |>.imp fun z hz â†¦ ?_
        rw [â† (x, z).fst_add_snd, map_add]
        simpa [eq_neg_iff_add_eq_zero, add_comm] using hz
      rcases Submodule.codisjoint_iff_exists_add_eq.mp
        (LinearMap.isCompl_of_proj H.choose_spec).codisjoint (y - z)
        with âŸ¨w, t, hw, ht, hsubâŸ©
      refine âŸ¨(x, w + z), (0, t), ?ker, by simpa using ht, ?addâŸ©
      case ker =>
        rwa [â† zero_add x, â† Prod.mk_add_mk, LinearMap.mem_ker, map_add, hz, add_zero]
      case add =>
        rw [Prod.mk_add_mk, add_zero, add_right_comm w z t, hsub, sub_add_cancel]

@[simp]
theorem chartImplicitData_leftFun {f : E Ã— F â†’ â„} {a : E Ã— F}
    (hfa : ContDiffMoreiraHolderAt k Î± f a) (hk : k â‰  0) (hdf : fderiv â„ f a âˆ˜L .inr â„ E F â‰  0) :
    (chartImplicitData f a hfa hk hdf).leftFun = f := by
  simp [chartImplicitData]

@[simp]
theorem chartImplicitData_leftDeriv {f : E Ã— F â†’ â„} {a : E Ã— F}
    (hfa : ContDiffMoreiraHolderAt k Î± f a) (hk : k â‰  0) (hdf : fderiv â„ f a âˆ˜L .inr â„ E F â‰  0) :
    (chartImplicitData f a hfa hk hdf).leftDeriv = fderiv â„ f a := by
  simp [chartImplicitData]

@[simp]
theorem fst_rightFun_chartImplicitData {f : E Ã— F â†’ â„} {a : E Ã— F}
    (hfa : ContDiffMoreiraHolderAt k Î± f a) (hk : k â‰  0) (hdf : fderiv â„ f a âˆ˜L .inr â„ E F â‰  0)
    (x : E Ã— F) : ((chartImplicitData f a hfa hk hdf).rightFun x).1 = x.1 := by
  simp [chartImplicitData]

@[simp]
theorem chartImplicitData_pt {f : E Ã— F â†’ â„} {a : E Ã— F}
    (hfa : ContDiffMoreiraHolderAt k Î± f a) (hk : k â‰  0) (hdf : fderiv â„ f a âˆ˜L .inr â„ E F â‰  0) :
    (chartImplicitData f a hfa hk hdf).pt = a := by
  simp [chartImplicitData]

theorem chartImplicitData_rightDeriv_apply_ker {f : E Ã— F â†’ â„} {a : E Ã— F}
    (hfa : ContDiffMoreiraHolderAt k Î± f a) (hk : k â‰  0) (hdf : fderiv â„ f a âˆ˜L .inr â„ E F â‰  0)
    (x : E) {y : F} (hy : fderiv â„ f a (0, y) = 0) :
    (chartImplicitData f a hfa hk hdf).rightDeriv (x, y) = (x, âŸ¨y, by simpaâŸ©) := by
  simpa [chartImplicitData] using
    Submodule.ClosedComplemented.of_finiteDimensional (LinearMap.ker (fderiv â„ f a âˆ˜L .inr â„ E F))
      |>.choose_spec âŸ¨y, by simpaâŸ©

theorem fderiv_implicitFunction_chartImplicitData_apply_mk_zero {f : E Ã— F â†’ â„} {a : E Ã— F}
    (hfa : ContDiffMoreiraHolderAt k Î± f a) (hk : k â‰  0) (hdf : fderiv â„ f a âˆ˜L .inr â„ E F â‰  0)
    (y : LinearMap.ker ((fderiv â„ f a).comp (ContinuousLinearMap.inr â„ E F))) :
    fderiv â„ ((chartImplicitData f a hfa hk hdf).implicitFunction (f a))
      ((chartImplicitData f a hfa hk hdf).rightFun a) (0, y) = (0, y.1) := by
  convert (chartImplicitData f a hfa hk hdf).fderiv_implicitFunction_apply_eq_iff.mpr _
  Â· simp
  Â· simp
  Â· simp
  Â· constructor
    Â· cases y with | mk y hy => simpa using hy
    Â· apply chartImplicitData_rightDeriv_apply_ker
      cases y with | mk y hy => simpa using hy

@[simp]
theorem fderiv_implicitFunction_chartImplicitData_comp_inr {f : E Ã— F â†’ â„} {a : E Ã— F}
    (hfa : ContDiffMoreiraHolderAt k Î± f a) (hk : k â‰  0) (hdf : fderiv â„ f a âˆ˜L .inr â„ E F â‰  0) :
    fderiv â„ ((chartImplicitData f a hfa hk hdf).implicitFunction (f a))
      ((chartImplicitData f a hfa hk hdf).rightFun a) âˆ˜L .inr â„ E _ =
      .inr â„ E F âˆ˜L Submodule.subtypeL _ := by
  ext1 x
  simp [fderiv_implicitFunction_chartImplicitData_apply_mk_zero]

theorem fst_implicitFunction_chartImplicitData_eventuallyEq {f : E Ã— F â†’ â„} {a : E Ã— F}
    (hfa : ContDiffMoreiraHolderAt k Î± f a) (hk : k â‰  0) (hdf : fderiv â„ f a âˆ˜L .inr â„ E F â‰  0) :
    Prod.fst âˆ˜ (chartImplicitData f a hfa hk hdf).implicitFunction (f a)
      =á¶ [ğ“ ((chartImplicitData f a hfa hk hdf).rightFun a)] Prod.fst := by
  have := (continuousAt_const.prodMk continuousAt_id).eventually
    (chartImplicitData f a hfa hk hdf).right_map_implicitFunction
  rw [chartImplicitData_pt] at this
  filter_upwards [this] with x hx
  simpa using congr($hx |>.1)

theorem map_implicitFunction_chartImplicitData_nhdsWithin_preimage {f : E Ã— F â†’ â„} {a : E Ã— F}
    (hfa : ContDiffMoreiraHolderAt k Î± f a) (hk : k â‰  0) (hdf : fderiv â„ f a âˆ˜L .inr â„ E F â‰  0)
    (s : Set (E Ã— F)) (hfs : f =á¶ [ğ“[s] a] 0) (ha : a âˆˆ s) :
    letI Ïˆ := chartImplicitData f a hfa hk hdf
    (ğ“[Ïˆ.implicitFunction 0 â»Â¹' s] (Ïˆ.rightFun a)).map (Ïˆ.implicitFunction 0) = ğ“[s] a := by
  set Ïˆ := chartImplicitData f a hfa hk hdf
  convert Ïˆ.map_implicitFunction_nhdsWithin_preimage s using 1
  Â· simp [Ïˆ, hfs.self_of_nhdsWithin ha]
  Â· rw [nhdsWithin_inter', inf_of_le_left]
    Â· congr 1
      simp [Ïˆ]
    Â· rw [le_principal_iff, chartImplicitData_pt]
      filter_upwards [hfs] with x hx
      simp [Ïˆ, hx, hfs.self_of_nhdsWithin ha]

def IsLargeAt (k : â„•) (Î± : I) (s : Set (E Ã— F)) (a : E Ã— F) : Prop :=
  âˆ€ f : E Ã— F â†’ â„, (âˆ€á¶  x in ğ“[s] a, ContDiffMoreiraHolderAt k Î± f x) â†’ f =á¶ [ğ“[s] a] 0 â†’
    fderiv â„ f a âˆ˜L .inr â„ E F = 0

omit [FiniteDimensional â„ E] [FiniteDimensional â„ F] [FiniteDimensional â„ G] in
theorem IsLargeAt.comp_continuousLinearEquiv (h : IsLargeAt k Î± s a) (e : G â‰ƒL[â„] F) :
    IsLargeAt k Î± (Prod.map id e â»Â¹' s) (Prod.map id e.symm a) := by
  intro f hfk hfâ‚€
  set e' := (ContinuousLinearEquiv.refl â„ E).prodCongr e
  specialize h (f âˆ˜ e'.symm) ?_ ?_
  Â· rw [â† e'.apply_symm_apply a, â† e'.map_nhdsWithin_preimage_eq, eventually_map]
    filter_upwards [hfk] with x hfx
    rw [â† e'.symm_apply_apply x] at hfx
    exact hfx.comp' e'.symm.contDiffMoreiraHolderAt (.inr e'.symm.differentiableAt)
  Â· rw [â† e'.apply_symm_apply a, â† e'.map_nhdsWithin_preimage_eq, eventuallyEq_map]
    filter_upwards [hfâ‚€]
    simp
  Â· rw [e'.symm.comp_right_fderiv] at h
    simpa [DFunLike.ext_iff, e.symm.surjective.forall, e'] using h

structure ChartStep (k : â„•) (Î± : I) (s : Set (E Ã— F)) (a : E Ã— F) (ha : a âˆˆ s) where
  dom : Submodule â„ F
  pt : E Ã— dom
  toFun : E Ã— dom â†’ E Ã— F
  apply_pt : toFun pt = a
  fst_comp_toFun_eventuallyEq : Prod.fst âˆ˜ toFun =á¶ [ğ“ pt] Prod.fst
  contDiffMoreiraHolderAt : âˆ€á¶  x in ğ“[toFun â»Â¹' s] pt, ContDiffMoreiraHolderAt k Î± toFun x
  map_toFun_nhdsWithin : (ğ“[toFun â»Â¹' s] pt).map toFun = ğ“[s] (toFun pt)
  snd_comp_fderiv_comp_inr : .snd â„ E F âˆ˜L fderiv â„ toFun pt âˆ˜L .inr â„ E dom = dom.subtypeL

namespace ChartStep

attribute [coe] toFun
attribute [simp] apply_pt

instance (ha : a âˆˆ s) : CoeFun (ChartStep k Î± s a ha) fun Ïˆ â†¦ E Ã— Ïˆ.dom â†’ E Ã— F where
  coe := toFun

omit [FiniteDimensional â„ E] [FiniteDimensional â„ F] in
theorem pt_mem_preimage {ha : a âˆˆ s} (f : ChartStep k Î± s a ha) : f.pt âˆˆ f â»Â¹' s := by
  simpa

omit [FiniteDimensional â„ E] [FiniteDimensional â„ F] in
theorem contDiffMoreiraHolderAt_pt {ha} (f : ChartStep k Î± s a ha) :
    ContDiffMoreiraHolderAt k Î± f f.pt :=
  f.contDiffMoreiraHolderAt.self_of_nhdsWithin f.pt_mem_preimage

omit [FiniteDimensional â„ E] [FiniteDimensional â„ F] in
theorem continuousAt_pt {ha} (f : ChartStep k Î± s a ha) :
    ContinuousAt f f.pt :=
  f.contDiffMoreiraHolderAt_pt.continuousAt

omit [FiniteDimensional â„ E] [FiniteDimensional â„ F] in
theorem differentiableAt_pt {ha} (f : ChartStep k Î± s a ha) (hk : k â‰  0) :
    DifferentiableAt â„ f f.pt :=
  f.contDiffMoreiraHolderAt_pt.differentiableAt hk

omit [FiniteDimensional â„ E] [FiniteDimensional â„ F] in
@[simp]
theorem fst_fderiv_apply {ha} (f : ChartStep k Î± s a ha) (hk : k â‰  0) (x : E Ã— f.dom) :
    (fderiv â„ f f.pt x).fst = x.fst := by
  simpa [fderiv_comp, f.differentiableAt_pt hk, fderiv_fst]
    using congr($(f.fst_comp_toFun_eventuallyEq.fderiv_eq (ğ•œ := â„)) x)

omit [FiniteDimensional â„ E] [FiniteDimensional â„ F] in
@[simp]
theorem fderiv_mk_zero {ha} (f : ChartStep k Î± s a ha) (x : f.dom) :
    fderiv â„ f f.pt (0, x) = (0, x.1) := by
  ext
  Â· by_cases hdf : DifferentiableAt â„ f f.pt
    Â· simpa [fderiv_comp, hdf, fderiv_fst]
        using congr($(f.fst_comp_toFun_eventuallyEq.fderiv_eq (ğ•œ := â„)) (0, x))
    Â· simp [fderiv_zero_of_not_differentiableAt hdf]
  Â· simpa using congr($(f.snd_comp_fderiv_comp_inr) x)

def top (a : E Ã— F) (ha : a âˆˆ s) : ChartStep k Î± s a ha where
  dom := âŠ¤
  pt := (a.1, âŸ¨a.2, by simpâŸ©)
  toFun := ContinuousLinearEquiv.prodCongr (.refl â„ E) Submodule.topContinuousEquiv
  apply_pt := rfl
  fst_comp_toFun_eventuallyEq := .rfl
  contDiffMoreiraHolderAt := .of_forall fun _ â†¦ ContinuousLinearEquiv.contDiffMoreiraHolderAt _
  map_toFun_nhdsWithin := by
    rw [â† ContinuousLinearEquiv.coe_toHomeomorph, (Homeomorph.isInducing _).map_nhdsWithin_eq,
      Homeomorph.image_preimage]
  snd_comp_fderiv_comp_inr := by
    rw [ContinuousLinearEquiv.fderiv]
    ext
    simp

private theorem kerOfCompInrNeZero_auxâ‚
    (hfk : âˆ€á¶  x in ğ“[s] a, ContDiffMoreiraHolderAt k Î± f x) (hfâ‚€ : f =á¶ [ğ“[s] a] 0)
    (hk : k â‰  0) (has : a âˆˆ s) (hdf : fderiv â„ f a âˆ˜L .inr â„ E F â‰  0) :
    (chartImplicitData f a (hfk.self_of_nhdsWithin has) hk hdf).implicitFunction 0
      ((chartImplicitData f a (hfk.self_of_nhdsWithin has) hk hdf).rightFun a) = a := by
  simpa [hfâ‚€.self_of_nhdsWithin has]
    using chartImplicitData f a (hfk.self_of_nhdsWithin has) hk hdf
      |>.implicitFunction_apply_image.self_of_nhds

def kerOfCompInrNeZero (k : â„•) (Î± : I) (s : Set (E Ã— F)) (a : E Ã— F) (f : E Ã— F â†’ â„)
    (hfk : âˆ€á¶  x in ğ“[s] a, ContDiffMoreiraHolderAt k Î± f x) (hfâ‚€ : f =á¶ [ğ“[s] a] 0)
    (hk : k â‰  0) (has : a âˆˆ s) (hdf : fderiv â„ f a âˆ˜L .inr â„ E F â‰  0) :
    ChartStep k Î± s a has where
  dom := LinearMap.ker (fderiv â„ f a âˆ˜L .inr â„ E F)
  pt := (chartImplicitData f a (hfk.self_of_nhdsWithin has) hk hdf).rightFun a
  toFun := (chartImplicitData f a (hfk.self_of_nhdsWithin has) hk hdf).implicitFunction 0
  apply_pt := kerOfCompInrNeZero_auxâ‚ hfk hfâ‚€ _ has _
  fst_comp_toFun_eventuallyEq := by
    simpa [hfâ‚€.self_of_nhdsWithin has]
      using fst_implicitFunction_chartImplicitData_eventuallyEq (hfk.self_of_nhdsWithin has) hk hdf
  contDiffMoreiraHolderAt := by
    have := map_implicitFunction_chartImplicitData_nhdsWithin_preimage (hfk.self_of_nhdsWithin has)
      hk hdf s hfâ‚€ has |>.le
    set Ïˆ := chartImplicitData f a (hfk.self_of_nhdsWithin has) hk hdf
    set g := Ïˆ.implicitFunction 0
    have Hâ‚€ := map_implicitFunction_chartImplicitData_nhdsWithin_preimage
      (hfk.self_of_nhdsWithin has) hk hdf s hfâ‚€ has
    have Hâ‚ : âˆ€á¶  x in ğ“[g â»Â¹' s] Ïˆ.rightFun a, (0, x) âˆˆ Ïˆ.toOpenPartialHomeomorph.target := by
      refine .filter_mono inf_le_left ?_
      refine Ïˆ.toOpenPartialHomeomorph.open_target.preimage (by fun_prop) |>.mem_nhds ?_
      simpa [Ïˆ, hfâ‚€.self_of_nhdsWithin has] using Ïˆ.map_pt_mem_toOpenPartialHomeomorph_target
    have Hâ‚‚ : âˆ€á¶  x in ğ“[s] a, (fderiv â„ Ïˆ.toOpenPartialHomeomorph x).IsInvertible := by
      refine .filter_mono inf_le_left ?_
      apply ContDiffAt.eventually_isInvertible_fderiv _ _ (n := k) (mod_cast hk)
      Â· have := hfk.self_of_nhdsWithin has |>.contDiffAt
        simp +unfoldPartialApp [ImplicitFunctionData.prodFun, Ïˆ, chartImplicitData]
        fun_prop
      Â· have := Ïˆ.hasStrictFDerivAt.hasFDerivAt.fderiv
        rw [chartImplicitData_pt] at this
        rw [Ïˆ.toOpenPartialHomeomorph_coe, this]
        exact ContinuousLinearMap.isInvertible_equiv
    rw [â† Hâ‚€, eventually_map] at hfk Hâ‚‚
    filter_upwards [Hâ‚, hfk, Hâ‚‚] with x hxâ‚ hxâ‚‚ hxâ‚ƒ
    refine (Ïˆ.toOpenPartialHomeomorph.contDiffMoreiraHolderAt_symm hxâ‚ hxâ‚ƒ ?_).comp
      (ContDiffAt.contDiffMoreiraHolderAt (by fun_prop) (WithTop.coe_lt_top _) Î±) hk
    refine .prodMk (by simpa [Ïˆ] using hxâ‚‚) ?_
    simp only [Ïˆ, chartImplicitData]
    exact ContDiffAt.contDiffMoreiraHolderAt (by fun_prop) (WithTop.coe_lt_top _) Î±
  map_toFun_nhdsWithin := by
    set Ïˆ := chartImplicitData f a (hfk.self_of_nhdsWithin has) hk hdf
    rw [map_implicitFunction_chartImplicitData_nhdsWithin_preimage, kerOfCompInrNeZero_auxâ‚]
    all_goals assumption
  snd_comp_fderiv_comp_inr := by
    have : f a = 0 := hfâ‚€.self_of_nhdsWithin has
    rw [â† this, fderiv_implicitFunction_chartImplicitData_comp_inr,
      â† ContinuousLinearMap.comp_assoc, ContinuousLinearMap.snd_comp_inr,
      ContinuousLinearMap.id_comp]

@[simp]
theorem kerOfCompInrNeZero_apply_pt
    (hfk : âˆ€á¶  x in ğ“[s] a, ContDiffMoreiraHolderAt k Î± f x) (hfâ‚€ : f =á¶ [ğ“[s] a] 0)
    (hk : k â‰  0) (has : a âˆˆ s) (hdf : fderiv â„ f a âˆ˜L .inr â„ E F â‰  0) :
    kerOfCompInrNeZero k Î± s a f hfk hfâ‚€ hk has hdf
      (kerOfCompInrNeZero k Î± s a f hfk hfâ‚€ hk has hdf).pt = a := by
  apply kerOfCompInrNeZero_auxâ‚ <;> assumption

protected def comp {ha} (g : ChartStep k Î± s a ha)
    (f : ChartStep k Î± (g â»Â¹' s) g.pt g.pt_mem_preimage) (hk : k â‰  0) :
    ChartStep k Î± s a ha := by
  set e := (ContinuousLinearEquiv.refl â„ E).prodCongr (g.dom.continuousEquivSubtypeMap f.dom)
  have heâ‚ : âˆ€ x, (e x).fst = x.fst := fun _ â†¦ rfl
  refine
    { dom := f.dom.map g.dom.subtype
      pt := e f.pt
      toFun := g âˆ˜ f âˆ˜ e.symm
      apply_pt := ?_
      fst_comp_toFun_eventuallyEq := ?_
      contDiffMoreiraHolderAt := ?_
      map_toFun_nhdsWithin := ?_
      snd_comp_fderiv_comp_inr := ?_ }
  Â· simp
  Â· rw [â† e.map_nhds_eq, eventuallyEq_map]
    have hg : Prod.fst âˆ˜ g âˆ˜ f =á¶ [ğ“ f.pt] Prod.fst âˆ˜ f := by
      have := g.fst_comp_toFun_eventuallyEq
      rw [â† f.apply_pt] at this
      exact f.continuousAt_pt.eventually this
    filter_upwards [f.fst_comp_toFun_eventuallyEq, hg] with x hfx hgx
    simp_all
  Â· simp only [â† e.map_nhdsWithin_preimage_eq, eventually_map]
    have hg := g.contDiffMoreiraHolderAt
    rw [â† f.apply_pt] at hg
    filter_upwards [f.contDiffMoreiraHolderAt,
      (f.continuousAt_pt.continuousWithinAt.tendsto_nhdsWithin <| by simp [MapsTo]).eventually hg]
      with x hfx hgx
    rw [â† e.symm_apply_apply x] at hfx hgx
    exact hgx.comp hfx hk |>.comp e.symm.contDiffMoreiraHolderAt hk
  Â· rw [â† Filter.map_map, â† Filter.map_map, e.symm.map_nhdsWithin_eq, preimage_comp, preimage_comp,
      e.symm.surjective.image_preimage, e.symm_apply_apply, f.map_toFun_nhdsWithin, f.apply_pt,
      g.map_toFun_nhdsWithin]
    simp
  Â· ext x
    rw [fderiv_comp, ContinuousLinearEquiv.comp_right_fderiv]
    Â· have : e.symm (0, x) = (0, (g.dom.continuousEquivSubtypeMap f.dom).symm x) := rfl
      simp [this]
    Â· simpa using g.differentiableAt_pt hk
    Â· exact f.differentiableAt_pt hk |>.comp _ e.symm.differentiableAt

theorem exists_isLargeAt {k : â„•} (Î± : I) {s : Set (E Ã— F)} {a : E Ã— F} (hk : k â‰  0) (ha : a âˆˆ s) :
    âˆƒ f : ChartStep k Î± s a ha, IsLargeAt k Î± (f â»Â¹' s) f.pt := by
  induction hF : dim F using Nat.strongRecOn generalizing F with | ind d ihd => ?_
  by_cases hlarge : IsLargeAt k Î± s a
  Â· exact âŸ¨.top a ha, hlarge.comp_continuousLinearEquiv Submodule.topContinuousEquivâŸ©
  Â· rw [IsLargeAt] at hlarge
    push_neg at hlarge
    rcases hlarge with âŸ¨f, hfk, hfâ‚€, hdfâŸ©
    set Ïˆ := kerOfCompInrNeZero k Î± s a f hfk hfâ‚€ hk ha hdf
    have : dim Ïˆ.dom < d := by
      simpa only [Ïˆ, kerOfCompInrNeZero, â† hF,
        â† (fderiv â„ f a âˆ˜L .inr â„ E F).finrank_range_add_finrank_ker, lt_add_iff_pos_left,
        pos_iff_ne_zero, ne_eq, Submodule.finrank_eq_zero, ContinuousLinearMap.range_eq_bot]
    rcases ihd (dim Ïˆ.dom) this Ïˆ.pt_mem_preimage rfl with âŸ¨g, hg_largeâŸ©
    use Ïˆ.comp g hk
    exact hg_large.comp_continuousLinearEquiv (Ïˆ.dom.continuousEquivSubtypeMap g.dom).symm

def ofLE {ha} (Ïˆ : ChartStep k Î± s a ha) (l : â„•) (hl : l â‰¤ k) : ChartStep l Î± s a ha where
  __ := Ïˆ
  contDiffMoreiraHolderAt := Ïˆ.contDiffMoreiraHolderAt.mono fun _x hx â†¦ hx.of_le hl

theorem isBigO_sub_rev {ha} (Ïˆ : ChartStep k Î± s a ha) (hk : k â‰  0) :
    (fun x â†¦ x.1 - x.2) =O[ğ“ (Ïˆ.pt, Ïˆ.pt)] (fun x â†¦ Ïˆ x.1 - Ïˆ x.2) := by
  set Ïˆ' := fderiv â„ Ïˆ Ïˆ.pt
  suffices Injective Ïˆ' by
    rcases Ïˆ'.antilipschitz_of_injective_of_isClosed_range this
      (LinearMap.coe_range Ïˆ' â–¸ Submodule.closed_of_finiteDimensional _) with âŸ¨C, hCâŸ©
    have : (fun x â†¦ x.1 - x.2) =O[ğ“ (Ïˆ.pt, Ïˆ.pt)] (fun x â†¦ Ïˆ' (x.1 - x.2)) := by
      refine .of_bound C <| .of_forall fun x â†¦ ?_
      convert ZeroHomClass.bound_of_antilipschitz Ïˆ' hC (x.1 - x.2)
    refine this.trans ?_
    refine Ïˆ.contDiffMoreiraHolderAt_pt.contDiffAt.hasStrictFDerivAt
      (by simpa [Nat.one_le_iff_ne_zero])
      |>.isLittleO |>.trans_isBigO this |>.right_isBigO_add |>.congr (fun _ â†¦ rfl) ?_
    simp [Ïˆ']
  rw [injective_iff_map_eq_zero]
  rintro âŸ¨x, yâŸ© h
  obtain rfl : x = 0 := by simpa [Ïˆ', hk] using congr(Prod.fst $h)
  simpa [Ïˆ'] using h

theorem isBigO_sub_rev_of_tendsto {Î² : Type*} {l : Filter Î²} {ha} (Ïˆ : ChartStep k Î± s a ha)
    (hk : k â‰  0) {f g : Î² â†’ E Ã— Ïˆ.dom} (hf : Tendsto f l (ğ“ Ïˆ.pt)) (hg : Tendsto g l (ğ“ Ïˆ.pt)) :
    (fun x â†¦ f x - g x) =O[l] (fun x â†¦ Ïˆ (f x) - Ïˆ (g x)) := by
  exact Ïˆ.isBigO_sub_rev hk |>.comp_tendsto (hf.prodMk_nhds hg)

end ChartStep

def chartChain {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F] [FiniteDimensional â„ F]
    (k : â„•) (hk : k â‰  0) (Î± : I) (s : Set (E Ã— F)) (a : E Ã— F) (ha : a âˆˆ s) :
    ChartStep 1 Î± s a ha :=
  match k, hk with
  | 1, _ =>
    (ChartStep.exists_isLargeAt Î± one_ne_zero ha).choose
  | l + 2, hl =>
    have H := ChartStep.exists_isLargeAt Î± hl ha
    let Ïˆ := H.choose
    let Ï† := chartChain (l + 1) l.succ_ne_zero Î± (Ïˆ â»Â¹' s) Ïˆ.pt Ïˆ.pt_mem_preimage
    (Ïˆ.ofLE _ (by simp)).comp Ï† one_ne_zero
